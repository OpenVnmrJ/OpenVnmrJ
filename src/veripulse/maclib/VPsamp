"macro VPsamp"
"Agilent VPsampinfo - supports sample popup for Veripulse"


exists(VPsamplefile,'veripulse'):$ex,$spath
if ($ex<0.5) then
  write('error','Sample list file does not exist!')
  abort
endif
if ($# <> 1) then
  return
endif

if ($1='popup') then
  $loclistfile=userdir+'/persistence/VPloclist'
  $rewrite=1
  if (traymax=97) then "actually 96 locations for 7620" $traymax=96 else $traymax=traymax endif
  exists($loclistfile,'file'):$ex
  if ($ex>0.5) then "file exists, but is it consistent with current traymax?"
    lookup('mfile',$loclistfile,'countline'):$numlocs
    if ($numlocs=$traymax) then $rewrite=0  endif
  endif
  if ($rewrite) then
    write('reset',$loclistfile)
    if ($traymax<9) then
      write('file',$loclistfile,'NA 0')
    else
      $i=1
      repeat
        write('file',$loclistfile,'%d %d',$i,$i)
        $i=$i+1
      until ($i>$traymax)
    endif
  endif
  destroy('VPsampEdit'):$e
  create('VPsampEdit','string')
  setgroup('VPsampEdit','display')
  VPsampEdit='n'

  vnmrjcmd('popup','mode:modeless','file:VPSampleInfo.xml','rebuild:yes','location:expViewArea','pnewupdate:true',`close:`+$0+`('close')`,'title:VeriPulse Sample Information')
endif

if ($1='close') then
  if (VPsampEdit <> 'n') then
    exists(userdir+'/veripulse','directory'):$e
    if (not $e) then
      mkdir(userdir+'/veripulse')
    endif
    $spath=userdir+'/veripulse/'+VPsamplefile
    write('reset',$spath)
    write('file',$spath,'# SampleID Solvent PartNumber Location ;Description\n')
    $num=size('VPsampID')
    $i=0
    while ($i < $num) do
      $i=$i+1
      write('file',$spath,'%-11s %-7s %11s %3s ;%-30s',VPsampID[$i],VPsolvent[$i],VPpn[$i],VPloc[$i],VPsampLabel[$i])
    endwhile
  endif
  destroy('VPsampEdit'):$e
  VPtest('readsamp')
endif

