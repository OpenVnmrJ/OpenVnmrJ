"macro autodirstatus"
exists(userdir+'/persistence/'+$0,'directory'):$adirex
if ($adirex=0) then
   shell('mkdir -p '+userdir+'/persistence/'+$0):$dum
endif

IF ($# < 2) THEN

   if ($# < 1) then
	$1='' 
   endif
   if ($1='') or ($1='refresh') then
       strstr(autodir,'studytray'):$istray
       if ($istray) then
	   $arg1='enter'
       else
	   $arg1='auto'
       endif
   else
	$arg1=$1
   endif

   $Auto='n'
   $subautodir=''
   if ($arg1='auto') then
   	$efile=autodir+'/enterQ'
   	$Auto='y'
   	shell('basename '+autodir):$subautodir
	$dir=autodir
   	$filetype='autodir'
   elseif ($arg1='enter') then
   	shell('basename '+globalenter):$subautodir
   	$efile=globalenter+'/'+$subautodir
	$dir=globalenter
   	$filetype='autofile'
   else
	$dir=$arg1
	exists($arg1+'/enterQ','file'):$eqex
	if ($eqex) then
	   $efile=$arg1+'/enterQ'
	   $Auto='y'
	   shell('basename '+$arg1):$subautodir
	   $filetype='autodir'
	else
	   shell('basename '+$arg1):$subautodir
	   $efile=$arg1+'/'+$subautodir
	   $filetype='autofile'
	endif
   endif
   if ($dir='') then return endif
   exists($dir,'directory'):$direx
   if ($direx=0) then return endif

   if (cpautodir=autodir) and ($Auto='y') then $curauto=1 else $curauto=0 endif

   exists('autodirfidname','parameter','global'):$parex
   if ($parex=0) then
    	create('autodirfidname','string','global')
   endif
   setprotect('autodirfidname','on',256,'global')
   $fidlog=userdir+'/persistence/'+$0+'/autodirfidlog'
   write('reset',$fidlog)
   if ($Auto='y') then
	autodirfidname[1]=$subautodir
    	autodirfidname[2]='','0'

	$macro='' $curloc='' $rack='' $zone='' $rack2='' $zone2=''
	shell('(cat '+$efile+'.macdir/currentsampleinfo | grep -w MACRO:)'):$macro
	shell('(cat '+$efile+'.macdir/currentsampleinfo | grep -w SAMPLE#:)'):$curloc
	shell('(cat '+$efile+'.macdir/currentsampleinfo | grep -w RACK:)'):$rack2
        shell('(cat '+$efile+'.macdir/currentsampleinfo | grep -w ZONE:)'):$zone2
	substr($rack2,2):$rack
	substr($zone2,2):$zone

	if ($macro<>'') and ($curloc<>'') then
	    substr($curloc,2):$rcurloc
	    $sloc='' format($rcurloc,0,0):$sloc
	    if ($rcurloc<100) then $sloc='0'+$sloc endif
	    if ($rcurloc<10) then $sloc='0'+$sloc endif
	    $s1='' $s2='' $clocdir=''
	    strstr($macro,'\''):$ret,$s1,$s2
	    if ($ret) then
		strstr($s2,'\''):$ret,$clocdir
	    endif
	    if ($ret) then
		exists($efile+'.macdir/'+$clocdir+'/current.fid/procpar','file'):$fileex
		$explist='' $curname=''
		if ($fileex) then
		   rtv($efile+'.macdir/'+$clocdir+'/current.fid/procpar','noabort','explist'):$explist
		   rtv($efile+'.macdir/'+$clocdir+'/current.fid/procpar','noabort','samplename'):$curname
		   
		   if ($explist='') then $fileex=0 endif
		endif
		if ($fileex) then
		   if ($rack<>'') and ($zone<>'') then
			write('fileline',$fidlog,'"Loc%sz%sr%s',$sloc,$zone,$rack)
		   else
		   	write('fileline',$fidlog,'"Loc%s',$sloc)
		   endif
		   write('fileline',$fidlog,'(%s)',$curname)
		   write('fileline',$fidlog,'%s (Active)" ',$explist)
		   write('fileline',$fidlog,'"%s.macdir/%s/current.fid"',$efile,$clocdir)
		   write('fileline',$fidlog,'\n')
		endif
	    endif
	endif

    	exists($efile+'.macdir/fidlog','file'):$fileex
    	if ($fileex) then
//	   shell('(sort -k 1,1 '+$efile+'.macdir/fidlog -o '+$fidlog+'1)'):$dum
		"Let us list them in the order they were collected"
	   shell('cp '+$efile+'.macdir/fidlog '+$fidlog+'1'):$dum
	   shell('cat '+$fidlog+'1 >> '+$fidlog):$dum
	   shell('rm -f '+$fidlog+'1'):$dum
//           shell('cat '+$efile+'.macdir/fidlog >> '+$fidlog):$dum
    	endif
        $count=0
        shell('(cat '+$fidlog+' | wc -l)'):$count
        format($count,0,0):autodirfidname[3]
   else
	autodirfidname='','','0'
   endif
   write('file',$fidlog,'"" ""')
   if (autodirfidname[3]='') then autodirfidname[3]='0' endif
   vnmrjcmd('pnew','autodirfidname')

   $firstchar=''
   substr($efile,1,1):$firstchar
   if ($firstchar<>'/') then
   	$efile=cpglobalauto+'/'+$efile
   endif

   $infofile=userdir+'/persistence/'+$0+'/autodir_queue'
   write('reset',$infofile)

   $ST=0 $DT=0 $NT=0 $CT=0
   $DST='' $SST='' $NST=''
   if ($curauto>0) then
	findstarttime($efile):$ST,$DT,$NT,$CT
	sec2ampm($ST+$CT):$SST
	sec2ampm($DT+$CT):$DST
	sec2ampm($NT+$CT):$NST
   else
	readTtimeQ($efile+'.macdir'):$ST,$DT,$NT
        if ($ST<1) then $SST='0 min' else
          formattime($ST):$SST
        endif
        if ($DT-$ST < 1) then $DST='0 min' else
          formattime($DT-$ST):$DST
        endif
        if ($NT<1) then $NST='0 min' else
          formattime($NT):$NST
        endif
   endif
   $nextloc=1
   nextlocQ($efile):$nextloc

   $runstart=''
   if ($Auto='y') then
  	shell('(cat '+$efile+'.macdir/DATE_runstart | tail -n 1)'):$runstart
   else
  	shell('(cat '+$efile+'.macdir/DATE_creation | tail -n 1)'):$runstart
   endif
   $s1='' $s2=''
   strstr($runstart,'_','last'):$ret,$s1,$s2
   if ($ret) then $runstart=$s1 endif
   $prioritytotal=0 $daytotal=0 $nighttotal=0 $active=''
   shell('(cat '+$efile+' | grep -w USER: | grep -cw priority)'):$prioritytotal
   shell('(cat '+$efile+' | grep -cw SAMPLE#:)'):$daytotal
   $daytotal=$daytotal-$prioritytotal
   shell('(cat '+$efile+'.macdir/overniteQ | grep -cw SAMPLE#:)'):$nighttotal
   shell('(cat '+$efile+'.macdir/currentsampleinfo | grep -w SAMPLE#:)'):$active
   $activeloc=''
   substr($active,2):$activeloc

        write('file',$infofile,'**%s created on %s**',$filetype,$runstart)
   if ($Auto='y') and ($curauto=0) then
	write('file',$infofile,'**NOTE:  This is NOT current automation directory**')
   endif
        write('file',$infofile,'Next Available Location: %d',$nextloc)
   if ($curauto>0) then
        write('file',$infofile,'SAMPLE# %s in CurrentQ',$activeloc)
   endif
	write('file',$infofile,'')
	write('file',$infofile,'Samples in %s',$subautodir)
        write('file',$infofile,'  %3.0f in PriorityQ',$prioritytotal)
        write('file',$infofile,'  %3.0f in DayQ',$daytotal)
        write('file',$infofile,'  %3.0f in NightQ',$nighttotal)

   if ($curauto>0) then
        write('file',$infofile,'')
        write('file',$infofile,'Next Submission will start around...')
        write('file',$infofile,'  PriorityQ : %s',$SST)
        write('file',$infofile,'       DayQ : %s',$DST)
        write('file',$infofile,'     NightQ : %s',$NST)
   else
	write('file',$infofile,'')
	write('file',$infofile,'Approx. Total time for samples in...')
        write('file',$infofile,'  PriorityQ : %s',$SST)
        write('file',$infofile,'       DayQ : %s',$DST)
        write('file',$infofile,'     NightQ : %s',$NST)
   endif

   write('file',$infofile,'')
   write('file',$infofile,'Current Queue')
   write('file',$infofile,'---------------------------------------------')

//   autodir=autodir
//   vnmrjcmd('popup','mode:modeless','file:autodirstat.xml','rebuild:yes','location:controlPanel','title:'+$subautodir)

   $tmpfile=userdir+'/persistence/'+$0+'/autodir_temp'
   write('reset',$tmpfile)
   write('file',$tmpfile,'SAMPLE#:')
   write('file',$tmpfile,'RACK:')
   write('file',$tmpfile,'ZONE:')
   write('file',$tmpfile,'USER:')
   write('file',$tmpfile,'STATUS:')
   write('file',$tmpfile,'----------')

   $doneQ=$efile+'.macdir/cpdoneQ'
   $daydoneQ=$efile+'.macdir/cpdonedayQ'
   $nightQ=$efile+'.macdir/overniteQ'
   $activeQ=$efile+'.macdir/currentsampleinfo'
   $dayQ=$efile

   exists($doneQ,'file'):$fileex
   if ($fileex) then
	$s1='' $s2='' $total=0
	readfile($doneQ,'$s1','$s2','SAMPLE#','local'):$total
	if ($total) then
	   $i=1
	   repeat
		$loc='' $user='USER: unknown' $locdir=''
		substr($s2[$i],1):$loc
                substr($s2[$i],3):$locdir
		shell('(cat '+$efile+'.macdir/'+$locdir+'/day/sampleinfo | grep -w USER:)'):$user
		write('file',$infofile,'SAMPLE#: %s',$loc)
		write('file',$infofile,'%s',$user)
		write('file',$infofile,'STATUS: Completed')
		write('file',$infofile,'---------------------------------------------')
		$i=$i+1
	   until $i > $total
	endif
   endif

   exists($daydoneQ,'file'):$fileex
   if ($fileex) then
        $s1='' $s2='' $total=0
        readfile($daydoneQ,'$s1','$s2','SAMPLE#','local'):$total
        if ($total) then
           $i=1
           repeat
                $loc='' $user='USER: unknown' $locdir=''
                substr($s2[$i],1):$loc
                substr($s2[$i],3):$locdir
                shell('(cat '+$efile+'.macdir/'+$locdir+'/day/sampleinfo | grep -w USER:)'):$user
                write('file',$infofile,'SAMPLE#: %s',$loc)
                write('file',$infofile,'%s',$user)
                write('file',$infofile,'STATUS: DayQdone')
                write('file',$infofile,'---------------------------------------------')
                $i=$i+1
           until $i > $total
        endif
   endif

   exists($activeQ,'file'):$fileex
   if ($fileex) then
      if ($curauto>0) then
	shell('(cat '+$activeQ+' | fgrep -f '+$tmpfile+' | sed -e \'s|STATUS:.*|STATUS: Active|g\' >> '+$infofile+')'):$dum
      else
        shell('(cat '+$activeQ+' | fgrep -f '+$tmpfile+' | sed -e \'s|STATUS:.*|STATUS: Incomplete|g\' >> '+$infofile+')'):$dum
      endif
   endif
   if ($curauto>0) then
           $ret1='' $ret2=0 $ret3='' $ret4=0 $tottime=0,0
           estimatestart('init'):$ret1,$ret2,$ret3,$ret4,$tottime
   endif
   exists($dayQ,'file'):$fileex
   if ($fileex) then
     if ($curauto>0) then
	$s1='' $s2=''
	readfile($dayQ,'$s1','$s2','SAMPLE#','local'):$total
	if ($total>0) then
	   $s3='' $s4='' $s5='' $s6=''
	   readfile($dayQ,'$s1','$s3','USER:','local')
	   readfile($dayQ,'$s1','$s4','RACK:','local')
	   readfile($dayQ,'$s1','$s5','ZONE:','local')
	   readfile($dayQ,'$s1','$s6','MACRO:','local')
	   $i=1
	   repeat
		$when='' $ST=-1 $CT=-1
	 	substr($s6[$i],1):$macro
		estimatestart('get',$macro,'enterQ',$ret1,$ret2,$tottime):$ST,$CT
		sec2ampm($ST+$CT):$when
		write('file',$infofile,'SAMPLE#:  %s',$s2[$i])
	      if (traymax=96) or (traymax=768) then
		write('file',$infofile,'RACK:  %s',$s4[$i])
		write('file',$infofile,'ZONE:  %s',$s5[$i])
	      endif
		write('file',$infofile,'USER:  %s',$s3[$i])
		write('file',$infofile,'STATUS:  Queued')
		write('file',$infofile,'START_TIME(est):  %s',$when)
                write('file',$infofile,'---------------------------------------------')
		$i=$i+1
	   until $i > $total
	endif
     else
        shell('(cat '+$dayQ+' | fgrep -f '+$tmpfile+' >> '+$infofile+')'):$dum
     endif
   endif

   exists($nightQ,'file'):$fileex
   if ($fileex) then
     if ($curauto>0) then
        $s1='' $s2=''
        readfile($nightQ,'$s1','$s2','SAMPLE#','local'):$total
        if ($total>0) then
           $s3='' $s4='' $s5='' $s6=''
           readfile($nightQ,'$s1','$s3','USER:','local')
           readfile($nightQ,'$s1','$s4','RACK:','local')
           readfile($nightQ,'$s1','$s5','ZONE:','local')
           readfile($nightQ,'$s1','$s6','MACRO:','local')
           $i=1
           repeat
                $when='' $ST=-1 $CT=-1
                substr($s6[$i],1):$macro
                estimatestart('get',$macro,'overniteQ',$ret3,$ret4,$tottime):$ST,$CT
		sec2ampm($ST+$CT):$when
                write('file',$infofile,'SAMPLE#:  %s',$s2[$i])
              if (traymax=96) or (traymax=768) then
                write('file',$infofile,'RACK:  %s',$s4[$i])
                write('file',$infofile,'ZONE:  %s',$s5[$i])
              endif
                write('file',$infofile,'USER:  %s',$s3[$i])
                write('file',$infofile,'STATUS:  NightQueue')
                write('file',$infofile,'START_TIME(est):  %s',$when)
                write('file',$infofile,'---------------------------------------------')
                $i=$i+1
           until $i > $total
        endif
     else
        shell('(cat '+$nightQ+' | fgrep -f '+$tmpfile+' | sed -e \'s|STATUS:.*|STATUS: NightQueue|g\' >> '+$infofile+')'):$dum
     endif
   endif

   shell('rm -f '+$tmpfile):$dum
   autodir=autodir
   vnmrjcmd('pnew','autodirfidname','autodir','userdir')
   if ($1<>'refresh') then
   	vnmrjcmd('popup','mode:modeless','file:autodirqueue.xml','pnewupdate:yes','rebuild:yes','title:'+$subautodir)
   endif


ELSE
  if ($1='get') then
    if $2<>'' then
	is_submitmode:$is
	if ($is) then walkupQ('logout') endif
	$firstchar='' substr($2,1,1):$firstchar
	if ($firstchar<>'/') then
	   $file=userdir+'/data/'+$2
	else
	   $file=$2
	endif
	strstr($file,'.macdir/'):$ret
	if ($ret) then
		rtNprocess($file)
	else
        	dndfid($file)
	endif
        autodirfidname[2]=''
    endif
  endif
ENDIF
