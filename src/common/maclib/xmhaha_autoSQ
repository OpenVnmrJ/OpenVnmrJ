"macro xmhaha_autoSQ"
"***************************************************************"
"  Builds autoQ in sqdir/tmpstudy based on cpdoneQ, cpdonedayQ	"
"	enterQ and overniteQ					"
"***************************************************************"
" arg1 is autodir or archived autodir or globalenter		"
" Other keyword arguments (all optional):			"
"---------------------------------------------------------------"
"  user     - display sample information for a given user	"
"		The arg immediately following the user keyword	"
"		is interpreted as the username			"
"  allusers - display sample information for all users		"
"				(DEFAULT) 			"
"		Same as 'user',''				"
"  current  - display sample information for current operator	"
"		Same as 'user',operator				"
"  rbld	    - rebuild all xml files in autodir xmldir/sqdir	"
"  rbld2lvl - rebuild all files in all locdir (rbld is implicit)"
"---------------------------------------------------------------"
pwd:$curdir
if ($# < 1) then $1='' endif
if ($1='') then 
   if (autodir<>'') then $1=autodir else return endif
endif
"****parse arguments******************************************"
$uinfo=''
$rbld=''
$rbld2=''

$argno=2
while ($argno <= $#) do
   $val=${$argno}
   if ($val='allusers') then
        $uinfo=''
   elseif ($val='user') then
        $argno=$argno+1
        $uinfo=${$argno}
   elseif ($val='current') then
        $uinfo=operator
   elseif ($val='rbld') then
	$rbld=$val
   elseif ($val='rbld2lvl') then
	$rbld='rbld'
	$rbld2='rbld'
  endif
  $argno=$argno+1
endwhile
$ok2=0
operights('viewallopstudies'):$ok2
if ($uinfo='') and (owner<>operator) and ($ok2<0.5) then
   $uinfo=operator
endif

exists('SpecViewPref','parameter','global'):$parex
if ($parex=0) then 
   create('SpecViewPref','string','global') 
   SpecViewPref='ACPEQ',''
endif
setprotect('SpecViewPref','on',256,'global')
$svpsize=size('SpecViewPref')
if ($svpsize<2) then SpecViewPref[2]='' endif
specviewpref('parse')

$adisp=SpecViewPref[1]
$sopt=0 $copt=0 $eopt=0 $qopt=0 $aopt=0 $popt=0 $fopt=0

strstr($adisp,'S'):$sopt
strstr($adisp,'A'):$aopt
strstr($adisp,'E'):$eopt
strstr($adisp,'F'):$fopt
if (traymax<1) then $sopt=0 endif
if ($sopt=0) then
   if ($fopt=0) then
   	strstr($adisp,'C'):$copt
   	strstr($adisp,'P'):$popt
   endif
   strstr($adisp,'Q'):$qopt
   if (traymax<1) then $qopt=0 endif
else
   if ($fopt=0) then
      strstr($adisp,'C'):$copt
   endif
endif
"***Make sure the directory exists*****"
$autodir=$1
$firstchar=''
substr($autodir,1,1):$firstchar
if ($firstchar<>'/') then
   if (cpglobalauto<>'') then
        $autodir=cpglobalauto+'/'+$autodir
   else
        $autodir=userdir+'/Automation/'+$autodir
   endif
endif
exists($autodir,'directory'):$direx
if ($direx=0) then
   write('line3','Directory %s not found',$1)
   return
endif

$curauto=0
$isnight='n'
exists($autodir+'/enterQ.macdir','directory'):$direx
if ($direx=0) then
   $st=0 $stx=''
   strstr($autodir,'.macdir/studytray'):$st,$stx
   if ($stx) then
	shell('dirname '+$stx):$autodir
   endif
   shell('basename '+$autodir):$Qname
   $copt=0 $eopt=0 $popt=0 $fopt=0
   if ($sopt) then $qopt=0 else $qopt=1 endif
else
   $Qname='enterQ'
   if (cpautodir=autodir) then $curauto=1 endif
   $Nx=0
   shell('(cat '+$autodir+'/enterQ.macdir/overniteQ | grep -wc SAMPLE#:)'):$Nx
   if ($Nx>0) then
     $isnight='n'
   else
     exists($autodir+'/enterQ.macdir/AUTOGLOBAL','file'):$e
     if ($e) then
       lookup('mfile',$autodir+'/enterQ.macdir/AUTOGLOBAL','seekcs','LONGRUN:','read',1):$isnight
     endif
   endif
endif

getAutoTraymax($autodir+'/'+$Qname):$thistray
if ($thistray=1) then $islcauto=1 else $islcauto=0 endif

$tmpfile=userdir+'/persistence/autoqtmp'
$subNdir=''
$subNfile=''
$subNi=1
$xmlfiles=''
$xm=1

"*************************************************************"
    $autosqdir=$autodir+'/'+$Qname+'.macdir/xmldir/'+$uinfo+'sqdir'
    exists($autosqdir,'directory'):$sqex
// Always rebuild subxml for specific user option (slower)
    if ($uinfo<>'') then shell('rm -rf '+$autosqdir):$dum $sqex=0 endif
    if ($rbld='rbld') then shell('rm -rf '+$autosqdir):$dum $sqex=0 endif

    if ($sqex=0) then
	xmhaha_asx($autodir,'',$uinfo,$rbld2)
        exists($autosqdir,'directory'):$sqex
    endif
    if ($sqex=0) then
        write('error','Unable to create sub xml files for %s',$autodir)
        return
    endif
    $timeest=''
    write('reset',$autosqdir+'/ActiveQ.xml')
    write('reset',$autosqdir+'/ActiveQ_SQ')
    if ($curauto) then
        xmhaha_asx($autodir,'ActiveQ',$uinfo,$rbld2):$timeest
    endif
    if ($sopt) then
	xmhaha_asx($autodir,'ChronQ',$uinfo,$rbld2)
    endif
    $autosqinfo=$autosqdir+'/info'
    $globalautosqinfo=$autodir+'/'+$Qname+'.macdir/xmldir/info'
    $subxml=''
    if ($curauto) and ($aopt) then 
	$subxml=$subxml,'ActiveQ'
    endif
    if (SpecViewPref[2]<>'reverse') then
    	if ($copt) then $subxml=$subxml,'DoneQ' endif
    	if ($popt) then $subxml=$subxml,'DoneDayQ' endif
    	if ($fopt) then $subxml=$subxml,'FidQ' endif
    	if ($eopt) then $subxml=$subxml,'ErrorQ' endif
    else
        if ($copt) then $subxml=$subxml,'RevDoneQ' endif
        if ($popt) then $subxml=$subxml,'RevDoneDayQ' endif
        if ($fopt) then $subxml=$subxml,'RevFidQ' endif
        if ($eopt) then $subxml=$subxml,'RevErrorQ' endif
    endif
    if ($curauto) and ($aopt=0) then
        $subxml=$subxml,'ActiveQ'
    endif
    if ($sopt) then $subxml=$subxml,'ChronQ' endif
    if ($qopt) then $subxml=$subxml,'PriorityQ','DayQ'
	if ($isnight='n') then $subxml=$subxml,'NightQ' endif
    endif
    if ($islcauto) then
	$subxml=$subxml,'DefPeakQ'
    endif
    $subsize=size('$subxml')
    $sx=1
    repeat
      if ($subxml[$sx]<>'') then
        exists($autosqdir+'/'+$subxml[$sx]+'.xml','file'):$ok
      else
        $ok=0
      endif
      if ($ok) then
        shell('(cat '+$autosqdir+'/'+$subxml[$sx]+'.xml | wc -l)'):$ok
      endif
      if ($ok) then
        $xmlfiles[$xm]=$autosqdir+'/'+$subxml[$sx]+'.xml'
        $subNdir[$xm]=$autosqinfo
        $subNfile[$xm]=$autosqdir+'/'+$subxml[$sx]+'_SQ'
	$xm=$xm+1
      endif
      $sx=$sx+1
    until $sx>$subsize

"****************************************************************************"
"  Now proceed to build the studyQ for this list"
"****************************************************************************"

cd($curdir):$dum
cqinitCP
vnmrjcmd('SQ delete tmpstudy')
$studyxml=sqdir+'/study.xml'
$sqinfo=sqdir+'/info'
$esq=sqdir+'/enterSQ'
write('reset',$esq)
write('reset',$studyxml)

    $subauto=''
    shell('basename '+$autodir):$subauto
    if ($islcauto) then
	shell('dirname '+$autodir):$subdir
	shell('basename '+$subdir):$subauto
    endif
    $tt=$subauto
    $ttxt=$tt
    if ($uinfo<>'') then
        $tt=$tt+' (Samples by '+$uinfo+')'
    endif
    write('reset',$studyxml)
    write('file',$studyxml,'<?xml version="1.0" encoding="UTF-8"?>')
    write('file',$studyxml,'<!DOCTYPE template>')
    write('file',$studyxml,'')
    write('file',$studyxml,'<template>')
    write('fileline',$studyxml,'  <protocol title="%s" ',$tt)
    write('fileline',$studyxml,'type="protocol" exp="grandparent" lock="off" ')
 if ($islcauto) then
    if ($uinfo<>'') then
        write('fileline',$studyxml,'name="LCautomationQueue4%s" ',$uinfo)
    else
        write('fileline',$studyxml,'name="LCautomationQueue" ')
    endif
 else
    if ($uinfo<>'') then
	write('fileline',$studyxml,'name="automationQueue4%s" ',$uinfo)
    else
        write('fileline',$studyxml,'name="automationQueue" ')
    endif
 endif
    write('fileline',$studyxml,'id="tmpstudy" tooltext="%s" expanded="true" >\n',$ttxt)


$total=size('$xmlfiles')
$i=1
repeat
   if ($xmlfiles[$i]<>'') then
	shell('(cat '+$xmlfiles[$i]+' >> '+$studyxml+')'):$dum
   endif
   $i=$i+1
until $i > $total

   write('file',$studyxml,'  </protocol>')
   write('file',$studyxml,'</template>')

$total=size('$subNdir')
$i=1
//write('reset',$tmpfile)
repeat
    $cpnodes=$subNfile[$i]
    $infodir=$subNdir[$i]
    exists($cpnodes,'file'):$fileex
    if ($fileex) then
       write('reset',$tmpfile)
       shell('(cat '+$cpnodes+' | awk \'{print $1}\' > '+$tmpfile+')'):$dum
	$ts1='' $ts2='' $tstot=0
	readfile($tmpfile,'$ts1','$ts2','','local'):$tstot
	if ($tstot>0) then
	    $ti=1
	    repeat
		exists($infodir+'/'+$ts1[$ti],'directory'):$direx
		if $direx then
		    cp('-r',$infodir+'/'+$ts1[$ti],$sqinfo):$dum
		else
		    exists($globalautosqinfo+'/'+$ts1[$ti],'directory'):$direx
		    if $direx then
		    	cp('-r',$globalautosqinfo+'/'+$ts1[$ti],$sqinfo):$dum
		    endif
		endif
	        $ti=$ti+1
	    until $ti>$tstot
	endif
//       pwd:$curdir2
//       cd($infodir):$dum
//       shell('(tar cf - `cat '+$tmpfile+'` | (cd '+$sqinfo+'; tar xfBp -))'):$dum
//       cd($curdir2):$dum
       shell('(cat '+$cpnodes+' | awk \'{print $1" "$2" "$3}\' >> '+$esq+')'):$dum
    endif
    $i=$i+1
until $i > $total

vnmrjcmd('SQ read '+$studyxml)
if ($curauto) and ($sopt=0) then
	xmhaha_settime($timeest)
endif

shell('rm -f "'+$tmpfile+'"'):$dum
cd($curdir):$dum
