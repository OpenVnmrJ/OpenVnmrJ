// Chempacker macro

if $# < 2 then $2=1 endif

if $0='addarray' then
    readheader(curexp+'/acqfil/fid'):$np,$dim
    write('line3','%s/exp%d/acqfil/fid',userdir,$1):$add2fid
    exists($add2fid,'file'):$fidex
    $dim2=0
    if $fidex then
	readheader($add2fid):$np2,$dim2
    endif
    $addcmd='add'
    wft('nodc')
else
	// for spectrum addition it is assumed that the
	// arraydim in the addsubexp is either same or none as
	// in curexp
    dss:$dim
    write('line3','%s/exp%d/procpar',userdir,$1):$add2pp
    exists($add2pp,'file'):$ppex
    $dim2=0
    if $ppex then $dim2=$dim endif
    $addcmd='spadd'
endif

    $addsub=addsubexp
    addsubexp=$1
    f full dc ds
    if not $dim2 then
    	$x=1
    	repeat
            select($x)
	    if $x=1 then {$addcmd}($2):$rdum else {$addcmd}($2,'new') endif
            $x=$x+1
    	until $x>$dim
    else
	$x=1
	repeat
	    select($x)
	    {$addcmd}($2,'trace',$x):$rdum
	    $x=$x+1
	until $x>$dim2
	while $x<=$dim do
	    select($x)
	    {$addcmd}($2,'new')
	    $x=$x+1
	endwhile
    endif
    addsubexp=$addsub
    return

