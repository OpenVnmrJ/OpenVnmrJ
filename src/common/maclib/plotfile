"macro plotfile"
"*********************************"
" plotfile - plot into a PDF, Postscript, PCL, HPGL or JPEG file "
"*********************************"

if $#=0 then $arg='auto' else $arg=$1 endif

resetplotter('setup')

exists('plotfile','parameter'):$exf
if not $exf then
   create('plotfile','string')
endif
exists('plotfileformat','parameter','global'):$exff
if not $exff then 
   create('plotfileformat','string','global')
   plotfileformat='ps'
endif
exists('printout','parameter','global'):$expo
if not $expo then
   create('printout','string','global')
   printout='printer'
endif

if $arg='setup' then return endif

$tmpps='/vnmr/tmp/tmpplot.ps'
exists($tmpps,'file'):$eold
if $eold then delete($tmpps,''):$eold endif

if plotfile=''       then write('error','Please define a filename first!') return endif
if plotfileformat='' then write('error','Please choose a format first!') return endif

exists('pltmod','parameter'):$ep
if $ep then
  if pltmod='off' then 
    pltmod='fixed'
    write('line3','pltmod was set to "off" - reset to "fixed"!')
  endif
endif

$ch=''
substr(plotfile,1,1):$ch
if ($ch = '/') then
  $plotfile=plotfile
else
  Svfname:$next_saved_fid length($next_saved_fid):$ln
  substr($next_saved_fid,$ln-4,1):$numstr
  substr($next_saved_fid,1,$ln-5):$current_fid
  $num=1      format($numstr,'lower'):$num $num=$num-1
  $numstr2='' format($num,'lower'):$numstr2
  $current_fid=$current_fid+$numstr2+'.fid'
  
  exists($current_fid,'directory'):$ef
  if ($ef) then
    exists($current_fid,'file','rw'):$ef
  endif
  if (file = '') then
    $efile=0
  else
    exists(file,'directory'):$efile
    if ($efile) then
      exists(file,'file','rw'):$efile
    endif
  endif

  if file='exp' and $ef then
    $plotfile=$current_fid+'/'+plotfile  // Plot to FID_directory.fid/myplot.xxx
  elseif file<>'exp' and $efile then
    $plotfile=file+'/'+plotfile
  else
    $pldir=userdir+'/plot'
    exists($pldir,'directory'):$exd
    if not $exd then 
      mkdir($pldir) 
    endif
    $plotfile=$pldir+'/'+plotfile       // Else plot to userdir/plots/myplot.xxx
  endif
endif

if (plotter<>'') then
  $ret=1
  checkplotter:$new,$ret
  if $ret=0 then
    return
  else 
    if plotter<>$new then plotter=$new endif
  endif
endif

if $arg='manual' then page($tmpps):$dum else plot endif

exists($tmpps,'file'):$eold
if not $eold then
  write('error','Error while printing - '+$tmpps+' does not exist!')
  return
endif

if plotfileformat='pdf' then
  $ps2pdf=''
  shell('which ps2pdfwr'):$ps2pdf
  if $ps2pdf='' then
    write('error','No PDF converter found - please install Ghostscript first!')
    return
  endif
  shell($ps2pdf+' '+$tmpps+' '+$plotfile+'.pdf')
  delete($tmpps,''):$eold
elseif plotfileformat='ps' then
  mv($tmpps,$plotfile+'.ps')
elseif plotfileformat='jpg' then
  shell('convert -rotate 90 -density 300 -quality 95 '+$tmpps+' '+$plotfile+'.jpg')
  delete($tmpps,''):$eold
elseif plotfileformat='png' then
  shell('convert -rotate 90 -density 300 -quality 95 '+$tmpps+' '+$plotfile+'.png')
  delete($tmpps,''):$eold
elseif plotfileformat='pcl' then
  mv($tmpps,$plotfile+'.pcl')
elseif plotfileformat='hpgl' then
  mv($tmpps,$plotfile+'.hpgl')
else
  write('error','Please choose a format first!')
endif
write('line3','%s.%s written', $plotfile, plotfileformat)

resetplotter
