// Chempacker macro
//  This is dss with color option for individual traces
//  Follows similar to dssi but not interactive.  So valid fo >128 traces
// Look in the header of the dssi macro for arguments and their relevance

// If argument is supplied, interpret it, set appropriate
//   parameter and call dssi again

    bgmode_is:$BG
// While in BG mode don't bother with mspec
    if $BG then ds(1) return endif

// The following is the generic dssi call after the parameters
//   are set appropriately
    clear(2)
    $colors=''
    destroy('dssicolors','global'):$dum
    exists('dssicolors','parameter'):$parex
    if $parex then $colors=dssicolors endif
    $colorsize=size('$colors')
    if ($colors[1]='') then $colorsize=0 endif

    if $colorsize then
      exists('colorMap','maclib'):$macex
      if $macex then
          $i=1
          repeat
              colorMap($colors[$i]):$cndx,$colors[$i]
              $i=$i+1
          until $i > $colorsize
      endif
    else
	$i=0
	$i=$i+1 $colors[$i]='spectrum'
        $i=$i+1 $colors[$i]='spectrum2'
        $i=$i+1 $colors[$i]='spectrum3'
        $i=$i+1 $colors[$i]='spectrum4'
        $i=$i+1 $colors[$i]='spectrum5'
        $i=$i+1 $colors[$i]='spectrum6'
        $i=$i+1 $colors[$i]='spectrum7'
        $i=$i+1 $colors[$i]='spectrum8'
        $i=$i+1 $colors[$i]='spectrum9'
    endif

    $trace=0
    destroy('dssitrace','global'):$dum
    exists('dssitrace','parameter'):$parex
    if $parex then 
	if dssitrace=0 then $parex=0 endif
    endif
    if $parex then
	$trace=dssitrace 
    else
	getvalue('arraydim','processed'):$dim
	$i=1
	while $i<=$dim do
	    $trace[$i]=$i
	    $i=$i+1
	endwhile
    endif
    $tracedim=size('$trace')
    if ($trace[1]=0) then $tracedim=0 endif
    if not $tracedim then return endif

    $groups=$tracedim
    $dodc='dodc' $scale=''
    if $# > 0 then 
	format($1,'isreal'):$real
	if $real then
	    $groups=$1 
	else
	    if $1<>'noscale' then
	        $dodc=$1
	    else
		$scale=$1
	    endif
	endif
    endif
    if $# > 1 then
	format($2,'isreal'):$real
	if not $real then
	    if $2='noscale' then
	    	$scale=$2
	    endif
	endif
    endif

    $grp=$tracedim/$groups
    $vo=vo
    if $groups>1 then
    	vo=($vo*$tracedim)/$groups
    endif

    if not $colorsize then
    	$i=10
    	while $i<$tracedim do
	    $x=($i mod 9)
	    if not $x then $x=9 endif
	    $colors[$i]=$colors[$x]
	    $i=$i+1
    	endwhile
    endif
    $colorsize=size('$colors')
    if $colors[1]='' then $colorsize=0 endif
	
	    $i=1 $j=1 $first=1 $vp=vp $sc=dss_sc
	    repeat
		if $j>$grp then vp=vp+vo $j=1 endif
		if $trace[$i] then
		    $ci=$trace[$i]
		    if not $first then
			if $colorsize >= $trace[$i] then
			  if $dodc='dodc' then
			    dssn($dodc,$trace[$i],$colors[$ci])
			  else
			    dssn($trace[$i],$colors[$ci])
			  endif
			else
			  if $dodc='dodc' then
			    dssn($dodc,$trace[$i])
			  else
			    dssn($trace[$i])
			  endif
			endif
		    else
			if $colorsize >= $trace[$i] then
			  if $dodc='dodc' then
			    dss($dodc,$trace[$i],$colors[$ci])
			  else
			    dss($trace[$i],$colors[$ci])
			  endif
			else
			  if $dodc='dodc' then
			    dss($dodc,$trace[$i])
			  else
			    dss($trace[$i])
			  endif
			endif
			if $scale<>'noscale' then
			    dscale
			endif
			$first=0
		    endif
		    $j=$j+1
//		    vp=vp+vo
//		    dss_sc=dss_sc+ho
		endif
		$i=$i+1
	    until $i > $tracedim
	    vp=$vp
	    vo=$vo
	    dss_sc=$sc
	    return
