"macro xmhaha_fullSQ"

$xmldir=$2
$sqdir=$2+'/sqdir'
$xml=$sqdir+'/study.xml'
$esq=$sqdir+'/enterSQ'
$xml2=$sqdir+'/scoutfid_study.xml'
$esq2=$sqdir+'/scoutfid_enterSQ'
$xml3=$sqdir+'/error_study.xml'
$esq3=$sqdir+'/error_enterSQ'
$xml4=$sqdir+'/scoutfid_error_study.xml'
$esq4=$sqdir+'/scoutfid_error_enterSQ'
$info=$sqdir+'/info'

IF ($1='get') THEN

    is_submitmode:$submit
    if ($submit) then
	$error=1 $scout=0
    else
	SampleViewPref('get','scout'):$scout
	SampleViewPref('get','error'):$error
    endif
    $retok=0 $retnode=''
    if ($error) and ($scout) then
	$xmlfile=$xml4
	$esqfile=$esq4
    elseif ($error) then
	$xmlfile=$xml3
	$esqfile=$esq3
    elseif ($scout) then
	$xmlfile=$xml2
	$esqfile=$esq2
    else
	$xmlfile=$xml
	$esqfile=$esq
    endif
    exists($xmlfile,'file'):$retok
    if ($retok) then
	exists($esqfile,'file'):$retok
    endif
    if ($retok) then
	exists($info,'directory'):$retok
    endif
    if ($retok) then
        cqinitCP
	shell('cd "'+$sqdir+'"; cp -r * '+cursqexp+'/tmpstudy'):$dum
	if ($error) and ($scout) then
	    mv(cursqexp+'/tmpstudy/scoutfid_error_study.xml',cursqexp+'/tmpstudy/study.xml'):$dum
	    mv(cursqexp+'/tmpstudy/scoutfid_error_enterSQ',cursqexp+'/tmpstudy/enterSQ'):$dum
	elseif ($error) then
	    mv(cursqexp+'/tmpstudy/error_study.xml',cursqexp+'/tmpstudy/study.xml'):$dum
	    mv(cursqexp+'/tmpstudy/error_enterSQ',cursqexp+'/tmpstudy/enterSQ'):$dum
	elseif ($scout) then
	    mv(cursqexp+'/tmpstudy/scoutfid_study.xml',cursqexp+'/tmpstudy/study.xml'):$dum
	    mv(cursqexp+'/tmpstudy/scoutfid_enterSQ',cursqexp+'/tmpstudy/enterSQ'):$dum
	endif
	rm('regex',cursqexp+'/tmpstudy','_study.xml$'):$dum
	rm('regex',cursqexp+'/tmpstudy','_enterSQ$'):$dum
  	vnmrjcmd('SQ delete tmpstudy')
  	vnmrjcmd('SQ read '+cursqexp+'/tmpstudy/study.xml')
	copyf($esqfile,'tail',1,'awk','$1','|wc'):$dumx,$retnode
        reconcileSQfids
    endif
    return($retok,$retnode)
ENDIF

IF ($1='set') THEN

    exists($sqdir,'directory'):$direx
    if ($direx) then
    	rm('-rf',$sqdir):$dum
    endif
    mkdir($sqdir):$e
    exists($sqdir,'file','w'):$ok
    if not ($ok) then return endif
    write('reset',$sqdir+'.info')

    $archsamp='' 
    strstr($xmldir,'/dirinfo/'):$ret,$archsamp
    $xmlreturn='','','','','','',''
    $xmlct=0,0,0,0,0,0,0
    $cxml='' $cxmlct=0
    $cpnodes='' $cpnodes2=''
    $sample='' $archive='' $samplename='' $studyowner=''
"**************************************************************"
    substr($archsamp,'dirname'):$archive
    substr($archsamp,'basename'):$sample
    $archivedirsample=$archsamp
    exists($archsamp+'/dirinfo/macdir/sampleglobal','file'):$sgex
    if ($sgex) then
        fread($archsamp+'/dirinfo/macdir/sampleglobal','usertree','reset')
        getvalue('samplename','usertree'):$samplename,$ok
        getvalue('studyowner','usertree'):$studyowner,$ok
    	if ($studyowner='') then
          getvalue('operator_','usertree'):$studyowner,$ok
    	endif
    else
	return
    endif

    $tmpfile=userdir+'/persistence/'+$sample+'_tmp'
    $tmpfile2=$tmpfile+'2'
    write('reset',$tmpfile)

    $fidlog='dirinfo/fidlog'
    $spectrumlog='dirinfo/spectrumlog'
    $craftlog='dirinfo/craftlog'
    $explistpar='dirinfo/macdir/explist'
    $expqueuepar='dirinfo/macdir/expqueue'
    $cmdnodes='dirinfo/macdir/CMDnodes'
    $acqlog='dirinfo/macdir/ACQlist'
    $EXPLIST='dirinfo/macdir/EXPLIST'
    $LONGLIST='dirinfo/macdir/LONGLIST'
"**************************************************************"
"  Find out what info should be used to build SQ        "

    $usefidlog='yes'
    $usespectrumlog='yes'
    $usecraftlog='yes'
    $useexplist='yes'
    $useEXPLIST='yes'
    $useACQlist='yes'
    $useLONGLIST='yes'
"**************************************************************"
"*****All local variable defined, proceed***"
"**********************************************************************"
    pwd:$curdir
    cd($archsamp):$dum
    $cpdir=$xmldir+'/cpnodeinfo'

"*************************************************************************"
	"***Get fidlog subxml***"
   	exists($xmldir+'/fidlog','file'):$xmlfileex
   	if ($xmlfileex) then
            $xmlreturn[1]=$xmldir+'/fidlog'
	    append($cpdir,'grep -w','Completed','awk','$1 $4',$tmpfile):$dum
	    exists($xmldir+'/clusterlog','file'):$cxmlfileex
	    if $cxmlfileex then
		$cxml=$xmldir+'/clusterlog'
		lookup('mfile',$cxml,'countline'):$cxmlct
		if $cxmlct=0 then $cxml='' endif
	    endif
   	endif
"*************************************************************************"
        "***Get spectrumlog subxml***"
        exists($xmldir+'/spectrumlog','file'):$xmlfileex
        if ($xmlfileex) then
            $xmlreturn[2]=$xmldir+'/spectrumlog'
	    append($cpdir,'grep -w','Spectrum','awk','$1 $4',$tmpfile):$dum
        endif
"*************************************************************************"
        "***Get craftlog subxml***"
        exists($xmldir+'/craftlog','file'):$xmlfileex
        if ($xmlfileex) then
            $xmlreturn[3]=$xmldir+'/craftlog'
	    append($cpdir,'grep -w','Craft','awk','$1 $4',$tmpfile):$dum
        endif
"*************************************************************************"
	"***Get errorlog subxml***"
   	exists($xmldir+'/errorlog','file'):$xmlfileex
   	if ($xmlfileex) then
	    $xmlreturn[4]=$xmldir+'/errorlog'
	    append($cpdir,'grep -w','Error','awk','$1 $4',$tmpfile):$dum
   	endif

"*************************************************************************"
        "***Get a explist subxml***"

    exists($xmldir+'/expqueue','file'):$xmlfileex
    if ($xmlfileex) then
        $xmlreturn[5]=$xmldir+'/expqueue'
    else
    	exists($xmldir+'/explist','file'):$xmlfileex
    	if ($xmlfileex) then
		$xmlreturn[5]=$xmldir+'/explist'
	endif
    endif
    if ($xmlfileex) then
	append($cpdir,'grep -w','Queued','awk','$1 $4',$tmpfile):$dum
    endif

"****************************************************************************"
        "***Get a EXPLIST subxml***"

    exists($xmldir+'/EXPLIST','file'):$xmlfileex
    if ($xmlfileex) then
	$xmlreturn[6]=$xmldir+'/EXPLIST'
	append($cpdir,'grep -w','Day_queued','awk','$1 $4',$tmpfile):$dum
    endif

"****************************************************************************"
        "***Get a LONGLIST subxml***"

    exists($xmldir+'/LONGLIST','file'):$xmlfileex
    if ($xmlfileex) then
	$xmlreturn[7]=$xmldir+'/LONGLIST'
	append($cpdir,'grep -w','Night_queued','awk','$1 $4',$tmpfile):$dum
    endif

"****************************************************************************"
    cd($curdir):$dum
"****************************************************************************"
"  Now proceed to build the studyQ for this list"

    $xmlsize=size('$xmlreturn')
    $i=1 $xmlok=0
    repeat
     	if ($xmlreturn[$i]<>'') then
	    exists($xmlreturn[$i],'file'):$ok
	    if $ok then
		lookup('mfile',$xmlreturn[$i],'countline'):$xmlct[$i]
	    endif
            if ($xmlct[$i]=0) then $xmlreturn[$i]='' else $xmlok=1 endif
     	endif
        $i=$i+1
    until $i>$xmlsize
    if ($xmlok=0) then
	delete($tmpfile,''):$dum
    	return
    endif

    $gtitle=$sample
    exists('img2cp','maclib'):$imgex
    if ($imgex) then
	$studyid='' $isimg=0
	img2cp('isimgsample',$archive+'/'+$gtitle):$isimg,$studyid
	if ($isimg) and ($studyid<>'') then
	    $gtitle=$studyid
	endif
    endif
    if ($sample='NMR') then  //This is LC NMR
        substr($archive,'basename'):$lcid
        $gtitle=$lcid
    endif

    $ttxt=$samplename
    $tt=$gtitle
    if ($studyowner<>operator) and (operator<>owner) then
	$tt=$tt+' (study owned by: '+$studyowner+')'
    endif

    write('reset',$xml)
    write('reset',$esq)
    mkdir($info):$e

    write('file',$xml,'<?xml version="1.0" encoding="UTF-8"?>')
    write('file',$xml,'<!DOCTYPE template>')
    write('file',$xml,'')
    write('file',$xml,'<template>')
    write('fileline',$xml,'  <protocol title="%s" ',$tt)
    write('fileline',$xml,'type="protocol" exp="grandparent" lock="off" ')
    write('fileline',$xml,'id="tmpstudy" tooltext="%s" expanded="true">\n',$ttxt)
    write('reset',$sqdir+'/cpnodeinfo')
    cp($xml,$xml3):$dum

    $filter='Completed','Spectrum','Craft','Error','Queued','Day_queued','Night_queued'
    pwd:$curdir
    if $cxml<>'' then
        append($cxml,$xml3):$dum
        append($cxml,$xml):$dum
    endif
    write('reset',$tmpfile2)
    $i=1
    repeat
        if ($xmlreturn[$i]<>'') then
            append($xmlreturn[$i],$xml3):$dum
	    if ($i<>4) then
			// not error nodes
		append($xmlreturn[$i],$xml):$dum
	    endif
	    if $i=3 then
			// craft nodes. close protocols
		write('file',$xml3,'  </protocol>')
		write('file',$xml,'  </protocol>')
	    endif
	    cd($xmldir):$dum
	    append('cpnodeinfo','grep -w',$filter[$i],'awk','$1 $2 $3',$esq3):$dum
	    if ($i<>4) then
			// not error nodes
		append('cpnodeinfo','grep -w',$filter[$i],'awk','$1 $2 $3',$esq):$dum
	    endif
	    append('cpnodeinfo','grep -w',$filter[$i],'awk','$1',$tmpfile2):$dum
        endif
        $i=$i+1
    until $i > $xmlsize

    if $cxmlct then
    	write('file',$sqdir+'.info','clusterlog %d',$cxmlct)
    endif
	write('file',$sqdir+'.info','fidlog %d',$xmlct[1])
        write('file',$sqdir+'.info','spectrumlog %d',$xmlct[2])
        write('file',$sqdir+'.info','craftlog %d',$xmlct[3])
        write('file',$sqdir+'.info','errorlog %d',$xmlct[4])
        write('file',$sqdir+'.info','explist %d',$xmlct[5])
        write('file',$sqdir+'.info','EXPLIST %d',$xmlct[6])
        write('file',$sqdir+'.info','LONGLIST %d',$xmlct[7])

    cd($curdir):$dum

    cp($xml,$xml2):$dum
    cp($esq,$esq2):$dum
    cp($esq3,$esq4):$dum
    cp($xml3,$xml4):$dum
    write('file',$xml,'  </protocol>')
    write('file',$xml,'</template>')
    write('file',$xml3,'  </protocol>')
    write('file',$xml3,'</template>')

        exists($xmldir+'/scoutlog','file'):$xmlfileex
        if ($xmlfileex) then
	    copyf($xmldir+'/scoutlog','grep','protocol','|wc'):$xmlfileex
	endif
	if ($xmlfileex) then
            append($xmldir+'/scoutlog',$xml2):$dum
            append($xmldir+'/scoutlog',$xml4):$dum
            cd($xmldir):$dum
            append('cpnodeinfo','grep -w','Scout','awk','$1 $2 $3',$esq2):$dum
            append('cpnodeinfo','grep -w','Scout','awk','$1 $2 $3',$esq4):$dum
            append('cpnodeinfo','grep -w','Scout','awk','$1',$tmpfile2):$dum
            write('file',$xml2,'  </protocol>')
            write('file',$xml4,'  </protocol>')
        endif
        cd($curdir):$dum

    	write('file',$xml2,'  </protocol>')
    	write('file',$xml2,'</template>')
        write('file',$xml4,'  </protocol>')
        write('file',$xml4,'</template>')

  $ts1='' $ts2='' $tstot=0
  readfile($tmpfile2,'$ts1','$ts2','','local'):$tstot
  if ($tstot) then
    $ti=1
    repeat
        cp('-r',$xmldir+'/info/'+$ts1[$ti],$info):$dum
        $ti=$ti+1
    until $ti>$tstot
  endif
  delete($tmpfile2,''):$dum

    chmod('-R g+w',$xmldir):$dum
    delete($sqdir+'/cpnodeinfo',''):$dum
    delete($tmpfile,''):$dum

ENDIF

IF ($1='check') THEN
    touch($sqdir+'.info')
    exists($sqdir+'.info','file'):$ok
    if not ($ok) then return endif
    $s1='' $s2=0
    readfile($sqdir+'.info','$s1','$s2','','local'):$total
    if ($total<6) then return(0) endif
    $i=1
    repeat
	$s3=0
	exists($xmldir+'/'+$s1[$i],'file'):$fex
	if $fex then
	    lookup('mfile',$xmldir+'/'+$s1[$i],'countline'):$s3
	endif
	if $s2[$i]<>$s3 then return(0) endif
	$i=$i+1
    until $i > $total
    return(1)
ENDIF
