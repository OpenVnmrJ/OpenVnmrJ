"macro Hdec"
"********************************************************"
"Hdec - Proton decoupler Autocalibration             "
"********************************************************"

mkpar('string','execprocess','','execplot','')
setgroup('execprocess','display')
setgroup('execplot','display')
execprocess='wft dssh darray(\'CalibrationResults\')'
execplot='execplot=\'\''

getlimit('dpwr'):$max,$min,$dbstep
if (console='inova') or (console='mercury') then $dbstep=1 endif
// For now force dbstep to integer or 0.5
if $dbstep>0.5 then $dbstep=trunc($dbstep+0.5) else $dbstep=0.5 endif

if ($# < 1) then $1 = 1 endif

IF ($1 = 1) THEN

    $fileex=0 $file=''
    cplog('PROTON','first'):$file,$asdir
    if ($file<>'') then rt($asdir+'/'+$file) else return endif
    wft
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    $solv=''
    format(solvent,'lower'):$solv
    tn='C13' dn='H1'  seqfil=seqfil
    if ($solv='cdcl3') then
  	setsw(0,-50)
  	dof=-2.6*dfrq-1500,-2.6*dfrq+1500 j1xh=151
    else
  	setsw(75,25)
  	dof=-1.7*dfrq-1500,-1.7dfrq+1500 j1xh=142
    endif
    getparam('tpwr',tn):tpwr
    getparam('pw90',tn):pw90
    pw=pw90

    ai fn=8k at=0.5 lb=1/at
    if (Console = 'mercury') then gain=20 else gain=40 endif
    wnt=''
    alock='n' wshim='n' r1=1 r3=1

    getparam('tpwr','H1'):$H1tpwr
    getparam('pw90','H1'):$H1pw90
    H1dpwr=$H1tpwr-20 H1dmf=trunc(1e5/$H1pw90)

    dm='nny' dmm='ccc' pplvl=H1pplvl vp=wc2max/2
    pp=H1pp dpwr=H1dpwr dmf=H1dmf
    d1=5 nt=8 alfa=20 rof2=10
    alfa=20 rof2=10
    setddrtc:$dum
    wnt='wft f full aph0 vsadj(20) dssh'
    caliblist=$0
    xmhaha_calib('synonym',$0):caliblist
    calibnode='_iteration'
    alock='n' wshim='n' r1=1
    clear(2)
    write('line3','_dpwr%g',dpwr):calibnode
    chkname(calibnode,'alnum-'):calibnode
    xmhaha_calib('addNode','Hdec(2)')

"********************************************************"
ELSEIF ($1 = 2) THEN

    cplog('CARBON','last'):$file,$asdir
    rt($asdir+'/'+$file)
    wft select(1) f full aph0 vsadj peak:$ht,cr wp=1000 sp=cr-450
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    av
    peak(sp,wp+sp):$ht,cr
    $thresh=0.9 $peaks=0
    repeat
 	th=$thresh*$ht nll:$peaks
 	$thresh=$thresh-0.1
    until ($peaks > 1 or th<0.05)

    if (th < 0.05) then
  	write('error','Hdec cannot find threshold.')
	abort
    endif

    $delta1=llfrq[2]-llfrq[1]

    select(2)
    peak(sp,wp+sp):$ht,cr
    $thresh=0.9 $peaks=0
    repeat
 	th=$thresh*$ht nll:$peaks
 	$thresh=$thresh-0.1
    until ($peaks > 1 or th<0.05)

    if (th < 0.05) then
  	write('error','Hdec cannot find threshold.')
	abort
    endif

    $delta2=llfrq[2] - llfrq[1]

    h2cal($delta1,$delta2,j1xh):$gammah2
    $line=''
    write('line3','%s - (measured) dpwr:%g  gammaB2:%0.1f',caliblist,dpwr,$gammah2):$line
    CalibrationResults=CalibrationResults,$line

    $ref_pw90=250000/$gammah2
    $ref_pwr=dpwr

    $req_gamma=2*12*dfrq
    $req_width=1e6/$req_gamma
    $mult = $req_width/$ref_pw90
    ln($mult):$lnmult
    $attnval = trunc(20*0.43429*$lnmult/$dbstep) * $dbstep
    exp($attnval*2.303/20):$mult
    $req_width=$ref_pw90*$mult
    $level=$ref_pwr-$attnval
    dpwr=$level dmf=1e6/$req_width dmfadj

    $line=''
    write('line3','%s - (recorded) dpwr:%g  dmf:%d',caliblist,dpwr,dmf):$line
    CalibrationResults=CalibrationResults,$line

    $req_width=H1pp
    $mult = $req_width/$ref_pw90
    ln($mult):$lnmult
    $attnval = trunc(20*0.43429*$lnmult/$dbstep)*$dbstep - $dbstep
    $level=$ref_pwr-$attnval
    if ($level > 63) then
  	$attnval=$ref_pwr-63
  	$level=63
    endif
    exp($attnval*2.303/20):$mult
    $req_width=$ref_pw90*$mult
    pp=$req_width pplvl=$level

    $strdmf='' $strdpwr='' 
//    format(dpwr,0,0):$strdpwr
    write('line3','%g',dpwr):$strdpwr
    format(dmf,0,0):$strdmf
    setparams('dpwr',$strdpwr,'H1')
    setparams('dmf',$strdmf,'H1')
    exists('decwave','parameter'):$decwaveex
    if ($decwaveex) then
       addparams('decwave','w','H1')
    endif

//Autocalsave('H1gammah2')

    seqfil='ppcal'
    d2=1/2/j1xh
    p1=2*pw
    dof=(dof[1]+dof[2])/2
    $ppinc=pp/18
    $ppinc=(trunc(($ppinc*20)+0.5))/20
    array('pp',7,4*$ppinc,$ppinc)
    pp=0,pp
    dm='nny' dmm='ccw'
    da r1=1 r2=1
    d1=20 
    nt=2 ss=-1 ph
    wnt='wft(`acq`) select(1) aph0 vsadj(20) dssh'
    write('line3','_pplvl%g_pp90',pplvl):calibnode
    chkname(calibnode,'alnum-'):calibnode
    execprocess='Hdec(4)'

ENDIF

"********************************************************"
IF ($1 = 3) or ($1=4) THEN

    if ($1=3) then
	cplog('ppcal','last'):$file,$asdir
	rt($asdir+'/'+$file)
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    endif
    clear(2)
    wft select(1) f full ph aph0 dc peak:$ht,cr
    $i=2
    $xi=1
    $pptotal=0

    repeat
  	select($i) dc peak:$ht[$i],cr
  	$ix2[$xi]=$ht[$i]/$ht[1]
  	if ($ix2[$xi] < 1) then
    	    ln($ix2[$xi]):$lni[$xi] 
    	    $lni[$xi]=$lni[$xi]/3 
    	    exp($lni[$xi]):$x[$xi]
    	    acos($x[$xi]):$rad[$xi]
    	    $angle[$xi]=$rad[$xi]*360/2/3.1416

    	    $pp[$xi]=pp[$i]*90/$angle[$xi]
    	    $pptotal=$pptotal+$pp[$xi]
    	    $xi=$xi+1
  	endif
  	$i=$i+1
    until $i > arraydim

    $pp=$pptotal/(arraydim-1)
    $pp=trunc($pp*20)/20 

    $line=''
    write('line3','%s - pplvl:%g  pp:%0.2f',caliblist,pplvl,$pp):$line
    CalibrationResults=CalibrationResults,$line
    if ($1=3) then
	writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')
    endif

    pp=$pp
    PWRcalib($pp,H1pp,'pplvl'):$pplvldiff
  	if $pplvldiff <> 0 then
	    if ($1=4) then
    		write('line3','_pplvl%g_pp90',pplvl):calibnode
	        chkname(calibnode,'alnum-'):calibnode
		xmhaha_calib('addNode','Hdec(3)','top')
    	    else
   	    	pplvl = pplvl + $pplvldiff
   	    	pp=H1pp
   	    	$ppinc=pp/18
   	    	$ppinc=(trunc(($ppinc*20)+0.5))/20
   	    	array('pp',7,4*$ppinc,$ppinc)
   	    	pp=0,pp
    		write('line3','_pplvl%g_pp90',pplvl):calibnode
	        chkname(calibnode,'alnum-'):calibnode
		execprocess='Hdec(4)'
  	    endif
	    return
 	endif

    $strpp='' $strpplvl=''
//    format($pp,0,1):$strpp
//    format(pplvl,0,0):$strpplvl
    write('line3','%0.3f',$pp):$strpp
    write('line3','%g',pplvl):$strpplvl
    $strdmf='' $strdpwr=''
//    format(dpwr,0,0):$strdpwr
    write('line3','%g',dpwr):$strdpwr
    format(dmf/4,0,0):$strdmf
    pp=$pp
    setparams('pplvl',$strpplvl,'H1')
    setparams('pp',$strpp,'H1')

    if ($1=4) then
	$CalibrationResults=CalibrationResults
	tmpsave('Hdec')

    	seqfil='s2pul'
    	dm='nny' dmm='ccw' d1=20 nt=1 ss=1
    	d2=0 p1=0
    	$dof=dof+(8*dfrq)
    	array('dof',33,$dof,-0.5*dfrq)
    	vp=0 wnt='wft(`acq`) select(1) aph0 vsadj(20) dssh'
	$ep='' write('line3','wft wp=%0.1f sp=%0.1f select(17) aph0 vsadj(100) dssh',wp,sp):$ep
    	execprocess=$ep+' darray(\'CalibrationResults\')'
    	execplot='cpplss'
    	$text='WALTZ-16 H1 decoupling profile:'
    	$atext='  +/- 8ppm from on-resonance'
    	$atext0='     in 0.5ppm steps'
    	$atext1='H1 dec. gammah2: '+$strdmf
    	$atext2 ='   at power level: '+$strdpwr
    	atext($text)
    	atext($atext) atext($atext0) atext($atext1) atext($atext2)
	CalibrationResults=$CalibrationResults
	calibnode='_waltz16_Profile'
	xmhaha_calib('savePar','H1dec_waltz')

	tmpreturn('Hdec')
    endif

ENDIF

"********************************************************"
