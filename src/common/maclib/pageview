"macro pageview"
"pageview - a utility macro to view plots prior to plot/save/email"
" This uses pdfplotpar global parameter as a tool for popup/action"
//	pdfplotpar is never set manually nor used for any other function
//	pdfplotpar[1]=plotname
//	pdfplotpar[2]=emailaddress
//	pdfplotpar[3]=plot option
//	pdfplotpar[4]=save option
//	pdfplotpar[5]=email option
//	pdfplotpar[6]=pathname of the sample directory
//	pdfplotpar[7]=truncated pathname for display purpose
//	pdfplotpar[8]=tmpplot name with date/time stamp
//	pdfplotpar[9]=plotformat from preferences
//	pdfplotpar[10]=flag to save/email in the preferences format
"************************************************************"
if ($# < 1) then $1='review' endif
if ($1='cancel') then return endif
bgmode_is:$bg
if ($bg) or (auto='y') then return endif

if ($1='clear') then
   rm('regex',userdir+'/persistence/plotpreviews','^'+operator+'_tmpplot'):$dum
   return
endif

if ($1='popup') then
        vnmrjcmd('pnew','pdfplotpar')
        $vjcmd='vnmrjcmd(\'popup\',\'mode:modal\',
                \'file:pageview.xml\',
                \'rebuild:yes\',
                \'pnewupdate:true\',
                \'location:topMenuBar\',
                \'ok:pageview(`proceed`)\',
                \'cancel:pageview(`cancel`)\',
                \'title: Plot View\')'
        exec($vjcmd)
        return
endif

$dum=''
uname:$uname
$showCmd='/usr/bin/acroread'
exists($showCmd,'file','x'):$aex
if ($aex=0) then
  $showCmd=systemdir+'/bin/vnmr_open'
  exists($showCmd,'file','x'):$aex
endif

$ps2pdfex=0 $ps2pdfwr=''
if ($uname <> 'Darwin') then
  $prog='ps2pdfwr'
else
  $prog='pstopdf'
endif
getbinpath($prog,'bin'):$ps2pdfex,$ps2pdfwr
if ($ps2pdfex = 0) then
  if ($uname = 'Darwin') then
    $prog='ps2pdf'
    getbinpath($prog,'bin'):$ps2pdfex,$ps2pdfwr
    if ($ps2pdfex = 0) then
      // check two places where brew may put ps2pdf
      $ps2pdfwr='/usr/local/bin/'+$prog
      exists($ps2pdfwr,'file'):$ps2pdfex
      if ($ps2pdfex = 0) then
        $ps2pdfwr='/opt/homebrew/bin/'+$prog
        exists($ps2pdfwr,'file'):$ps2pdfex
      endif
    endif
    if ($ps2pdfex = 0) then
      write('error','Postscript to PDF conversion tool not installed. From a terminal, run ovjGetps2pdf')
      return
    endif
    $ps2pdfex=2
  else
    write('error','Postscript to PDF conversion tool ( %s ) not installed.',$prog)
    return
  endif
endif

"************************************************************"
if ($1='setup') then
   //if (plotter='none') or (plotter='') then
   //  write('error','plotter is not set to a valid device')
   //endif
   $psplotter='' $ret=0
   is_psplotter:$psplotter,$ret
   if ($aex=0) or ($ps2pdfex=0) or ($ret=0) then
     PDFpreview='n'
   else
    PDFpreview='y'
   endif
   exists('pdfplotpar','parameter','global'):$plpon
   if ($plpon=0) then create('pdfplotpar','string','global') endif
   setprotect('pdfplotpar','on',256,'global')
   exists('pdfpreview','parameter','global'):$oldpdfon
   if ($oldpdfon) then destroy('pdfpreview','global') endif
   exists(userdir+'/persistence/plotpreviews','directory'):$direx
   if ($direx=0) then
	mkdir('-p',userdir+'/persistence/plotpreviews'):$dum
   endif
   rm('regex',userdir+'/persistence/plotpreviews','^'+operator+'_tmpplot'):$dum
   return
endif

if ($1='auto') then
   if ($aex=0) then
      write('error',$showCmd+' is not found')
      return
   endif

   if ($ps2pdfex=0) then
      write('error',$prog+' is not found')
      return
   endif

   is_psplotter:$psplotter,$ret
   if ($ret=0) then
	write('error','A postscript plotter not found')
	return
   endif
   $plotter=plotter
   if ($psplotter<>$plotter) then
	plotter=$psplotter
   endif
   docpplot('pageview')
   if ($psplotter<>$plotter) then
	plotter=$plotter
   endif

endif

if ($1='review') then

  $plf='pdf'
  getadminfo('plotformat'):$plf
  
  $error=0
   if ($aex=0) then
      write('error',$showCmd+' is not found')
      $error=1
   endif
   if ($ps2pdfex=0) and ($plf='pdf') then
      write('error',$prog+' is not found')
      $error=1
   endif
   getplottertype(plotter):$plottype
   if ($plottype<>3) and ($plottype<>4) then
        write('error','%s is not a ps plotter',plotter)
	$error=1
   endif
  if ($error) then
    page('clear')
    return
  endif

  pdfplotpar[1]=''
  pdfplotpar[2]=emailaddr
  pdfplotpar[3]=''
  pdfplotpar[4]=''
  pdfplotpar[5]=''
  pdfplotpar[6]=''
  pdfplotpar[7]=''
  pdfplotpar[8]=''
  pdfplotpar[9]=$plf
  if ($plf<>'pdf') then 
	pdfplotpar[10]='y'
  else
	pdfplotpar[10]='n'
  endif

  isnewexp:$isnew
  $archive='' $sample=''
  if ($isnew=0) then
        substr(file,'dirname'):$archive
        exists($archive+'/plots','directory'):$direx
	if $direx then exists($archive+'/plots','file','w'):$direx endif
        if ($direx=0) then $isnew=1 endif
  endif
  if $plf='postscript' then $plf='ps' endif
  if ($isnew=0) then
	$expname='' $plotname='' $name=''
	substr(file,'basename'):$expname,$ext
        Svfname($archive+'/plots/'+$expname+'_plot','','.pdf, .'+$plf):$plotname
	substr($plotname,'basename'):$name,$ext2
        substr($plotname,'basename','.pdf'):$name
	write('plotter',0,0,''):$ys
	$y=-(4*$ys+5)
	$x=5
	write('plotter',$x,$y,'Plotname: --Not assigned--')
	pdfplotpar[1]=$name
	pdfplotpar[6]=$archive
	substr($archive,'basename'):$sample
	pdfplotpar[7]='(..../'+$sample+'/plots/)'
  else
	exists(userdir+'/plots','directory'):$direx
	if not $direx then mkdir(userdir+'/plots'):$dum endif
	pdfplotpar[6]=userdir+'/plots'
	Svfname(userdir+'/plots/$operator$_plot%R4%','','.pdf, .'+$plf):$name
	substr($name,'basename'):$name,$ext
	pdfplotpar[1]=$name
	pdfplotpar[7]='(.../vnmrsys/plots/)'
  endif

  pwd:$curdir
  exists(userdir+'/persistence/plotpreviews','directory'):$direx
  if ($direx=0) then mkdir('-p',userdir+'/persistence/plotpreviews'):$dum endif
  cd(userdir+'/persistence/plotpreviews'):$dum
  $tmpplot='' $pdfname=''
  Svfname(userdir+'/persistence/plotpreviews/'+operator+'_tmpplot_','','.pdf, .'+$plf):$pdfname
  substr($pdfname,'basename','.pdf'):$tmpplot
  pdfplotpar[8]=$tmpplot

  delete($tmpplot,''):$dum
  delete($tmpplot+'.pdf',''):$dum
  plotpage($tmpplot):$dum
 
  copyf($tmpplot,'sed g','Plotname: --Not assigned--','',$tmpplot+'2'):$dum
 
//  write('line3','Please wait...')
  if ($uname <> 'Darwin') then
    shell('('+$ps2pdfwr+' '+$tmpplot+'2 '+$tmpplot+'.pdf)'):$dum
  else
    if ($ps2pdfex = 2) then
      shell('('+$ps2pdfwr+' '+$tmpplot+'2 '+$tmpplot+'.pdf)'):$dum
    else
      shell('('+$ps2pdfwr+' '+$tmpplot+'2  -o '+$tmpplot+'.pdf)'):$dum
    endif
  endif
  exists($tmpplot+'.pdf','file'):$isok
  if ($isok<>0) then
      chmod('a+x',$tmpplot+'.pdf')
  endif
  if ($plf<>'pdf') then
    if $plf='ps' then
	copy($tmpplot+'2',$tmpplot+'.'+$plf):$dum
    else
        shell('(convert -density 300 '+$tmpplot+'2 '+$tmpplot+'.'+$plf+')'):$dum
    endif
    delete($tmpplot+'2',''):$dum
  else
    delete($tmpplot+'2',''):$dum
    $tmpplot=$tmpplot+'.pdf'
  endif
  cd($curdir):$dum

  if ($isok=0) then
        write('error','Generation of pdf plot Failed!')
  else
	pageview('popup')
        shell('('+$showCmd+' '+userdir+'/persistence/plotpreviews/'+$tmpplot+') &'):$dum
  endif

endif
"************************************************************"
if ($1='proceed') then

  $plf=pdfplotpar[9]
  if $plf='postscript' then $plf='ps' endif

  $archsamp=pdfplotpar[6]
  $isstudy=0
  $name=''
  substr(pdfplotpar[1],1,1):$fc
  if $fc='/' then
	substr(pdfplotpar[1],'dirname'):$archsamp,$name,$ext
	ckstring($name):$name
  elseif pdfplotpar[1]<>'' then
	substr(pdfplotpar[1],'basename'):$name,$ext
        ckstring($name):$name
  endif
  exists($archsamp+'/dirinfo','directory'):$isstudy
  if $isstudy then $plotdir=$archsamp+'/plots' else $plotdir=$archsamp endif

  exists($plotdir,'directory'):$pldirex
  if not $pldirex then
	mkdir('-p',$plotdir):$dum
	exists($plotdir,'directory'):$pldirex
	if not $pldirex then
	    write('error','Could not create %s',$plotdir)
	    pageview('popup')
	    return
	endif
  endif

  $plotopt=0
  $saveopt=0
  $emailopt=0
  $eaddress=pdfplotpar[2]
  if (pdfplotpar[3]='plot') then $plotopt=1 endif
  if (pdfplotpar[4]='save') then $saveopt=1 endif
  if (pdfplotpar[5]='email') then $emailopt=1 endif
//      if plotname is empty, save and email options are OFF
  if ($name='') then $saveopt=0 $emailopt=0 endif

//      if email flag is off, email option is OFF
  if (emailflag='n') then 
	// write('line3','emailflag is OFF')
	$emailopt=0 
  endif
//	if emailaddress is empty
  if ($eaddress='') and ($emailopt>0) then
	write('error','Please provide email address')
	pageview('popup')
	return
  endif

  $plot=userdir+'/persistence/plotpreviews/'+pdfplotpar[8]
  exists($plot,'file'):$fileex
  exists($plot+'.pdf','file'):$fileex2
  if ($fileex) and ($fileex2) and ($saveopt) then
	exists($plotdir+'/'+$name+'.pdf','file'):$plotex
	if ($plotex=0) and ($plf<>'pdf') then
	    exists($plotdir+'/'+$name+'.'+$plf,'file'):$plotex
	endif
        if ($plotex) then
           write('error','Plot %s already exists. Enter different plot name',$name)
	   pageview('popup')
	   return
	endif
  endif
  if ($fileex) then
	cp($plot,$plot+'_tmp'):$dum
  endif

  if ($fileex) and ($fileex2) and (($saveopt) or ($emailopt)) then
      if $isstudy then
        copyf($plot+'_tmp','sed g','Plotname: --Not assigned','Plotname: '+$name,$plot+'2'):$dum
      else
        copyf($plot+'_tmp','sed g','Plotname: --Not assigned','',$plot+'2'):$dum
      endif
      exists($plot+'2','file'):$fileex3
      if ($fileex3) then
        if ($uname <> 'Darwin') then
  	  shell('('+$ps2pdfwr+' '+$plot+'2 '+$plot+'2.pdf)'):$dum
        else
  	  shell('('+$ps2pdfwr+' '+$plot+'2 -o '+$plot+'2.pdf)'):$dum
        endif
	if ($plf<>'pdf') then
	   if $plf='ps' then
		copy($plot+'2',$plot+'2.'+$plf):$dum
	   else
	   	shell('(convert -density 300 '+$plot+'2 '+$plot+'2.'+$plf+')'):$dum
	   endif
	endif
	exists($plot+'2.pdf','file'):$fileex4
	if ($fileex4) then
		mv($plot+'2',$plot+'_tmp'):$dum
		mv($plot+'2.pdf',$plot+'.pdf'):$dum
		if ($plf<>'pdf') then
		   mv($plot+'2.'+$plf,$plot+'.'+$plf):$dum
		endif
	endif
      endif
  endif

//      send to plotter if plot option is ON
  if ($fileex) and ($plotopt) then
     if ($saveopt=0) and ($emailopt=0) then
        copyf($plot+'_tmp','sed g','Plotname: --Not assigned','',$plot+'2'):$dum
      	exists($plot+'2','file'):$fileex3
      	if ($fileex3) then
                mv($plot+'2',$plot+'_tmp'):$dum
	endif
     endif
     $osplotter=''
     getplottertype(plotter,'osname'):$osplotter
     string2array($osplotter):$osplotter
     array2string($osplotter):$osplotter
     if ($osplotter='') then
       shell('(vnmrplot '+$plot+'_tmp )'):$dum
     else
       shell('(vnmrplot '+$plot+'_tmp "'+ $osplotter+'")'):$dum
     endif
  endif

  if ($fileex2) then
//	save to directory if save option is ON
     $saved=0 $saved2=0
     if ($saveopt) then
	copy($plot+'.pdf',$plotdir+'/'+$name+'.pdf'):$dum
	exists($plotdir+'/'+$name+'.pdf','file'):$saved
	if ($saved) then
    		write('line3','Plot saved as %s/%s.pdf',$plotdir,$name)
	    if $isstudy then
    		cplog($name,'plot','pdf')
                datamirror($plotdir+'/'+$name+'.pdf','plot')
	    endif
	endif
        if ($plf<>'pdf') and (pdfplotpar[10]='y') then
	   copy($plot+'.'+$plf,$plotdir+'/'+$name+'.'+$plf):$dum
	   exists($plotdir+'/'+$name+'.'+$plf,'file'):$saved2
	   if ($saved2) then
                write('line3','Plot saved as %s/%s.%s',$plotdir,$name,$plf)
	      if $isstudy then
                cplog($name,'plot',$plf)
                datamirror($plotdir+'/'+$name+'.'+$plf,'plot')
	      endif
	   endif
	endif
     endif
//	email to emailaddress if email option is ON
     if ($emailopt) then
	pwd:$curdir
	$efile=''
	if ($saved) then
	    $efile=$efile+' '+$name+'.pdf'
	    if ($saved2) then
		$efile=$efile+' '+$name+'.'+$plf
	    endif
	    cd($plotdir):$dum
	    vnmremail($efile,$eaddress)
	else
	    cd(userdir+'/persistence/plotpreviews'):$dum
	    if ($name<>'') then
		cp(pdfplotpar[8]+'.pdf',$name+'.pdf'):$dum
		copy($name+'.pdf','/vnmr/tmp'):$dum
		$efile='/vnmr/tmp/'+$name+'.pdf'
		if ($plf<>'pdf') and (pdfplotpar[10]='y') then
		   cp(pdfplotpar[8]+'.'+$plf,$name+'.'+$plf):$dum
		   copy($name+'.'+$plf,'/vnmr/tmp'):$dum
		   $efile=$efile+' /vnmr/tmp/'+$name+'.'+$plf
		endif
		vnmremail($efile,$eaddress)
	 	delete($name+'.pdf',''):$dum
		if ($plf<>'pdf') and (pdfplotpar[10]='y') then
		   delete($name+'.'+$plf,''):$dum
		endif
	    else
		copy(pdfplotpar[8]+'.pdf','/vnmr/tmp'):$dum
	    	vnmremail('/vnmr/tmp/'+pdfplotpar[8]+'.pdf',$eaddress)
	    endif
	endif
	cd($curdir):$dum
     endif
  endif
  delete($plot+'_tmp',''):$dum
  pdfplotpar[3]=''
  pdfplotpar[4]=''
  pdfplotpar[5]=''
endif
