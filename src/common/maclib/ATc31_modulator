"macro ATc31_modulator"
"ATc31_modulator" "C13 Channel 2 Fine Power Control Test"

if (at_c13tests='n') then ATnext return endif  "optional skipping"

if ($#=0) then
   ATrtp('standard')  
   at_currenttest='c13_modulator_c'
   dn='C13' 
   seqfil='ATrfhomo' 
   pwxlvl=at_pwx90lvlc j=142 pwx=0
   text('C13 channel 2 linear modulator test')
   "proper sfrq/dfrq for on-resonance HDO/13CH3OD is 3.97675 at 20C"
   "observed 13C shift is 0.01675 ppm per degree"

   $dfrqcorrect=sfrq/3.9767612
   dof=($dfrqcorrect-dfrq)*1e6
   dof=dof+(temp-20)*(0.01675*dfrq)   "correct for temperature change"

   lb='n' sb=at/4 s1 av
   wnt=''
   wexp='ATc31_modulator(`PART1`)'
   dtext
   ATcycle au
 
elseif ($1='PART1') then

   wft f vsadj 
   sp=2.8p wp=0.8p vsadj 
   th=100 nll
   getll(1):$int,cr 
   cr=cr-71
   movetof
   axis='h'
   wnt='ATwft select(1) sp=2.8p wp=0.8p vsadj dssh dtext'
   s1
   av
   pwx=0 $pwx360=4*at_pwx90c
   pwx[2]=$pwx360-4,$pwx360-3.6,$pwx360-3.2,$pwx360-2.8,$pwx360-2.4,$pwx360-2,$pwx360-1.6,$pwx360-1.2,$pwx360-.8,$pwx360-.4,$pwx360,$pwx360+.4,$pwx360+.8,$pwx360+1.2,$pwx360+1.6,$pwx360+2,$pwx360+2.4,$pwx360+2.8,$pwx360+3.2,$pwx360+3.6,$pwx360+4
   text('C13 RF homogeneity: 0 degree, 360 array')
   wnt='ATwft select(1) sp=2.8p wp=0.8p vsadj dssh dtext'
   wexp='ATc31_modulator(`PART2`)'
   ATcycle au

elseif ($1='PART2') then

   wft
   sp=2.8p wp=0.8p vsadj dssh dtext
   $counter=1
   select(1) peak:$firstmaxheight $bestindex=$counter
   $counter=2     
   select($counter) peak:$ht $bestheight=$ht $bestindex=$counter
   repeat
      select($counter) 
      peak:$ht 
      if ($ht>$bestheight) then
         $bestheight=$ht                  "finds 360 (next maximum)"
         $bestindex=$counter
      endif
      $counter=$counter+1
   until ($counter>22)
   $pwx360=pwx[$bestindex]

   pwx=$pwx360 "set up a 360 degree pulse for full modulator power"
   exists('dpwrf','parameter'):$e
   if $e=0 then
    create('dpwrf','real')                "controls fine power"
   endif
   array('dpwrf',100,4095,-40)

   wnt='ATwft select(1) sp=2.8p wp=0.8p vsadj dssh dtext'
    text('C13 channel 2 array fine power')
   atext('Vary dpwrf from 4095 down in steps of 40')
   atext('First spectrum is for 360 degree pulse')
   atext('Flip angle decreases from left to right')

   wexp='ATc31_modulator(`PART3`)'
   ATcycle au

elseif ($1='PART3') then

   wft
   sp=2.8p wp=0.8p vsadj dssh dtext
   if ((at_plotauto='y') and (at_printparams='n'))
    then ATpltext(wcmax-95,wc2max-6)
   endif
   ATplot
   ATsvf


   $counter=1
   select(1) peak:$firstpeakheight $bestindex=$counter
   $bestheight=$firstpeakheight
   repeat
      select($counter)
      peak:$ht
      if ($ht<$bestheight) then
         $bestheight=$ht
         $bestindex=$counter
      endif
      $counter=$counter+1
   until ($ht>1.5*$bestheight) or ($counter>arraydim)
   $firstnullindex=$bestindex
   $ratio=$bestheight/$firstpeakheight
   $ratio=trunc($ratio*10000)/10000
   $bestheight?
   $firstpeakheight?
   write('line3','ratio of maximum to minimum = %2.2f',$ratio)
   if (at_printparams='y') then
     pap ATpltext
     pps(120,0,wcmax-120,90) ATpage('C13mod_c_parameters')
   endif
   ATrecord('C13MODc','C13 Linear Modulator','ratio',$ratio)
   write('file',autotestdir+'/REPORT','C13 Linear Modulator: Raio=%2.2f',$ratio)
ATnext
endif
