// Chempacker macro

$t='/vnmr/tmp/version'
rm('-f',$t):$dum
cp('/vnmr/vnmrrev',$t)
chmod('a+w',$t)

$logfile='/vnmr/adm/log/options'
exists($logfile,'file'):$e
if $e then
  $file='/vnmr/adm/options/options'
  exists($file,'file'):$e
endif
if $e then
  $psFile=''
  $psLabel=''
  readfile($file,'$psFile','$psLabel','','local'):$num
  if ($num > 0 ) then
     write('file',$t,'\nOPTIONS:')
     lookup('mfile',$logfile,'filekey'):$key
     $i = 0
     while ($i < $num) do
        $i = $i + 1
        lookup('mfile',$key,'countcs',$psFile[$i]):$found
        if ($found) then
            write('file',$t,'%s',$psLabel[$i])
        endif
     endwhile
  endif
endif

$patch=systemdir+'/adm/patch'
exists($patch,'directory'):$ep
if $ep then 
  write('file',$t,'\nPATCHES:')
  shell('cd '+$patch+'; ls -d * >> '+$t+'; cat'):$e
endif

exists('/etc/redhat-release','file'):$e
if $e then
  write('file',$t,'\nOS:')
  shell('cat /etc/redhat-release >> ' +$t+'; cat'):$e
else
  exists('/etc/os-release','file'):$e
  if $e then
    write('file',$t,'\nOS:')
    shell('. /etc/os-release; echo $PRETTY_NAME'):$s
    write('file',$t,$s)
  else
    getbinpath('sw_vers'):$e
    if $e then
      write('file',$t,'\nOS:')
      write('file',$t,'Name:      MacOS')
      $val=''
      shell('sw_vers --productVersion'):$val
      write('file',$t,'Version:   %s',$val)
    endif
  endif
endif

dousermacro($0,$t)
chmod('a+w',$t)
