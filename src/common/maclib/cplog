"macro cplog"
$tailX='tail'
whichtail:$tailX

$name='' $archsamp=''
if ($# < 2) then return($name,$archsamp) endif
$arg2=$2

/*
isnewexp:$new
if (auto='y') then $new=1 endif
if ($new=0) then
      substr(file,'dirname'):$archsamp
else
      $archsamp=archivedir+'/'+sample
endif
*/

if ($arg2='write') then $arg2='fid' endif

IF (($arg2 <> 'last') and 
    ($arg2 <> 'lastcraft') and
    ($arg2 <> 'iscpfid') and 
    ($arg2 <> 'parud') and 
    ($arg2 <> 'first') and 
    ($arg2 <> 'wrxml')) THEN

  if ($# < 3) then $3=pslabel endif
  if ($# > 3) then $archsamp=$4 endif

  if $archsamp='' then
    isnewexp:$new
    if (auto='y') then $new=1 endif
    if ($new=0) then
      substr(file,'dirname'):$archsamp
    else
      $archsamp=archivedir+'/'+sample
    endif
  endif

  $listfile=$arg2+'list' 
  exists($archsamp+'/dirinfo','directory'):$direx
  if ($direx=0) and ($arg2<>'scout') then
	return($name,$archsamp)
  endif

  if ($arg2 = 'fid') or ($arg2='cmd') or ($arg2='spectrum') or ($arg2='craft') then
     if ($# < 3) then $3=pslabel endif
     $name=$1 $lname=''
     if ($arg2='fid') then
	$fidlog=$archsamp+'/dirinfo/fidlog'
  	write('file',$fidlog,'%s: %s',$3,$name)
	if (appmode='lc') and (auto='y') then
	    $peakfidlog=$archsamp+'/dirinfo/peakfidlog'
	    $peak='' format(lcpeak,0,0):$peak
	    if (lcpeak<100) then $peak='0'+$peak endif
	    if (lcpeak<10) then $peak='0'+$peak endif
	    write('file',$peakfidlog,'%s: %s %s/%s',$peak,$3,$archsamp,$name)
	endif
	$type='LIB'
     elseif ($arg2='cmd') then
	$3=$name
	$name=$name+'_CmdNode_NullPath'
	$type='CMD'
     elseif ($arg2='spectrum') then
	$spectrumlog=$archsamp+'/dirinfo/spectrumlog'
	write('file',$spectrumlog,'%s: %s',$3,$name)
	$type='SPEC'
     elseif ($arg2='craft') then
        $craftlog=$archsamp+'/dirinfo/craftlog'
        write('file',$craftlog,'%s: %s',$3,$name)
        $type='CRFT'
     endif
     iscurrentfid('',archivedir+'/'+sample):$iscurrent
     if (auto='y') or ($iscurrent) then
	$relpath=$archsamp+'/'+$name
	$locdir='' $sloc=''
	getlocid('current'):$sloc,$locdir
        if ($arg2='fid') then
	    if (auto='n') then $sloc='000'
	    else
	       if ($sloc='') then $sloc='0' endif
	       $loc=0 format($sloc,'lower'):$loc
	       if ($loc<100) then $sloc='0'+$sloc endif
	       if ($loc<10) then $sloc='0'+$sloc endif
	    endif
	    if (traymax=96) or (traymax=768) then
		write('fileline',autodir+'/enterQ.macdir/fidlog','"Loc%sz%dr%d',$sloc,vrack,vzone)
	    else
	    	write('fileline',autodir+'/enterQ.macdir/fidlog','"Loc%s',$sloc)
	    endif
	    write('fileline',autodir+'/enterQ.macdir/fidlog','(%s): ',samplename)
	    write('fileline',autodir+'/enterQ.macdir/fidlog','%s" ',$name)
	    write('fileline',autodir+'/enterQ.macdir/fidlog','"%s"',$relpath)
	    write('fileline',autodir+'/enterQ.macdir/fidlog','\n')
	    $explist=explist if ($explist='') then $explist=$3 endif
            write('file',autodir+'/enterQ.macdir/expfidlog','%s %s',$explist,$relpath)
            xmhahasx_add(autodir,'',$relpath,'FidQ')
        elseif ($arg2='cmd') then
	    $explist=$3
            write('file',autodir+'/enterQ.macdir/expfidlog','%s %s',$explist,$relpath)
	endif
	if ($locdir<>'') then
		$activedir=autodir+'/enterQ.macdir/'+$locdir+'/xmldir/Active'
		exists($activedir,'directory'):$direx
		if ($direx=0) then
		    shell('mkdir '+$activedir):$dum
		    exists($activedir,'directory'):$direx
		endif
		if ($direx) then
			write('file',$activedir+'/fidlog','%s %s',$explist,$relpath)
		endif
	endif
     endif
     cplog($archsamp,'wrxml',$name,$type)
  elseif ($arg2='scout') then
     if ($# < 3) then $3=pslabel endif
     $s1='' $ret=0
     strstr($archsamp,'/scoutfids'):$ret,$s1
     if ($ret=0) then return($name,$archsamp) endif
     $archsamp=$s1
     exists($archsamp+'/dirinfo','directory'):$direx
     if ($direx=0) then
        return($name,$archsamp)
     endif
     $name=$1
     $scoutlog=$archsamp+'/dirinfo/scoutlog'
     write('file',$scoutlog,'%s: %s',$3,$name)
     $type='Scout'
     cplog($archsamp,'wrxml',$name,$type)
     return
  endif
  if ($arg2<>'cmd') and ($arg2<>'spectrum') and ($arg2<>'craft') then
     $date=''
     shell('date +%Y-%m-%d'):$date
     $time=''
     shell('date +%H%M%S'):$time
     $username=operator
     if (auto = 'y') then
        $username='auto for '+studyowner
     else
        if (operator <> studyowner) then
	    $username=operator+' for '+studyowner
        endif
     endif
     if ($arg2 <> 'fid') and ($3<>'') then
	$name=$1+'.'+$3
     endif

     $chronentry=$date+' : T'+$time+' : '+$name+' : Saved by : '+$username
     write('file',$archsamp+'/dirinfo/'+$listfile,'%s',$chronentry)
  endif

ELSEIF ($arg2='wrxml') THEN

  if $1='' then
    isnewexp:$new
    if (auto='y') then $new=1 endif
    if ($new=0) then
      substr(file,'dirname'):$archsamp
    else
      $archsamp=archivedir+'/'+sample
    endif

  else
    $archsamp=$1
  endif

	$fids=$3 $scout=0 $spec=0 $craft=0
	if ($# < 4) then $4='LIB' endif
	if ($4='Scout') then $4='LIB' $scout=1 endif
	if ($4='SPEC') then $4='LIB' $spec=1 endif
        if ($4='CRFT') then $4='LIB' $craft=1 endif

	if ($fids='') then return endif
	if ($scout) then 
	    $fidname=$archsamp+'/scoutfids/'+$fids
	elseif ($4='LIB') then
	    $fidname=$archsamp+'/'+$fids
	else
	    $fidname=''
	endif
	$fidex=0 $ext=''
        if ($4='LIB') then
	    exists($fidname,'directory'):$fidex
	    if(not $fidex) then
		if ($spec) then
		  $ext='vfs'
		  exists($fidname+'.'+$ext,'directory'):$fidex
	        else
		  if $craft then
		      $ext='vfs'
                      exists($fidname+'.'+$ext,'directory'):$fidex
		  endif
		  if not $fidex then
		      $ext='fid'
                      exists($fidname+'.'+$ext,'directory'):$fidex
		  endif
                  if(not $fidex) then
		      $ext='REC'
                      exists($fidname+'.'+$ext,'directory'):$fidex
		  endif
                  if(not $fidex) then
		      $ext='rec'
                      exists($fidname+'.'+$ext,'directory'):$fidex
                  endif
		endif
	    endif
        else
                $fidex=1
        endif

        $xmldir=$archsamp+'/dirinfo/xmldir'
	if ($scout) then
	    $xml=$xmldir+'/scoutlog'
	elseif ($spec) then
	    $xml=$xmldir+'/spectrumlog'
	elseif ($craft) then
            $xml=$xmldir+'/craftlog'
	else
            $xml=$xmldir+'/fidlog'
	endif

        $cpnodes=$xmldir+'/cpnodeinfo'
	exists($xmldir,'directory'):$direx
	if ($direx=0) then
		xmhaha_subSQ($archsamp)
		$fidex=0
	endif
	if ($fidex) then
           $xmlcount=0
	   shell('touch '+$xml):$dum
           shell('(cat '+$xml+' | wc -l)'):$xmlcount
	   if ($xmlcount=0) then
		if ($scout) then
		    xmhaha_subSQ($archsamp,'scoutlog')
		else
		    xmhaha_subSQ($archsamp,'fidlog')
		endif
		$fidex=0
	   endif
	endif
	if ($fidex) then
	    $pslabel='' $cpname=''
	    $title=$fids
	    if ($4='LIB') then
		if not $spec and not $craft then
                    rtv($fidname,'pslabel'):$pslabel
		    rtv($fidname,'noabort','explist'):$cpname
		    if ($cpname='') then $cpname=$pslabel endif
		    $path=$fidname
		else
		    substr($fids,'basename'):$cpname,$xt
		    $pslabel=$cpname
		    $path=$fidname+'.'+$ext
		    $title=$fids+'.'+$ext
		endif
	    else
		$cpname=$fids
		strstr($cpname,'_','last'):$ret,$pslabel
		if ($ret=0) then $pslabel=$cpname endif
		$path=''
	    endif
		$tt='' substr($archsamp,'basename'):$tt
        	$atx=1 $attr=''
        	$attr[$atx]='status','Completed' $atx=$atx+2
		if $spec then
		    $attr[$atx]='sqstatus','Spectrum' $atx=$atx+2
		elseif $craft then
                    $attr[$atx]='sqstatus','Craft' $atx=$atx+2
		else
		    $attr[$atx]='sqstatus','Completed' $atx=$atx+2
		endif
        	$attr[$atx]='time','' $atx=$atx+2
        	$attr[$atx]='lock','on' $atx=$atx+2
		$attr[$atx]='CPnode',$cpname $atx=$atx+2
		$attr[$atx]='title',$title $atx=$atx+2
		$attr[$atx]='tooltext',$tt $atx=$atx+2
		$attr[$atx]='data',$path $atx=$atx+2
		$attr[$atx]='type',$4 $atx=$atx+2

		if ($pslabel<>$cpname) and (not $spec) and (not $craft) then
		    write('reset',$cpnodes+'1')
		    shell('(cat '+$cpnodes+' | grep -w Completed > '+$cpnodes+'1)'):$dum
                    shell('(cat '+$cpnodes+' | grep -vw Completed | grep -vw '+$cpname+' >> '+$cpnodes+'1)'):$dum
                    shell('mv '+$cpnodes+'1 '+$cpnodes):$dum
		endif
		$snode=''
		xmhaha_addnode($pslabel,$attr,'',$xml):$snode
		if ($snode<>'') then xmhaha_fullSQ('set',$xmldir) endif
		shell('chmod -R g+w '+$xmldir):$dum
	endif

ELSEIF ($arg2='last') or ($arg2 = 'first') THEN

  if $archsamp='' then
    isnewexp:$new
    if (auto='y') then $new=1 endif
    if ($new=0) then
      substr(file,'dirname'):$archsamp
    else
      $archsamp=archivedir+'/'+sample
    endif
  endif

    if $1='' then
    	$fidlog=$archsamp+'/dirinfo/fidlog'
	exists($fidlog,'file'):$fileex
	if not $fileex then
	    return($1,$archsamp)
	endif
	$s1='' $lname='' $lastname=''
	readfile($fidlog,'$s1','$lname','','local'):$total
	$lastname=$lname[$total]
	return($lastname,$archsamp)
    endif

    $xxx=1 $checktn='' $checkdn=''
    $xi=3
    while ($xi <= $#) do
       $xiStr=''
       format($xi,0,0):$xiStr
       if not typeof('$'+$xiStr) then
	   $xxx=${$xi}
       else
	   if ${$xi} = 'checktn' then
		$xi=$xi+1
		$checktn=${$xi}
	   elseif ${$xi} = 'checkdn' then
		$xi=$xi+1
		$checkdn=${$xi}
	   else
		$archsamp=${$xi}
	   endif
       endif
       $xi=$xi+1
    endwhile
/*
    if ($# > 2) then 
	if typeof('$3') then $archsamp=$3 else $xxx=$3 endif
	if ($# > 3) then
	   if typeof('$4') then $archsamp=$4 else $xxx=$4 endif
	endif
    endif
*/

    $fidlog=$archsamp+'/dirinfo/fidlog'
    $name=$1 $lname='' $total=0 $lastname=''
    exists($fidlog,'file'):$lsfex
    if ($lsfex=0) then
        $fidlog=$archsamp+'/dirinfo/lastfids'
        exists($fidlog,'file'):$lsfex
    endif
    if ($lsfex) then
      shell('(grep -iw -- '+$1+': '+$fidlog+' | wc -l)'):$total
    endif
    if ($total) then
	$i=1 $j=0
	repeat
	   $sx='' format($i,'lower'):$sx
	   if ($arg2='last') then
	      shell('(grep -iw -- '+$1+': '+$fidlog+' | '+$tailX+' -n '+$sx+' | head -n 1)'):$lname
	   else
              shell('(grep -iw -- '+$1+': '+$fidlog+' | head -n '+$sx+' | '+$tailX+' -n 1)'):$lname
	   endif
	   $lastname=''
	   substr($lname,2):$lastname
           substr($lastname,'basename','.fid .REC .rec'):$lastname,$ext
           exists($archsamp+'/'+$lastname+'.REC','directory'):$isthere
           if (not $isthere) then
                exists($archsamp+'/'+$lastname+'.rec','directory'):$isthere
             if (not $isthere) then
                exists($archsamp+'/'+$lastname+'.fid','directory'):$isthere
             endif
           endif
	   if ($isthere) then 
//  ALWAYS evaluate lockfreq against lockfreq_ for a spectrometer
		expactive:$active
		$lockfreq=lockfreq
		if $active>-0.5 then
		    rtv($archsamp+'/'+$lastname,'noabort','lockfreq_'):$lockfreq
		endif
		abs(lockfreq-$lockfreq):$diff
		if ($diff > 0.001) then
		    $lastname=''
		elseif ($checktn<>'') or ($checkdn<>'') then
		    rtv($archsamp+'/'+$lastname,'tn'):$tn
		    rtv($archsamp+'/'+$lastname,'dn'):$dn
		    if ($checktn='') then $checktn=$tn endif
		    if ($checkdn='') then $checkdn=$dn endif
		    if ($tn=$checktn) and ($dn=$checkdn) then $j=$j+1 else $lastname='' endif
		else
		    $j=$j+1
		endif
	   endif
	   if ($j+1 > $xxx) then $i=$total endif
	   $i=$i+1
	until $i > $total
    endif
    $name=$lastname

ELSEIF ($arg2 = 'iscpfid') THEN

    if ($# > 2) then
        if ($3<>'') then $archsamp=$3 endif
    endif
    if $archsamp='' then
    	isnewexp:$new
    	if (auto='y') then $new=1 endif
    	if ($new=0) then
    	  substr(file,'dirname'):$archsamp
    	else
    	  $archsamp=archivedir+'/'+sample
    	endif
    endif

    $fidlog=$archsamp+'/dirinfo/fidlog'
    exists($fidlog,'file'):$lsfex
    if ($lsfex=0) then
	$fidlog=$archsamp+'/dirinfo/lastfids'
	exists($fidlog,'file'):$lsfex
    endif
    if ($lsfex=0) then return(0) endif

    $studypar=$archsamp+'/studypar'
    exists($studypar,'file'):$studyex

    $isthere=0
    shell('(grep -wc -- '+$1+' "'+$fidlog+'")'):$isthere
    if (not $isthere) then
        shell('(grep -wc -- '+$1+'.REC "'+$fidlog+'")'):$isthere
      if (not $isthere) then
        shell('(grep -wc -- '+$1+'.rec "'+$fidlog+'")'):$isthere
        if (not $isthere) then
           shell('(grep -wc -- '+$1+'.fid "'+$fidlog+'")'):$isthere
        endif
      endif
    endif
    if ($isthere) and ($studyex=0) then
	savestudypar($archsamp)
    endif
    return($isthere)

ELSEIF ($arg2 = 'parud') THEN

  if $archsamp='' then
    isnewexp:$new
    if (auto='y') then $new=1 endif
    if ($new=0) then
      substr(file,'dirname'):$archsamp
    else
      $archsamp=archivedir+'/'+sample
    endif
  endif

  $listfile='parupdate'
  exists($archsamp+'/dirinfo','directory'):$direx
  if ($direx=0) then
        return
  endif

  $date=''
  shell('date +%Y-%m-%d'):$date
  $time=''
  shell('date +%H%M%S'):$time
  $username=operator

  write('file',$archsamp+'/dirinfo/'+$listfile,'-------------------------------------')
  $chronentry=$date+'-'+$time+' '+$1+' updated by '+$username+'\@'+systemname
  write('file',$archsamp+'/dirinfo/'+$listfile,'%s',$chronentry)
  write('file',$archsamp+'/dirinfo/'+$listfile,'Login Username: %s',owner)
  shell('cat '+$3+' >> '+$archsamp+'/dirinfo/'+$listfile):$dum

ELSEIF ($arg2='lastcraft') THEN

    $name=''
    substr($1,'dirname'):$d,$b,$e
    $log=$d+'/dirinfo/craftModelsLog'
    $s1='' $s2=''
    readfile($log,'$s1','$s2',$b+':','local'):$tot
    if $tot then
        $dir=$d+'/CRAFT/'+$s2[$tot]+'.cfat'
        exists($dir,'directory'):$direx
	if not $direx then
    	    $dir=$d+'/CRAFT/'+$s2[$tot]+'.crft'
    	    exists($dir,'directory'):$direx
	endif
	if $direx then
	    $name=$dir
	endif
    endif
    $archsamp=$d

ENDIF

    return($name,$archsamp)
  
