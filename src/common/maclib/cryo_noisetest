"macro cryo_noisetest"

"a series of experiments to automate testing of ChilliProbe decoupling noise"
"abort acquisition to stop"


$timefile='/vnmr/tmp/burntime'
$outfile=curexp+'/noisefile'
$outfile2=curexp+'/specnoise'
$vsfactor='/vnmr/tmp/vsfactor'
$strtflg='n'
dsp='n'

beepoff


IF ($#=0) THEN 
	$burntime=40.0 $burnflg='yes'
	input('How many minutes for each burn cycle. 40 is default. ? '):$burntime1
        $t1=typeof('$burntime1')
          if $t1=1 then            " input is sting or CR "
              $burntime=40.0 
          else
              $burntime=$burntime1
          endif
	write('reset',$timefile)
	write('file',$timefile,'%s %3.0f',$burnflg,$burntime)
	cryo_noisetest('TESTC1','START')
        return
ENDIF
	
"NOBURN option added to allow testing noise return with system idle between tests"

IF ($1='NOBURN') THEN 
	$burntime=60.0 $burnflg='no'
	input('How many minutes between each test cycle? 60 is default. '):$burntime
	write('reset',$timefile)
	write('file',$timefile,'%s %3.0f',$burnflg,$burntime)
	cryo_noisetest('TESTC1','START')
        return
ENDIF
	
if ($#=2) then
	if ($2= 'START') then $strtflg='y' endif
endif

IF ($1 = 'TESTC1') THEN
	if ($strtflg='y') then
		setup('H1','D2O') dn='C13'
		if (numrfch>2) then dn2='N15' dm2='nnn' endif
		dm='nnn' dmm='ccc' fn=16000 dmf=8000 
		dpwr=-12
		at=.128 d1=.872 tpwr=-16 pw=0 nt=1 ss=0 pad=0 gain=50 
		wshim='n' fn=16000 in='n' spin='n' alock='n' vs=1000 ai av
		write('reset',$outfile)
		write('reset',$outfile2)
		wexp='cryo_noisetest(\'TESTC1\')'
		au
                return
	endif
	if (dpwr=-12) then 
		write('file',$outfile,'Carbon Decoupling')
		write('file',$outfile,'at= %2.4f dmm= %s ',at,dmm)
		write('file',$outfile,'dpwr     noise ')
		write('file',$outfile2,'Carbon Decoupling')
		write('file',$outfile2,'at= %2.4f dmm= %s ',at,dmm)
		write('file',$outfile2,'dpwr     noise ')

		wft rfl=0 rfp=-sw/2 sp=10 wp=.8*(sw/2) dc peak:$hgt,$dum
		vs=vs*(5/$hgt)
		write('reset',$vsfactor)
		write('file',$vsfactor,'%5.0f ',vs)		
	endif
	noise:$dum1,$dum2,$dum3,$dum4,$noise
	write('file',$outfile,'%2.0f       %2.2f  ',dpwr,$noise)
	wft rfl=0 rfp=-sw/2 sp=10 wp=.8*(sw/2) dc peak:$hgt,$dum
	write('file',$outfile2,'%2.0f      %4.0f ',dpwr,$hgt)

	if (dpwr<48) then
		dpwr=dpwr+2 dm='nny'
		wexp='cryo_noisetest(\'TESTC1\')'
		au
                return
	else 
		cryo_noisetest('TESTC2','START')
	endif
ENDIF


IF ($1 = 'TESTC2') THEN
	if ($strtflg='y') then
		setup('H1','D2O') dn='C13' 
		dm='nnn' dmm='ccw' fn=16000 dmf=8000 
		if (numrfch>2) then dn2='N15' dm2='nnn' endif 
		dpwr=-12
		at=.128 d1=.872 tpwr=-16 pw=0 nt=1 ss=0 pad=0 gain=50
		wshim='n' in='n' spin='n' alock='n' vs=1000 ai av
		lookup('file',$vsfactor)
		lookup('read',1):vs
		wexp='cryo_noisetest(\'TESTC2\')'
		au
                return
	endif
	if (dpwr=-12) then 
		write('file',$outfile,'at= %2.4f dmm= %s  ',at,dmm)
		write('file',$outfile,'dpwr     noise ')
		write('file',$outfile2,'at= %2.4f dmm= %s ',at,dmm)
		write('file',$outfile2,'dpwr     noise ')
	endif
	noise:$dum1,$dum2,$dum3,$dum4,$noise
	write('file',$outfile,'%2.0f       %2.2f  ',dpwr,$noise)
	wft rfl=0 rfp=-sw/2 sp=10 wp=.8*(sw/2) dc peak:$hgt,$dum
	write('file',$outfile2,'%2.0f      %4.0f ',dpwr,$hgt)

	if (dpwr < 48) then
		dpwr=dpwr+2 dm='nny'
		wexp='cryo_noisetest(\'TESTC2\')'
		au
                return
	else
		if (numrfch>2) then
		cryo_noisetest('TESTN1','START')
		else
		cryo_noisetest('DONE')
		endif
	endif
ENDIF


IF ($1 = 'TESTN1') THEN
	if ($strtflg='y') then
		setup('H1','D2O') dn='C13' dn2='N15'
		dm='nnn' dmm2='ccc' fn=16000 dmf2=8000 dm2='nnn' dpwr2=-12
		at=.128 d1=.872 tpwr=-16 pw=0 nt=1 ss=0 pad=0 gain=50
		wshim='n' in='n' spin='n' alock='n' vs=1000 ai av
		lookup('file',$vsfactor)
		lookup('read',1):vs
		wexp='cryo_noisetest(\'TESTN1\')'
		au
                return
	endif
	if (dpwr2=-12) then 
		write('file',$outfile,'Nitrogen Decoupling')
		write('file',$outfile,'at= %2.4f dmm2= %s ',at,dmm2)
		write('file',$outfile,'dpwr2     noise ')
		write('file',$outfile2,'Nitrogen Decoupling')
		write('file',$outfile2,'at= %2.4f dmm2= %s ',at,dmm2)
		write('file',$outfile2,'dpwr     noise ')
	endif
	noise:$dum1,$dum2,$dum3,$dum4,$noise
	write('file',$outfile,'%2.0f       %2.2f  ',dpwr2,$noise)
	wft rfl=0 rfp=-sw/2 sp=10 wp=.8*(sw/2) dc peak:$hgt,$dum
	write('file',$outfile2,'%2.0f      %4.0f ',dpwr2,$hgt)

	if (dpwr2 < 48) then
		dpwr2=dpwr2+2 dm2='nny'
		wexp='cryo_noisetest(\'TESTN1\')'
		au
                return
	else
		cryo_noisetest('TESTN2','START')
	endif
ENDIF


IF ($1 = 'TESTN2') THEN
	if ($strtflg='y') then
		setup('H1','D2O') dn='C13' dn2='N15'
		dm='nnn' dmm2='ccw' fn=16000 dmf2=8000 dm2='nnn' dpwr2=-12
		at=.128 d1=.872 tpwr=-16 pw=0 nt=1 ss=0 pad=0 gain=50
		wshim='n' in='n' spin='n' alock='n' vs=1000 ai av
		lookup('file',$vsfactor)
		lookup('read',1):vs
		wexp='cryo_noisetest(\'TESTN2\')'
		au
                return
	endif
	if (dpwr2=-12) then 
		write('file',$outfile,'at= %2.4f dmm2 %s ',at,dmm2)
		write('file',$outfile,'dpwr2     noise ')
		write('file',$outfile2,'at= %2.4f dmm2= %s ',at,dmm2)
		write('file',$outfile2,'dpwr     noise ')
	endif
	noise:$dum1,$dum2,$dum3,$dum4,$noise
	write('file',$outfile,'%2.0f       %2.2f  ',dpwr2,$noise)
	wft rfl=0 rfp=-sw/2 sp=10 wp=.8*(sw/2) dc  peak:$hgt,$dum
	write('file',$outfile2,'%2.0f      %4.0f ',dpwr2,$hgt)

	if (dpwr2 < 48) then
		dpwr2=dpwr2+2 dm2='nny'
		wexp='cryo_noisetest(\'TESTN2\')'
		au
                return
	else
		cryo_noisetest('DONE')
	endif
ENDIF


" Archive the Results "

IF ($1 = 'DONE') THEN
	$date=''
        systemtime('%d-%b-%Y-%T'):$date

	exists(userdir+'/data','file'):$t
	if ($t=0) then mkdir(userdir+'/data') endif
	exists(userdir+'/data/testlib','file'):$tl
	if ($tl=0) then mkdir(userdir+'/data/testlib') endif
	substr(probe,'basename'):$probe
	$archive=userdir+'/data/testlib/'+$probe+'FID'+$date
	$archive2=userdir+'/data/testlib/'+$probe+'SPEC'+$date

	cp(curexp+'/noisefile',$archive)
	cp(curexp+'/specnoise',$archive2)
	


	f full
	cryo_cnd    "This will display the result"
	sleep(5)
	cryo_cnd('plot')
	cryo_cndspec
	sleep(5)
	cryo_cndspec('plot')	
	cryo_noisetest('BURN')
ENDIF

IF ($1 = 'BURN') THEN
	exists(userdir+'/parlib/cryo_burnin.par','file'):$ex0
	if ($ex0) then
		rtp(userdir+'/parlib/cryo_burnin')
	else
		rtp('/vnmr/parlib/cryo_burnin')
	endif
	dmm='ccg'
	if (numrfch>2) then dn2='N15' dmm2='ccg' dpwr2=40 dm2='nyn' d2=.1 endif
	exists($timefile,'file'):$e
	if ($e=1) then
		lookup('file',$timefile)
		lookup('read',2):$burnflg,$dotime
	else
		$dotime=20
	endif
	beepon
	wexp='cryo_noisetest(\'TESTC1\',\'START\')'
	if ($burnflg='no') then
		pw=0 pwx=0 dm='nnn' dm2='nnn'
		phase=1 gzlvl1=0 gzlvl3=0 hsglvl=0 nullflg='n' sspul='n' 
		d1=2 satmode='nnn' satdly=0 satpwr=-16 mult=0
	endif
	time($dotime)
	au
        return
ENDIF
