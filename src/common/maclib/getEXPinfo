"macro getEXPinfo"
if ($1='') then return endif

$REQexp='','always'
$ACQtime=0
$apptype=''
$tn='' $dn=''
$refsource1=''
exists($1,'parlib'):$parex,$parlib
if ($parex) then
   fread($parlib,'usertree','reset')
   getvalue('REQexp',1,'usertree'):$REQexp[1],$ok
   getvalue('REQexp',2,'usertree'):$REQexp[2],$ok
   getvalue('ACQtime','usertree'):$ACQtime,$ok
   getvalue('apptype','usertree'):$apptype,$ok
   getvalue('tn','usertree'):$tn,$ok
   getvalue('dn','usertree'):$dn,$ok
   getvalue('refsource1','usertree'):$refsource1,$ok
endif
   "***********If ACQtime is zero in parlib, perhaps it is set in the protocols"
   "***********If REQexp[1]='' then check if it is defined in the protocol"
   "******Don't check protocols for apptype"

if ($ACQtime=0) then
   $stime=''
   getattr($1,'action','time'):$stime
   invformattime($stime):$ACQtime
endif
if ($REQexp[1]='') and ($apptype<>'std1D') then
   $required=''
   getattr($1,'action','required'):$required
   $REQexp[1]=$required
endif
// Add a section for default NUS to adjust ACQtime
if $apptype='homo2D' or $apptype='hetero2D' then
   $nusdefault='no'
   getadminfo('nusdefault'):$nusdefault
   if $nusdefault='yes' then $ACQtime=$ACQtime/2 endif
endif
// Add a section for AgileNT to adjust ACQtime
AgileNT('time',$1,$ACQtime):$ACQtime

if ($REQexp[1]='') then $REQexp[2]='' endif

if (appmode='lc') and ($1<>'Autoscout') then
   $required=''
   string2array($REQexp[1]):$required
   teststr('$required','wet1D','local'):$iswet
   if ($iswet) then
      $required[$iswet]='Autoscout Lc11d lc1d wet1D'
      array2string($required):$REQexp[1]
   endif
endif

if ($apptype='std1D') then $tn='' $dn='' endif
return($REQexp,$ACQtime,$apptype,$tn,$dn,$refsource1)
