// Chempacker macro
// Derivative of /vnmr/maclib/digfilt, but all wft calls are replaced with ft

if ($# < 1) then
  write('error','Error - must specify destination experiment number')
  return
endif

$expno = $1
if (typeof('$expno')) then
  write('error','Error - first argument must be destination experiment number')
  return
endif

$args[1] = ''
$twoddc = 0

$i = 1
$numargs = 0

while ($i < $#) do
  $i = $i + 1
  $strval = ${$i}
  if ($strval <> 't2dc') then
    $numargs = $numargs + 1
    $args[$numargs] = $strval
  else
    $twoddc = 1
  endif
endwhile

exists('ni','parameter'):$e
if ($e) then
  if (ni > 1 and $twoddc = 0) then
    $numargs = $numargs + 1
    $args[$numargs] = 'nodc'
  endif
endif

$i = 0
while ($i < $numargs) do
  $i = $i + 1
  write('alpha','%s',$args[$i])
endwhile

if ($numargs = 0) then
  ft('downsamp',$expno)
elseif ($numargs = 1) then
  ft('downsamp',$expno,$args[1])
elseif ($numargs = 2) then
  ft('downsamp',$expno,$args[1],$args[2])
elseif ($numargs = 3) then
  ft('downsamp',$expno,$args[1],$args[2],$args[3])
elseif ($numargs = 4) then
  ft('downsamp',$expno,$args[1],$args[2],$args[3],$args[4])
elseif ($numargs = 5) then
  ft('downsamp',$expno,$args[1],$args[2],$args[3],$args[4],$args[5])
elseif ($numargs > 5) then
  write('error','more than 10 args not supported by macro "digfilt"')
endif
