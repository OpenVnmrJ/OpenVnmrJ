"macro T1Rsetup"
"T1Rsetup - setup a t1rho experiment"

"usage: T1Rsetup(min. t1rho expected, max. t1rho expected, total time (hours))"
     "or T1Rsetup  (in this case, interactive input is invoked)"

"s.l.patt, 15 april 1987"
" I Goljer Nov 2004 added progressive saturation " 
" B Heise Feb 2022 changed to T1rho "

exists('T1Rmin','parameter'):$epar

if $#=3 "expect four arguments"
then "read arguments"
  $mint1r=$1 $maxt1r=$2 $time=$3
elseif ($#=0 and $epar) then
  $mint1r=T1Rmin $maxt1r=T1Rmax $time=T1Rexptime
else "interactive input if three arguments not present"
  input('Enter minimum T1rho expected (s): '):$mint1r
  input('Enter maximum T1rho expected (s): '):$maxt1r
  input('Enter total experiment time (hours): '):$time
endif

if $mint1r=0 then
  write('error','Min T1R must be > 0!') return
endif

"calculate tau values"
$ntaus=1 d2=0 array='d2' "reset array"
d2[1]=$mint1r/8
repeat "set up array"
  $ntaus=$ntaus+1
  d2[$ntaus]=2*d2[$ntaus-1]
until (d2[$ntaus]>3.5*$maxt1r) and ($ntaus>5)

"set pulse sequence parameters"
pw=pw90 p1=pw90 d1=$maxt1r

"set display parameters"
vp=wc2max/2 th=wc2max/8 ai vo=0
"calculate nt based on time desired"
$averagetau=2*d2[$ntaus]/$ntaus
$time_per_transient=$ntaus*(at+d1+$averagetau)
$nt=(($time*3600)/$time_per_transient)+0.5

gain='y'
if $nt>3 then
  $nt=$nt/4 format($nt,0,0):nt nt=4*nt  "make nt a multiple of 4"
else
  format($nt,0,0):nt
endif

if (nt>4 and dsp<>'i' and fsq<>'y') then
  if Console='vnmrs' then il='n' else il='y' endif bs=4
  while bs<nt/8 do
    bs=bs+4
  endwhile
else
  il='n'
endif

$hours=(nt*$time_per_transient)/3600
$hours=trunc($hours)
$minutes=(nt*$time_per_transient)/60 - 60*$hours

if auto='y' then
  write('alpha','Experiment set up using %d tau values',$ntaus)
  write('alpha','Approximate acquisition time %.0f hours %.0f minutes',$hours,$minutes)
else
  write('line3','Using %d tau values, acquisition time approx. %.0f h %.0f min',$ntaus,$hours,$minutes)
endif

