"macro Pobs"
"********************************************************"
"Pobs - Carbon observe   Autocalibration             "
"********************************************************"

mkpar('string','execprocess','','execplot','')
setgroup('execprocess','display')
setgroup('execplot','display')

execprocess='wft dssh darray(\'CalibrationResults\')'
execplot='execplot=\'\''

getlimit('tpwr'):$max,$min,$dbstep
if (console='inova') or (console='mercury') then $dbstep=1 endif
// For now force dbstep to integer or 0.5
if $dbstep>0.5 then $dbstep=trunc($dbstep+0.5) else $dbstep=0.5 endif

is_IP:$isIP
$arg=''
if ($# < 1) then
    if $isIP then
        $arg='1'
    else
    	$tpwr=0
    	getparam('tpwr','P31'):$tpwr
    	if ($tpwr=0) then $arg='1' else $arg='init' endif
    endif
else
    if not typeof('$1') then
    	format($1,0,0):$arg
    else
	$arg=$1
    endif
endif

"*************************************************************"
IF ($arg = '1') THEN

    $file='' $asdir=''
    cplog('PROTON','first'):$file,$asdir
    if ($file<>'') then rt($asdir+'/'+$file) else return endif
    wft
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''

    tn='P31' dn='H1' crl sw=30k tof=0
    $P31pwx=100 getparam('pwx','P31'):$P31pwx
    pslabel='PHOSPHORUS'
    if (macro='P31_calibration') then
	setsw(-40,0) d1=20 
    else
    	setsw(40,15) setoffset(4,dn):dof d1=10 dm='nny' dmm='ccw' setdecpars
    endif
    acqdisp
    setddrtc:$dum
    setref

    tpwr=P31tpwr pw90=P31pw90 pw=pw90
    //dpwr=H1dpwr dmf=H1dmf pplvl=H1pplvl pp=H1pp
    //dm='nnn' dmm='ccc'

    getparam('tpwr'):$power
    getparam('pw90'):$oldpw
    $node='Pobs(3)'
    if ($power <> 0) then
  	$factor = P31pw90/$oldpw
  	ln($factor):$lnfactor
  	$tpwrdiff = trunc(20*0.43429*$lnfactor/$dbstep)*$dbstep
 	if ($tpwrdiff < 3) and ($tpwrdiff > -3) then
  	    if (($power-$tpwrdiff)>63) then
     		$tpwrdiff=$power-63
     		exp($tpwrdiff*2.303/20):$mult
     		P31pw90=P31pw90*$mult
  	    endif
  	    tpwr=$power-$tpwrdiff
	    $node='Pobs(5,P31pw90)'
 	endif
    endif
    
    ai at=1 fn=4*np lb=1/at nt=1 
    if (Console = 'mercury') then gain=20 else gain=30 endif
    wnt='wft f full vsadj'
    caliblist=$0
    xmhaha_calib('synonym',$0):caliblist
    calibnode='_iteration'
    execprocess='Pobs(2)'
    write('line3','_tpwr%g',tpwr):calibnode
    chkname(calibnode,'alnum-'):calibnode
    xmhaha_calib('addNode',$node)
    alock='n' wshim='n' r1=1 r3=1
    clear(2)

"********************************************************"
ELSEIF ($arg = '2') THEN
    f peak:$ht,cr wp=100 sp=cr-50
    execprocess='wft'

"********************************************************"

ELSEIF ($arg = '3') THEN

    cplog('PHOSPHORUS','last'):$file,$asdir
    rt($asdir+'/'+$file)
    wft
    execprocess='wft dssh darray(\'CalibrationResults\')'
    execplot='execplot=\'\''
    getparam('tpwr'):$power
    if (tpwr = $power-3) then    "if the power came from a probe file use finer step"
   	$strtpw=trunc((P31pw90/4) + 0.5)
   	array('pw',20,$strtpw,2)
    else
   	array('pw',40,2,2)
    endif
    vp=wc2max/2
    wnt='wft(`acq`) select(1) vsadj(10) dssh'
    alock='n' wshim='n' r1=1 clear(2)
    write('line3','_tpwr%g_pw90',tpwr):calibnode
    chkname(calibnode,'alnum-'):calibnode
    xmhaha_calib('addNode','Pobs(4)')

"********************************************************"

ELSEIF ($arg = '4') THEN

    cplog('PHOSPHORUS','last'):$file,$asdir
    rt($asdir+'/'+$file)
    wft av 
    execprocess='wft dssh darray(\'CalibrationResults\')'
    execplot='execplot=\'\''
    $counter=1
    repeat
  	select($counter)
  	peak(sp,wp+sp):$ht
  	if $counter=1 then $maxht = $ht $pw90=pw[1] $minht=$ht
	else
     	    if $ht>$maxht then $maxht=$ht $pw90=pw[$counter] endif
	    if $minht>$ht then $minht=$ht endif
  	endif
  	$counter=$counter+1
    until ($counter>arraydim)

    if $minht/$maxht > 0.5 then
        write('error','Autocalibration failed (min/max > 0.5) - determine manually')
	if ($isIP) then
           write('line3','31P pw90 calibration failed, target pulse width not achievable'):$str
	   VPlog('ExpError',$str)
	   atext($str)
	   email('FAIL')
	endif
        abort
    endif

    $line=''
    write('line3','Pobs- tpwr:%g  pw90:%0.3f',tpwr,$pw90):$line
    CalibrationResults=CalibrationResults,$line
    writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

    if ($pw90 > 2*P31pw90) then
  	if (r1 < 2) then
    	    r1 = r1+1
    	    tpwr=tpwr+6
    	    write('line3','_tpwr%g_pw90',tpwr):calibnode
            chkname(calibnode,'alnum-'):calibnode
	    xmhaha_calib('addNode','Pobs(4)')
    	    return
    	else
    	    write('error','Autocalibration failed on second try -- determine manually')
	    if ($isIP) then
               write('line3','31P pw90 calibration failed, target pulse width not achievable'):$str
	       VPlog('ExpError',$str)
	       atext($str)
	       email('FAIL')
	    endif
    	    abort
    	endif
    endif

    PWRcalib($pw90,P31pw90,'tpwr'):$tpwrdiff,$adim,$pwinc
    if ($tpwrdiff=0) then
        Pobs(5a,$pw90)
    else
        tpwr=tpwr+$tpwrdiff
	array('pw',$adim,1,$pwinc)
        clear(2)
        write('line3','_tpwr%g_pw90',tpwr):calibnode
        chkname(calibnode,'alnum-'):calibnode
        xmhaha_calib('addNode','Pobs(6)')
    endif

"********************************************************"

ELSEIF ($arg = '5') or ($arg='5a') THEN

    if ($arg='5') then
    	cplog('PHOSPHORUS','last'):$file,$asdir
    	rt($asdir+'/'+$file)
    	wft av
    	execprocess='wft dssh darray(\'CalibrationResults\')'
    	execplot='execplot=\'\''
    endif
    $pw90=$2
    ds(1)
    array('pw',17,3.2*$pw90,0.1*$pw90)
    clear(2) full
    da d1=10
    r1=1 
    vp=wc2max/2
    wnt='wft(`acq`) select(1) vsadj(10) dssh'
    write('line3','_tpwr%g_pw360',tpwr):calibnode
    chkname(calibnode,'alnum-'):calibnode
    execprocess='Pobs(\'7a\')'

"********************************************************"

ELSEIF ($arg = '6') THEN
    if $# < 2 then $2='' endif
    cplog('PHOSPHORUS','last'):$file,$asdir
    rt($asdir+'/'+$file)
    clear(2)
    wft av 
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    $counter=1
    repeat
  	select($counter)
  	peak(sp,sp+wp):$ht
  	if $counter=1 then $maxht = $ht $pw90=pw[1] 
	else
     	    if $ht>$maxht then $maxht=$ht $pw90=pw[$counter] endif
  	endif
  	$counter=$counter+1
    until ($counter>arraydim)

    $line=''
    write('line3','Pobs- tpwr:%g  pw90:%0.3f',tpwr,$pw90):$line
    CalibrationResults=CalibrationResults,$line
    writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

    select(1)
// if this is the last iteration, STOP !
    PWRcalib($pw90,P31pw90,'tpwr'):$tpwrdiff,$adim,$pwinc
    if $2='last' and $tpwrdiff>0 then $tpwrdiff=0 endif
    if $tpwrdiff=0 then
        Pobs('5a',$pw90)
    else
        tpwr=tpwr+$tpwrdiff
	array('pw',$adim,1,$pwinc)
        clear(2)
        write('line3','_tpwr%g_pw90',tpwr):calibnode
        chkname(calibnode,'alnum-'):calibnode
// if pwrdiff is negative 1 dbstep
// This would be the last iteration
        if $tpwrdiff>=-1*$dbstep and $tpwrdiff<0 then
	    $2='last'
	endif
	$arg2='Pobs(6,`'+$2+'`)'
        xmhaha_calib('addNode',$arg2)
    endif

"********************************************************"
ELSEIF ($arg = '7') or ($arg='7a') THEN

    if ($arg='7') then
    	cplog('PHOSPHORUS','last'):$file,$asdir
    	rt($asdir+'/'+$file)
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    endif
    clear(2)
    wft av
    $counter=1
    repeat
  	select($counter)
  	peak(sp,sp+wp):$ht
  	if $counter=1 then $minht = $ht $pw360=pw[1] 
	else
     	    if $ht<$minht then $minht=$ht $pw360=pw[$counter] endif
        endif
   	$counter=$counter+1
    until $counter>arraydim

    $pw90=$pw360/4.0
    $line=''
    write('line3','Pobs- tpwr:%g  pw90:%0.3f',tpwr,$pw90):$line
    CalibrationResults=CalibrationResults,$line
    if ($arg='7') then
    	writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')
    endif

    if ($pw90 > P31pw90) then
 	if (tpwr < 63) then
		if ($arg='7a') then
		    xmhaha_calib('addNode','Pobs(7)','top')
		else
   		    tpwr=tpwr+$dbstep r1=r1+1
   		    array('pw',17,3.2*$pw90,0.1*$pw90)
		    execprocess='Pobs(\'7a\')'
		endif
   		return
 	endif
    endif

    pw90=$pw90
    $diff = pw[2] - pw[1]
    if ($diff > 1) then
	if ($arg='7a') then
	    xmhaha_calib('addNode','Pobs(7)','top')
	else
 	    array('pw',11,$pw360-4,0.8)
	    execprocess='Pobs(\'7a\')'
	endif
	return
    endif

    $strpw90='' $strtpwr=''
    write('line3','%0.3f',pw90):$strpw90
    write('line3','%g',tpwr):$strtpwr
//    format(pw90,0,1):$strpw90
//    format(tpwr,0,0):$strtpwr
    setparams('pw90',$strpw90)
    setparams('tpwr',$strtpwr)

    if ($arg='7a') then
    	Pobs('finish')
    endif

"********************************************************"

"Pobs: P31pw90 calibration macro "
" options/arguments: "
" (none) : initial setup "
" 'refine' : refine parameters as needed & initial Haupt result "
" 'define' : define tpwr for array "
"  'array' : set up 360 degree pw array "
" 'save90' : process 360 degree pw array, save data, update probe file "


ELSEIF ($arg = 'init') THEN
  " initial setup of P31pw90 calibration "

    $macro=macro
    savesampglobal('cp')
    write('reset','/vnmr/tmp/calibpars')
    $list=''
    module('arraylist','calibpars'):$list
    if $list[1]<>'' then
    	writeparam('/vnmr/tmp/calibpars','$list')
    endif
    is_IP:$isIP
    PHOSPHORUS
    if ($isIP) then IPmodule('get') endif
    getsampglobal('cp')
    fread('/vnmr/tmp/calibpars')
    rm('-f','/vnmr/tmp/calibpars'):$dum
    CalibrationResults=''
    mkpar('string','execprocess','','execplot','')
    setgroup('execprocess','display')
    setgroup('execplot','display')

    execprocess='wft dssh darray(\'CalibrationResults\')'
    execplot='execplot=\'\''

    $solv='' format(solvent,'lower'):$solv

    seqfil='s2pul' vp=0 at=1 expfilt('acq')
    lb=2/at fn=64k pslabel='PHOSPHORUS' dg
    if $macro='P31_calibration' then
    	setsw(0,-40) d1=20 dm='n'
    else
    	setsw(40,15) setoffset(4,dn):dof d1=10 dm='nny' dmm='ccw' setdecpars
    endif

    tpwr=P31tpwr 
    getparam('dpwr','H1'):$dpwr
    if ($dpwr = 0) then           "dpwr calibration not yet done"
  	$cf=1
  	getparam('tpwr','H1'):$tpwr
  	getparam('pw90','H1'):$pw90
  	getparam('tpwr_cf','H1'):$cf
  	$width=250000/(3*sfrq)
  	$mult = $width/($pw90*$cf)
  	ln($mult):$lnmult
  	$attnval = trunc(20*0.43429*$lnmult/$dbstep)*$dbstep
  	exp($attnval*2.303/20):$mult
	$ref_pw90=$pw90*$cf
	$ref_pwr=$tpwr
  	$width=$ref_pw90*$mult
  	$level=$ref_pwr-$attnval
  	dmf=1e6/$width dmfadj(90)
  	dpwr=$level
    endif

    nt = 1  ss = 0 
    ai fn= 4*np  lb=1/at  lp = 0

    getparam('pw90'):$p31pw90
    if $p31pw90 < 0.5 then "calibrations have not been done"
    	pw = 14,28  "values assume tpwr will be at 51 "
    else
    	pw = trunc(20*$p31pw90/3 + 0.5)/10
    	pw = pw, 2*pw
    endif

    wnt='wft dssh'
    alock='n' wshim='n' spin = 0 r1=1 r3=1
    caliblist=$0
    xmhaha_calib('synonym',$0):caliblist
    calibnode='_iteration'
    write('line3','_tpwr%g_pw90',tpwr):calibnode
    chkname(calibnode,'alnum-'):calibnode
    xmhaha_calib('addNode','Pobs(\'refine\')')
    return

ELSEIF ($arg = 'refine') then

    cplog('PHOSPHORUS','last'):$file,$asdir
    rt($asdir+'/'+$file)
    wft f full 
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    select(1)
    aph

    cr = sp + wp/10  delta = wp/10
    dsn:$sn
    if ($sn < 5) then
   	if (pw[1] = 14) then
     	    pw = 20,40  "try larger pulse widths"
   	elseif (nt < 33) then
     	    nt = nt*4
     	    write('line3','S/N too low, nt increased to %d',nt)
   	else
	"to get here pw & nt already increased "
     	    write('line3','S/N too low; P31 pw calibration aborted')
	    abort
   	endif
        write('line3','_tpwr%g_pw90',tpwr):calibnode
        chkname(calibnode,'alnum-'):calibnode
	xmhaha_calib('addNode','Pobs(\'refine\')')
        return
    else
    	" S/N is adequate;  proceed with calibration "
    	dc 
    	aph
    	vsadj(100) 
    	th = 40  nll
    endif
    select(2) dc peak:$hgt

    abortoff
    if ($hgt > 100) then 
   	aph
    endif
    aborton

    fp
    pw_haupt:$pw90

    $line=''
    write('line3','Pobs- tpwr:%g  pw90:%0.3f',tpwr,$pw90):$line
    CalibrationResults=CalibrationResults,$line
    writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

    if $pw90 = 0 then
  	write('error','P31 pw90 calibration failed!')
  	abort
    endif


 "initial estimate may not be reliable"
 "reset pw array & find better estimate at initial tpwr"

    pw = trunc(20*$pw90/3 + 0.5)/10
    pw = pw, 2*pw
    da
    write('line3','_tpwr%g_pw90',tpwr):calibnode
    chkname(calibnode,'alnum-'):calibnode
    xmhaha_calib('addNode','Pobs(\'define\')')

    return

ELSEIF ($arg = 'define') THEN
  " get good pw90 from Haupt array "

    cplog('PHOSPHORUS','last'):$file,$asdir
    rt($asdir+'/'+$file)
    wft f full
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    select(1)
    dc if (console='vnmrs') then aph0 else aph endif vsadj(100)
    th = 40  nll

    fp
    pw_haupt:$pw90

    $line=''
    write('line3','Pobs- tpwr:%g  pw90:%0.3f',tpwr,$pw90):$line
    CalibrationResults=CalibrationResults,$line
    writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

    PWRcalib($pw90,P31pw90,'tpwr'):$tpwrdiff
    if $tpwrdiff=0 then
        Pobs('array',$pw90)
    else
        tpwr=tpwr+$tpwrdiff
        pw = trunc(20*P31pw90/3 + 0.5)/10
        pw = pw, 2*pw
        da
        write('line3','_tpwr%g_pw90',tpwr):calibnode
        chkname(calibnode,'alnum-'):calibnode
        xmhaha_calib('addNode','Pobs(\'define\')')
    endif

ELSEIF ($arg = 'array') then

    if ($# < 2) then
  	$pw90 = P31pw90
    else
  	$pw90 = $2
    endif

    $pw15=trunc(10*$pw90/6 + 0.5)/10
    ds(1) aph

    array('pw',9,20*$pw15,$pw15) "from 300 to 420 degrees, in 15 degree steps "
    clear(2) full vp = wc2max/2 - 10  
    wnt = 'wft dssh(`dodc`)'  
    if (nt < 2) then nt = 2 endif
    write('line3','_tpwr%g_pw360',tpwr):calibnode
    chkname(calibnode,'alnum-'):calibnode
    execprocess='Pobs(\'save90\')'
    return

ELSEIF ($arg='save90') then

    "process 360 degree pulse array, update P31pw90 & P31tpwr"
    wft
    select(1) f full dc  cr = sp+40 delta=40 crmode = 'b'
    vsadj(0.25*wc2max)  dssh('dodc')

    dsn:$sn,$noise
    $noise = $noise * vs  "scale for comparison with peak heights"

    $i = 2
    repeat
  	select($i)  dc
  	$i = $i+1
    until ($i > celem)
    fp
    $file = curexp+'/fp.out'
    $key=''
    lookup('mfile',$file,'seek','of','read','skip',6,'read',1,'filekey'):$filines,$nspecs,$key
    if ($nspecs <> celem) then
    	write('error','You must run \"fp\" before using %s',$0)
    	return(0)
    endif
    lookup('mfile',$key,'seek','(mm)','filekey'):$key
    $nlines = size('llfrq')
    if ($filines <> $nlines) then
    	write('error','You must run \"fp\" before using %s',$0)
    	return(0)
    endif

    $j = 1 $k = 0  $pw360 = 0
    repeat  "for each line"
  	$i = 1  $hgt = 0
  	repeat  "in each spectrum"
     	    lookup('mfile',$key,'skip',2,'read','filekey'):$hgt[$i],$key
     	    $i = $i+1
  	until($i > celem)

" scan intensity array, looking for the last negative & first positive "
" peak intensities above the noise level "

  	$i = 0
  	repeat
    	    $i = $i+1
  	until ($hgt[$i] > -2*$noise) or ($i = (celem-1))
  	$lastneg = $i-1

  	repeat
    	    $i = $i+1
  	until ($hgt[$i] > 2*$noise) or ($i = celem)
  	$firstpos = $i

  	if ($hgt[$firstpos] > 2*$noise) then 
  " calculate 360 degree pulse width by linear interpolation "
    	    $frac = -$hgt[$lastneg]/($hgt[$firstpos] - $hgt[$lastneg])
    	    $pw360 =  $pw360 + pw[$lastneg] + $frac*(pw[$firstpos] - pw[$lastneg])
    	    $k = $k+1
  	else
    " didn't find a reliable positive peak "
    	    write('error',' unreliable result in P31 pw90 calibration!')
    	    return
  	endif

    	$j = $j+1  "continue with next line"
    until ($j > $nlines)

    if ($k < 0.5) then
  	write('error','P31 autocalibration failed!')
  	abort
    endif

  	pw90 = 0.25 * $pw360/$k
  	$strpw90='' $strtpwr=''  $strp31pw90 = ''
	write('line3','%0.3f',pw90):$strpw90
	write('line3','%g',tpwr):$strtpwr
//  	format(pw90+0.024,0,1):$strpw90
//  	format(tpwr,0,0):$strtpwr
  	format(P31pw90 + 0.049,0,1):$strp31pw90
  	setparams('pw90',$strpw90)
  	setparams('tpwr',$strtpwr)


    $line=''
    write('line3','Pobs- tpwr:%g  pw90:%0.3f',tpwr,pw90):$line
    CalibrationResults=CalibrationResults,$line
    Pobs('finish')

ELSEIF ($arg='finish') THEN

    $CalibrationResults=CalibrationResults
    tmpsave('Pobs')
        $strpw90='' $strtpwr=''  $strp31pw90 = ''
        write('line3','%0.3f',pw90):$strpw90
        write('line3','%g',tpwr):$strtpwr
//        format(pw90+0.024,0,1):$strpw90
//        format(tpwr,0,0):$strtpwr
        format(P31pw90 + 0.049,0,1):$strp31pw90
    	nt=1 lb=5/at 
    	wnt='wft select(1) f peak:$ht,cr wp=100 sp=cr-50 dssh'
    	$text='P31 obs. 90 degree pw: '+$strpw90
    	$atext='  at power level: '+$strtpwr
    	$atext2 = 'Requested value was '+$strp31pw90
    	atext($text)
    	atext($atext)
    	atext($atext2)

	$ep=''
	CalibrationResults=$CalibrationResults

        is_IP:$isIP
        // Test for VeriPulse environment
        if ($isIP) then
	   $cmd=''
	   exists('VParcingtest','maclib'):$arcex
	   if ($arcex) then
	       if ($isIP) then $cmd='VParcingtest' endif
	   endif
           $pw90s='' $tpwrs=''
           format(pw90,1,1):$pw90s
           format(tpwr,1,0):$tpwrs
	   VPgettarget('P31pw90'):$target
           $IPteststatus='FAIL'
           if (pw90<=$target) then $IPteststatus='PASS' endif

	   //Add the sample info to the text file
	   VPtext
           write('line3','31P 90-degree pulse width \(direct\)'):$line
           atext($line)
           CalibrationResults=$line
           write('line3','pw90: %.1f us at pulse power level %.1f dB',pw90,tpwr):$line
           atext($line)
           CalibrationResults=CalibrationResults,$line
           write('line3','Target <= %.1f us',$target):$line
           atext($line)
           CalibrationResults=CalibrationResults,$line
           write('line3','Test status: %s',$IPteststatus):$line
           atext($line)

	   //Add amp power information if calibration table exists
	   exists('VPgetpower','maclib'):$vpex
	   if ($vpex) then
	      VPgetpower('check'):$pwrtabex
	      if ($pwrtabex) then
   		   VPgetpower(VPpowertabpath,'P31','direct',tpwr,pw90,$target):$atext
	           atext($atext)
	      endif
	   endif

           write('line3','wft wp=%0.1f sp=%0.1f select(9) ph aph0 vsadj(50) %s dssh VPhistory(%.2f,%.2f) rmh',wp,sp,$cmd,pw90,tpwr):$ep
	   wstart='eOptions=IPeOptions'
	   profileflg='y'
    	   $pwinc=pw90/7  //sandra modified
           format($pwinc,0,1):$pwinc //sandra modified
           array('pw',35,$pwinc,$pwinc) //sandra modified
    	   execplot='calibplot'
	else
    	   execplot='cpplss'
           write('line3','wft wp=%0.1f sp=%0.1f select(3) ph aph0 vsadj(50) dssh',wp,sp):$ep
    	   $pwinc= trunc((pw90/3)+0.5)
    	   array('pw',20,$pwinc,$pwinc)
           if (Console = 'mercury') then gain=20 else gain=30 endif
        endif

    	execprocess=$ep+' darray(\'CalibrationResults\')'
	calibnode='_PWnutation'
	xmhaha_calib('savePar','P31pw90')
        tmpreturn('Pobs')

ENDIF
