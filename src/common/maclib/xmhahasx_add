"macro xmhahasx_add"
"***************************************************************"
" arg1 - autodir path						"
" arg2 - locdir							"
" arg3 - subxml file to be added from locdir			"
" arg4 - destination file					"
"***************************************************************"
if ($# < 4) then 
   return
endif

$2size=size('$2')
$3size=size('$3')
$4size=size('$4')
if ($2size<>$3size) or ($2size<>$4size) or ($3size<>$4size) then
	write('error','Arg 2, 3 and 4 do not have same size')
	return
endif

$autodir=$1
$firstchar=''
substr($autodir,1,1):$firstchar
if ($firstchar<>'/') then
   if (cpglobalauto<>'') then
        $autodir=cpglobalauto+'/'+$autodir
   else
        $autodir=userdir+'/Automation/'+$autodir
   endif
endif
exists($autodir,'directory'):$direx
if ($direx=0) then
   write('line3','Directory %s not found',$1)
   return
endif
"**************************************************************"
//  Always rebuild location xml file
$i=1
repeat
   if ($2[$i]<>'') then
   	xmhaha_subautoSQ($autodir,'',$2[$i])
   endif
   $i=$i+1
until $i > $2size

"**************************************************************"
exists($autodir+'/enterQ.macdir','directory'):$direx
if ($direx=0) then
   $st=0 $stx=''
   strstr($autodir,'.macdir/studytray'):$st,$stx
   if ($st) then
	substr($stx,'dirname'):$autodir
   endif
   substr($autodir,'basename'):$Qname
else
   $Qname='enterQ'
endif
$xmlinfodir=$autodir+'/'+$Qname+'.macdir/xmldir/info'
$tmpfile=userdir+'/persistence/autoqtmp'
$xmldir=$autodir+'/'+$Qname+'.macdir/xmldir'
$sqdir=$xmldir+'/sqdir'
$sqinfo=$sqdir+'/info'

// Build everything if the sqdir is not found
exists($sqinfo,'directory'):$direx
if ($direx=0) then
        xmhaha_asx($autodir)
        return
endif

$4u=''
sort($4,'u'):$index,$4u
$4usize=size('$4u')
if ($4u[1]='') then return endif
$z=1
repeat
   $queue=$sqdir+'/'+$4u[$z]+'.xml'
   $esq=$sqdir+'/'+$4u[$z]+'_SQ'
   exists($queue,'file'):$fileex
   if ($fileex) then
        append($queue,'|wc'):$totlines
        if ($totlines<2) then $fileex=0 endif
   endif
   if ($fileex=0) then
		// Build the entire queue file if not found
        xmhaha_asx($autodir,$4u[$z])
   else
		// Create a tmp file without the grandpa node
   	write('reset',$queue+'tmp')
   	$tail=$totlines-2
   	$head=$totlines-1
   	copyf($queue,'head',$head,'tail',$tail,$queue+'tmp'):$dum
	$x=1
	repeat
	  if ($4[$x]=$4u[$z]) then
	     if ($4u[$z]<>'FidQ') then
	    	$locdir=$autodir+'/'+$Qname+'.macdir/'+$2[$x]
	    	$subxml=$locdir+'/xmldir/'+$3[$x]+'.xml'
	    	$cpnodes=$locdir+'/xmldir/'+$3[$x]+'_SQ'
	    	exists($subxml,'file'):$fileex2
	    	if ($fileex2=0) and ($4u[$z]='ErrorQ') then
		    $subxml=$locdir+'/xmldir/LocInfo.xml'
		    $cpnodes=$locdir+'/xmldir/LocInfo_SQ'
		    exists($subxml,'file'):$fileex2
	    	endif
	    	if ($fileex2) then
   		    append($subxml,$queue+'tmp'):$dum
   		    $s1='' $s2=''
   		    touch($cpnodes):$dum
   		    readfile($cpnodes,'$s1','$s2','','local'):$nodetot
   		    if ($nodetot) then
    		    	$j=1
     		    	repeat
   		            cp('-r',$xmlinfodir+'/'+$s1[$j],$sqinfo):$dum
  		            $j=$j+1
   		    	until $j > $nodetot
   		    endif
   		    append($cpnodes,$esq):$dum
	    	endif
	     else
		$fidname=$3[$x]
		$fc='' substr($fidname,1,1):$fc
		if ($fc<>'/') then $fidname=userdir+'/data/'+$fidname endif
                $ext='.REC'
		exists($fidname+$ext,'directory'):$fidex
		if ($fidex=0) then
                    $ext='.rec'
		    exists($fidname+$ext,'directory'):$fidex
		endif
		if ($fidex=0) then
                    $ext=''
            	    exists($fidname+'.fid','directory'):$fidex
		endif
            	write('reset',$sqdir+'/cpnodeinfo')
            	if ($fidex) then
                    $pslabel='' $cpname='' $studyowner='' $samplename=''
                    fread($fidname+$ext,'usertree','reset')
		    getvalue('explist','usertree'):$cpname,$dum
               	    getvalue('pslabel','usertree'):$pslabel,$dum
		    getvalue('samplename','usertree'):$samplename,$dum
		    if ($cpname='') then $cpname=$pslabel endif
                    getvalue('studyowner','usertree'):$studyowner,$dum
                    if ($studyowner='') then
                   	getvalue('operator_','usertree'):$studyowner,$dum
                    endif
		    $loc='' 
		    getvalue('loc_','usertree'):$loc,$dum
		    if ($loc<>'') and ($loc<>'0') then $loc='loc'+$loc+'_' else $loc='' endif
		    $sample='' substr($fidname,'dirname'):$sample
		    substr($sample,'basename'):$sample
		    if ($samplename='') then $samplename=$sample endif
                    $path=$fidname
                    substr($fidname,'basename'):$title
                    $atx=1 $attr=''
                    $attr[$atx]='status','Completed' $atx=$atx+2
                    $attr[$atx]='sqstatus','Completed' $atx=$atx+2
                    $attr[$atx]='time','' $atx=$atx+2
                    $attr[$atx]='lock','on' $atx=$atx+2
                    $attr[$atx]='CPnode',$cpname $atx=$atx+2
                    $attr[$atx]='title',$title $atx=$atx+2
		    $tt=$loc+$studyowner+' '+$samplename
                    $attr[$atx]='tooltext',$tt $atx=$atx+2
                    $attr[$atx]='data',$path $atx=$atx+2
                    $snode=''
                    xmhaha_addnode($pslabel,$attr,'',$queue+'tmp'):$snode
                    if ($snode<>'') then
                        append($sqdir+'/cpnodeinfo','awk','$1 $2 $3',$esq):$dum
                   	write('reset',$sqdir+'/cpnodeinfo')
                   	cp('-r',$xmlinfodir+'/'+$snode,$sqinfo):$dum
                    endif
                endif
                delete($sqdir+'/cpnodeinfo',''):$dum
	     endif
	  endif
	    $x=$x+1
	until $x > $2size
		// Now close the queue xml file
        copyf($esq,'grep -v','^Qn',$esq+'tmp'):$dum
   	xmhahasx_Qnodes($queue)
   	append($queue+'tmp',$queue):$dum
   	write('file',$queue,'  </protocol>')
   	delete($queue+'tmp',''):$dum
	append($esq+'tmp',$esq):$dum
	delete($esq+'tmp',''):$dum
   endif
   $z=$z+1
until $z>$4usize

