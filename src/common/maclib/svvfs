"macro svvfs"

if ($# < 2) then $2='' endif
if ($# < 3) then $3='' endif
$fc='' substr($1,1,1):$fc
if $fc<>'/' then
    pwd:$curdir
    $1=$curdir+'/'+$1
endif

$b='' $e='' $d=''
substr($1,'dirname'):$d,$b,$e
$1=$d+'/'+$b+'.vfs'
exists($1,'directory'):$direx
if ($direx) then
    if ($2='force') then $confirm=1
    else
    	$confirm=0
    	write('line3','%s exists. overwrite?',$1):$conf
    	confirm($conf):$confirm
    endif
    if not $confirm then return endif
    rm('-rf',$1):$dum
endif
exists($1,'directory'):$direx
if $direx then
    write('error','%s could not be overwritten',$1)
    return
endif

    // Use svf/svp to make sure the procpar 
    // is based on appropriate acquisition/processing/display groups
exists(curexp+'/acqfil/fid','file'):$fidex
if ($3='fid') and $fidex then
    Svfname($d+'/'+$b+'_%R2%','.fid','.fid, .par'):$tmpfid
    svf($tmpfid,'nodb')
else
    Svfname($d+'/'+$b+'_%R2%','.par','.fid, .par'):$tmpfid
    svp($tmpfid,'nodb')
endif
rename($tmpfid,$1):$dum

flush
mkdir($1+'/datdir'):$dum
mkdir($1+'/acqfil'):$dum
copy(curexp+'/curpar',$1):$dum
copy(curexp+'/datdir/data',$1+'/datdir'):$dum
copy(curexp+'/datdir/phasefile',$1+'/datdir'):$dum
if ($3='fid') then
    exists($1+'/fid','file'):$fidex
    if $fidex then
	rename($1+'/fid',$1+'/acqfil/fid'):$dum
    endif
    exists($1+'/sampling.sch','file'):$schex
    if $schex then
	cp($1+'/sampling.sch',$1+'/acqfil/sampling.sch'):$dum
    endif
else
    rm('-f',$1+'/fid'):$dum
    rm('-f',$1+'/sampling.sch'):$dum
endif
return(1)
