"macro Augmap"
"********************************************************"
"Augmap - Autogradient map generation macro            "
"********************************************************"

$gradtype=gradtype
exists('tmpgradtype','parameter'):$e
if ($e) then is_postvj22b:$e endif
if ($e) then $gradtype=tmpgradtype endif
$d3optlk=0,0.25
if (h1freq<450) then $d3optlk=0,0.18 endif
getparam('gradient','Probe'):$probegradient
if $probegradient='n' then $gradtype='nnh' endif
substr($gradtype,3,1):$zgradtype


$style='db'
getparam('style','Probe'):$style

IF ($1 = 0) THEN

if ($# < 2) then $2='lk' endif
$pfgon3=''
substr(pfgon,3,1):$pfgon3
if ($pfgon3='n') then pfgon='nny' endif

$dn=dn

groupcopy('current','processed','acquisition')
groupcopy('current','processed','display')
groupcopy('current','processed','processing')

gmapsys

$dum=''
beepoff
if ($2 = 'lk') then
 gmapsys('setH2'):$dum
  if $style='db' then nt=4 else nt=1 endif
  d1=2 pw=300 tpwr=38
  getparam('tpwr','lk'):tpwr
  getparam('pw90','lk'):pw
  if (tpwr=0) then tpwr=38 endif
  if (pw=0) then pw=300 endif
elseif ($2 = 'H1') then
  gmapsys('setH1'):$dum
  nt=1 pw=0.5 d1=5
elseif ($2 = 'hs') then
  gmapsys('setHSforH2'):$dum
  nt=2 d1=5 pw=300 tpwr=38
  getparam('tpwr','lk'):tpwr
  getparam('pw90','lk'):pw
  if (tpwr=0) then tpwr=38 endif
  if (pw=0) then pw=300 endif
else
  gmapsys('quit'):$dum
endif
beepon

$tn=$2 if $tn='hs' then $tn='lk' endif
sb=at/3 sbs=sb/2 gf='n' lb='n' awc='n'
tn=$tn d3=0 r1=1
if (tn <> 'H1') then dn='H1' else dn=$dn endif

if ($2 <> 'hs') then
  $size=16
  getparam('rfsize','Probe'):$size
  $size=$size/10
  if ($size=0) then $size=1.6 endif
  getgamma(tn):$gamma
  $gammabar=$gamma/(2*3.1416*10000)
//  $gammabar=(sfrq*1000000)/(234.94*h1freq)
	"Find GperCM that will give a profile with half the sw"
  $gpercm=(sw/($gammabar*$size))/2
  format($gpercm,0,1):$gpercm
  fixgrd($gpercm):gzlvl
endif

if (spintype<>'tach') then spin=0 endif
if (Console = 'mercury') then gain=0 else gain=20 endif
if ($2 = 'H1') then gain=0 
  if (Console = 'mercury') then pw=0.2 endif
endif

module('add','ChemPack','basic')
PFGflg='y'

mkpar('real','vtcomplvl',0)
vtcomplvl=0

wnt='if auto=\'n\' then wft f full dssh endif'

if ($tn='lk') then
  seqfil='s2pul' p1=0 nt=1 np=np*8 
  exists('proc1','parameter'):$pon
  if ($pon>0) then proc1='ft' endif
  sinebell
  if ($2 = 'hs') then
    sw=sw*8 
    if (dsp <> 'n') then oversamp=1 endif
  endif
  wexp='Augmap(1)'
  au('next','wait')
  banner('finding offset')
else
  Augmap(1)
endif


ENDIF

"********************************************************"
IF ($1 = 1) THEN

 if (tn = 'lk') then
   wft('nodc') setref f full av peak:$ht,cr movetof
   if $style='db' then nt=4 else nt=1 endif
   np=np/8 sb=at/3 sbs=sb/2
   if ($zgradtype='h') then
     sw=sw/8 
     if (dsp <> 'n') then oversamp=1 endif
   endif
   seqfil='gmapz'
 endif

 if ($zgradtype='h') then
   Augmap(5)
 else
   wexp='Augmap(2)'
   au('next','wait')
   banner('Optimizing gradient level')
 endif
 
ENDIF

"********************************************************"
IF ($1 = 2) THEN

wft f full
ai vsadj(100)
"peak:$ht,cr "
rfl=sw/2 rfp=0 cr=0
dres(cr,.1):$width,$hzpp,$f1,$f2
if (r1 > 1) then
 if (($width < 0.25*sw) or ($width > 0.75*sw)) then
  printon
  write('alpha','Gradient map failed')
  write('alpha','Check PFG amplifier and pfgon parameter')
  write('alpha','Gradient map generation aborted')
  printoff
  gmapsys('quit')
  return
 endif
else
 if ($width=0) then $width = sw endif
 if (tn='H1') then
    gzlvl=gzlvl*sw*0.5/$width
 else
    gzlvl=gzlvl*sw*0.4/$width
 endif
 r1=r1+1
 au('next','wait') 
 banner('Optimizing gradient level')
 return
endif

$date=''
shell('date +%Y-%m-%d'):$date
if (tn = 'lk') then 
 d3=$d3optlk
else 
 d3=0,.005 
endif
ss=1
$mapname=probe+'_'+tn+'_'+$date
$ok=''
gmaplistfunc('wrshimcal',$mapname):$ok

gmapsys('findgzwin')
wexp=wexp+' Augmap(5)'
banner('Optimizing window and offset')

ENDIF

"********************************************************"

IF ($1 = 3) THEN

if $style='db' then gzsize=4 else gzsize=5 endif
gmap_findtof='n'

if (tn = 'H1') then
    if shimset=4 or shimset=7 or shimset=17 or shimset=20 then gzsize=5 endif
    if shimset=5 or shimset=9 then gzsize=5 endif
else
  if ($zgradtype = 'h') then
    gmap_findtof='y'
    gzwin=gzwin/1.1
  else
    gzlvl = 1.1* gzlvl
  endif
endif
gmapsys('shimmap','overwrite')
wexp=wexp+' Augmap(4)' 
banner('Making Gradient map')

ENDIF

"********************************************************"

IF ($1 = 4) THEN

$mapname=''
gmapshim('mapname'):$mapname
if ($zgradtype='h') then $tn='hs' else $tn=tn endif
$text=$tn+' Gradient map for the probe: '+probe
$atext='   Mapname : '+$mapname
text($text) atext($atext)
gmaprep('explmenu','ebase')
dtext
if (tn <> 'H1' and pltopt='y') then
   pltext(80,wc2max*0.98)
   gmaprep('explmenu','pbase')
endif
$dum=''
$ffile = userdir+'/gshimlib/shimmaps/'+$mapname+'.fid'

    exists('/vnmr/vnmrrev','file','rw'):$owner
    if ($owner>0.5) then
      exists(systemdir+'/gshimlib','file'):$xx
      if ($xx < 0.5) then
       mkdir(systemdir+'/gshimlib')
       mkdir(systemdir+'/gshimlib/shimmaps')
      else
       exists(systemdir+'/gshimlib/shimmaps','file'):$xy
       if ($xy < 0.5) then
        mkdir(systemdir+'/gshimlib/shimmaps')
       endif
      endif

      $tfile = systemdir+'/gshimlib/shimmaps'
      $cmd = 'cp -r '+$ffile+' '+$tfile
      shell($cmd):$dum
    endif

if $zgradtype='h' then $tn='hs' else $tn=tn endif

$Gmap=$tn+'map'
if (probe <> '') then
  setparams($Gmap,$mapname,'Probe')
endif
$tpwrs='' $pw90s=''
format(tpwr,0,0):$tpwrs
format(pw,0,1):$pw90s
$tn=tn
gmapsys('quit')
dsp=dsp

exists('explist','parameter'):$explistex
if ($explistex) then $explist=explist else $explist='' endif

if ($explist = 'findz0') then

 addlknuc
 setlkpars(lockpower,lockgain,lockphase)
 setparams('tpwr',$tpwrs,'lk')
 setparams('pw90',$pw90s,'lk')
 explist='' wshim='g'
 Aufindz0(1)

elseif (explist = 'findz0ifc') then

 addlknuc
 setlkpars(lockpower,lockgain,lockphase)
 setparams('tpwr',$tpwrs,'lk')
 setparams('pw90',$pw90s,'lk')
 explist='' wshim='g'
 Aufindz0ifc(1)

else

if (probe <> '') then
 if ($tn='lk') then
   wexp='Augmap(7,`'+$tn+'`)' wshim='g'
   if ($zgradtype='h') then 
     gmapshim_au('hsshim','glideau')
   else
     gmapshim_au('lkshim','glideau')
   endif
 else
   wexp='banner(`DONE`)' wshim='g'
   gmapshim_au('H1shim')
 endif
else
 gmapshim
endif
endif

ENDIF

"********************************************************"

IF ($1 = 5) THEN

if (tn <> 'lk') then
 Augmap(3)
 return
endif
seqfil='s2pul'
d3=0 pw=0 nt=1 tpwr=0
getparam('pw90','lk'):pw
getparam('tpwr','lk'):tpwr
if (tpwr=0) then 
  if (pw=0) then pw=300 endif
  tpwr=38 
  array('tpwr',15,tpwr-7,1)
  wexp='Augmap(6)'
  au('next','wait')
  banner('Optimizing pulse width')
else
  full
  d3=$d3optlk seqfil='gmapz'
  if ($zgradtype='h') then
   p1=2*pw nt=2
   Augmap(8)
  else
   if $style='db' then nt=4 else nt=1 endif
   Augmap(3)
  endif
endif

ENDIF

"********************************************************"

IF ($1 = 6) THEN

beepoff
wft av f full ds(1) vsadj(100)
$counter=1
repeat
  select($counter)
  peak:$ht
  if $counter=1 then $maxht = $ht $tpwr=tpwr[1] else
     if $ht>$maxht then $maxht=$ht $tpwr=tpwr[$counter] 
     else $counter=arraydim
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)

ds(1) full
tpwr=$tpwr 
d3=$d3optlk seqfil='gmapz'
if ($zgradtype='h') then
 p1=2*pw nt=2 
 Augmap(8)
else
 if $style='db' then nt=4 else nt=1 endif
 Augmap(3)
endif
beepon

ENDIF

"********************************************************"

IF ($1 = 7) THEN

  $date=''
  shell('date +%Y-%m-%d_%H%M%S'):$date
  
  wft 
  if ($zgradtype='h') then
    $tn='hs'
  else
    $tn=$2
  endif

  $probepath='' $pex=0 $appdir=''
  exists(probe,'probes/'+probe):$pex,$probepath
  strstr($probepath,'/probes/'):$pex,$appdir
  exists($appdir+'/shims/'+probe+'_'+$tn+'_d2o','file'):$shimex
  if ($shimex) then
       shell('rm '+$appdir+'/shims/'+probe+'_'+$tn+'_d2o')
  endif
  exists($appdir+'/shims','directory'):$shimdirex
  if ($shimdirex=0) then
       shell('mkdir '+$appdir+'/shims'):$dum
  endif
  svs($appdir+'/shims/'+probe+'_'+$tn+'_d2o')
  svs($appdir+'/shims/'+probe+'_'+$tn+'_d2o_'+$date)

  banner('DONE')

ENDIF
"********************************************************"
IF ($1 = 8) THEN

$date=''
shell('date +%Y-%m-%d'):$date
d3=$d3optlk
ss=1

$mapname=probe+'_hs_'+$date
$ok=''
gmaplistfunc('wrshimcal',$mapname):$ok

gmapsys('findgzwin')
wexp=wexp+' Augmap(3)'
banner('Optimizing window and offset')

ENDIF
