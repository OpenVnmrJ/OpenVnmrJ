"macro dssi"

// dssi:$dim
// this does nothing but return number of traces.

//  One keyword argument string argument (voup or vodown or vo0 or all or select).  
//	The voup or vodown argument
//	increase/decrease vo by 10% or 1 whichever is more 
//	and reexecutes this macro without argument

//	The vo0 will set vo=0 and call this macro again

//	The all argument is same as dssi(1)

//	The select argument will take all the subsequence trace numbers (arguments)
//	and display only those traces (example:  dssi('select',1,3,5,6) will display
//	trace numbers 1, 3, 5 and 6 only

//	The unselect argument will work inverse of select argument
//	(example: dssi('unselect',1,3,5) will display all traces but 1, 3 and 5)

//  Without any of the keyworded first arguments the following applies
//  Three optional real arguments (arg1 - start trace; arg2 - end trace and arg3 - stepsize)

// The real and string arguments are mutually exclusive

//*** another special case (2021-04-27)
   // one or two arguments
   // one string and one non-array'ed real
   // The string cannot be one of the specials, above
   // some examples below:
   //  dssi(1,'red') will display trace 1 in red
   //  dssi(4) will display trace 4 in dssicolors[4] OR in spectrum color
   //  dssi('red') will display trace 1 red
//*** end of special case

// Use of dssi with a return value is a special case
    if($##>0) then
	dss:$dim
	if($dim>1) then return($dim)
	else return(arraydim) endif
    endif

// if the number of arguments is =1 or 2, check for a special case
    $special=0
    if $#>0 then
	if typeof('$1') then
	    if $1='voup' or
           	$1='vodown' or 
           	$1='vo0' or 
           	$1='select' or 
           	$1='unselect' or 
           	$1='all' then 
                    $special=1 
	    endif
	else
	    if $1=0 then $special=1 endif
	endif
    endif

    if ($# > 0 and $# < 3) and not $special then
	$color='' $inx=0
	if typeof('$1') then
	    $color=$1
	    if $#>1 then
		if not typeof('$2') then
		    $inx=$2
		    if not $inx then $inx=1 endif
		endif
	    else
		$inx=1
	    endif
	else
	    $inx=$1
	    if $#>1 then
		$color=$2 
	    endif
	endif
 		// Exclude an array of $inx
	$size=size('$inx')
	    if $color='' then
	        exists('dssicolors','parameter'):$pex
    		if $pex then $size=size('dssicolors') else $size=0 endif
    		if $size>=$inx then
    		    $color=dssicolors[$inx]
    		endif
	    endif
	    if $color='' then $color='spectrum' endif
	    mspec('clear')
	    write('line3',`mspec(%d,'%s')`,$inx,$color):$cmd
	    exec($cmd)
	    intmod='off' ds
	    return
    endif

// end of special case
	      
// If argument is supplied, interpret it, set appropriate
//   parameter and call dssi again
    if ($# > 0) then
	if typeof('$1') then
	    $vo=vo*0.1
	    if $vo<0.2 then $vo=0.2 endif
	    if ($1='voup') then
		if vo=0 then
			// This is a special case, reset vo to a nominal value
			// based on the number of trace
		    exists('dssitrace','parameter'):$pex
		    if $pex=1 then if dssitrace=0 then $pex=0 endif endif
		    if not $pex then
			readheader(curexp+'/acqfil/fid'):$np,$tdim
		    else
			$tdim=size('dssitrace')
		    endif
		    vo=0.7*wc2max/$tdim
		else
	    	    vo=vo+$vo
		endif
	    	dssi
	    elseif ($1='vodown') then
	        $vo=vo-$vo
	    	if $vo<0 then $vo=0 endif
	    	vo=$vo
	    	dssi
	    elseif ($1='vo0') then
		vo=0
		dssi
	    elseif ($1='all') then
		dssi(0)
	    elseif ($1='select') then
		if $# < 2 then dssi(0)
		else
		    exists('dssitrace','parameter'):$pex
		    if not $pex then
		    	create('dssitrace','real','current',0):$dum
		    endif
		    setgroup('dssitrace','display')
		    dssitrace=0
		    $i=2 $j=1
		    while $i <= $# do
			dssitrace[$j]=${$i}
			$j=size('dssitrace')
			$j=$j+1
			$i=$i+1
		    endwhile
		    dssi
		endif
	    elseif ($1='unselect') then
		if $# < 2 then dssi(0)
		else
                    exists('dssitrace','parameter'):$pex
                    if not $pex then
                        create('dssitrace','real','current',0):$dum
		    endif
                    setgroup('dssitrace','display')
		    getvalue('arraydim','processed'):$dim
		    $i=1 $trace=0
		    repeat
			$trace[$i]=$i
			$i=$i+1
		    until $i > $dim
		    $i=2
		    while $i <= $# do
			$ix=${$i}
			if $ix <= $dim and $ix > 0 then
			    $trace[$ix]=0
			endif
			$i=$i+1
		    endwhile
		    $i=1 $j=1
		    dssitrace=0
		    repeat
			if $trace[$i]>0 then
			    dssitrace[$j]=$trace[$i]
			    $j=$j+1
			endif
			$i=$i+1
		    until $i > $dim
		    dssi
		endif
	    endif
	else
	    getvalue('arraydim','processed'):$dim
	    $start=$1 $end=$dim $step=1
	    if not $start then $end=0
	    else
	        if ($# > 1) then $end=$2 endif
	        if ($# > 2) then $step=$3 endif
	    endif
            exists('dssitrace','parameter'):$pex
            if not $pex then
                create('dssitrace','real','current',0):$dum
            endif
	    setgroup('dssitrace','display')
	    dssitrace=0
	    $x=$start $j=1
	    repeat
		dssitrace[$j]=$x
		$j=$j+1
		$x=$x+$step
	    until $x > $end
	    dssi
	endif
	return
    endif

    bgmode_is:$BG
// While in BG mode don't bother with mspec
    if $BG then ds(1) return endif

// The following is the generic dssi call after the parameters
//   are set appropriately
    mspec('clear')
    $colors=''
    destroy('dssicolors','global'):$dum
    exists('dssicolors','parameter'):$parex
    if $parex then $colors=dssicolors endif
    $colorsize=size('$colors')
    if ($colors[1]='') then $colorsize=0 endif

    if $colorsize then
      exists('colorMap','maclib'):$macex
      if $macex then
          $i=1
          repeat
              colorMap($colors[$i]):$cndx,$colors[$i]
              $i=$i+1
          until $i > $colorsize
      endif
    endif

    $trace=0
    destroy('dssitrace','global'):$dum
    exists('dssitrace','parameter'):$parex
    if $parex then $trace=dssitrace endif
    $tracedim=size('$trace')
    if ($trace[1]=0) then $tracedim=0 endif

// evaluate the total number of real traces and
//  if it exceeds 512, mspec will abort.  
//  if it exceeds 512, call dssn instead for each
//  trace
    if $tracedim>512 then
	$i=1 $realdim=0
	repeat
	    if ($trace[$i]>0.5) then
	    	$realdim=$realdim+1
	    endif
	    $i=$i+1
	until $i > $tracedim
	if $realdim > 512 then
	    write('line3','Warning: %d traces.  Interactive display limited to 512',$realdim)
	    $i=1 $first=1 $vp=vp
	    $vo=vo vo=wc2max/2/$realdim
	    repeat
		if $trace[$i] then
		    $ix=$trace[$i]
		    if not $first then
			if $colorsize >= $ix then
			    dssn($trace[$i],$colors[$ix])
			else
			    dssn($trace[$i])
			endif
		    else
			if $colorsize >= $ix then
			    dss($trace[$i],$colors[$ix])
			else
			    dss($trace[$i])
			endif
			$first=0
		    endif
		    vp=vp+vo
		endif
		$i=$i+1
	    until $i > $tracedim
	    vp=$vp
	    dscale
	    return
	endif
    endif

// Now move on with mspec
// define default colors
    $arg2=''
    if ($colorsize) then
        $x=1
        repeat
            if $arg2='' then
                    write('line3','\'%s\'',$colors[$x]):$arg2
            else
                    write('line3','%s,\'%s\'',$arg2,$colors[$x]):$arg2
            endif
            $x=$x+1
        until $x>$colorsize
    endif

// define traces
// associate traces with its colors
    $arg=''
    if ($tracedim) then
	$arg2=''
	$x=1
	repeat
	    if ($trace[$x]) then
	      if $arg='' then
		write('line3','%d',$trace[$x]):$arg
              else
                write('line3','%s,%d',$arg,$trace[$x]):$arg
	      endif
	      if $colorsize then
	      	if $colorsize >= $trace[$x] then
		    $inx=$trace[$x]
	      	else
		    $inx=$colorsize
	      	endif
	      	if $arg2='' then
		    write('line3','\'%s\'',$colors[$inx]):$arg2
	      	else
		    write('line3','%s,\'%s\'',$arg2,$colors[$inx]):$arg2
	      	endif
	      endif
	    endif
	    $x=$x+1
	until $x>$tracedim
    endif

// execute mspec
    $cmd='mspec('
    if ($arg='') then
	$cmd=$cmd+'\'all\''
    endif
    if ($arg2='') then
	$cmd=$cmd+$arg
    else
	$cmd=$cmd+$arg+','+$arg2
    endif
    $cmd=$cmd+')'
    exec($cmd):$dum2
    intmod='off' ds
