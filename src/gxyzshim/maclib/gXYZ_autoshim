"macro gXYZ_autoshim"

werr='return'
//gxyzsetlocalpars
create('execprocess','string','current',''):$dum
create('execplot','string','current',''):$dum
setgroup('execprocess','display')
setgroup('execplot','display')
$macdir=archivedir+'/'+sample+'/dirinfo/macdir'
$alias=$macdir+'/alias'

$function=''
psCmd('active'):$psActive
if ($#<0.5) then
	write('line3','Using mapname %s',mapname)
	{$0}(ncycles)
else
        if (not typeof('$1')) then
	    $function='init' $ncycles=$1
	    write('line3','Using %d cycles of shimming',$ncycles)
	else
	    $function=$1
	endif
endif

if $function='error' then
        write('line3','invalid argument supplied to gxyzautoshim')
        abort
endif
 
"***********************************************************************"
if ($function = 'init') then

    $gxyzmappar=archivedir+'/'+sample+'/dirinfo/parlib/gxyzmap.par'
    $do1Dmap='n'
    exists($gxyzmappar,'directory'):$direx
    if not $direx then
       write('error','gxyzmap.par not found')
       abort
    endif
    rtp($gxyzmappar)
    //Do I need this next call???
    //gxyzsetlocalpars
    write('line3','Using 3D maps %s',mapname)
   
   if ($psActive=1) then
     psCmd('set','errors','all')
   else
     werr=''
   endif
   exists('wxyzshim','parameter'):$e
   if ($e<0.5) then
	   create('wxyzshim','string')
   endif
   "if ($#>1) then wxyzshim=$2 endif"
   
   exists('varytau','parameter'):$e
   if ($e<0.5) then
	   create('varytau','flag')
	   varytau='y'
   endif
   exists('ncycles','parameter'):$e
   if ($e<0.5) then
	   create('ncycles','real')
   endif
   ncycles=$ncycles
   exists('cycle','parameter'):$e
   if ($e<0.5) then
	   create('cycle','real')
   endif
   cycle=1
   ni=4 ni2=4
   length(gxyzcode):$codelength
   if $codelength=36 then
	   ni=6 ni2=6
   elseif $codelength=64 then
	   ni=8 ni2=8
   elseif $codelength=144 then
	   ni=12 ni2=12
   endif
   gxyzfitgroup = '3rd'
   gxyzfitshims
   $gxyzcalibdir=userdir+'/3Dshimlib/calibs'
   exists($gxyzcalibdir,'file'):$e
   if $e < 0.5 then shell('mkdir '+$gxyzcalibdir):$dum endif
   $date=''
   shell('date +%d%h%Y'):$date
   $gxyzautoshimdir=$gxyzcalibdir+'/'+mapname
   exists($gxyzautoshimdir,'file'):$e
   if $e < 0.5 then shell('mkdir '+$gxyzautoshimdir):$dum endif
   create('GradTypeOverride','string','current',''):$dum
   GradTypeOverride='aa'
   auargs('gradtype_aa')
   {$0}('shim','auto')
endif

"end of initial call of gxyzautoshim(N) or gxyzautoshim(N,'macro')"
"***********************************************************************"
 

"***********************************************************************"
if ($function='shim') then

   "gxyzautoshim('shim'):  do one cycle of gradient shimming"
   exists(userdir+'/shims/tmpgxyzautoshim','file'):$e
   if $e > 0.5 then shell('rm '+userdir+'/shims/tmpgxyzautoshim'):$dum endif
   readallshims
   svs('tmpgxyzautoshim'):$dum
   pad=15
   if (auto<>'y') then
     wnt='wft(`acq`,1,celem) f full dsshall gxyztext(`shimming`)'
   else
     wnt='gxyztext(`shimming`)'
   endif
   clear(2)
   caliblist=$0
   xmhaha_calib('synonym',$0):caliblist
   calibnode='_autoshim'
   create('GradTypeOverride','string','current',''):$dum
   GradTypeOverride='aa'
   auargs('gradtype_aa')
   //xmhaha_calib('addNode',$0+'(\'processmap\')','next',120)
   gxyztext('shimming')
   execprocess=$0+'(\'processmap\',\'auto\')' 
   execplot='execplot=\'\''
   return
endif "from call of gxyzautoshim('shim') "
"***********************************************************************"


"***********************************************************************"
if ($function='processmap') then

   execprocess='return'
   "Read the shims from before processing"
   rts('tmpgxyzautoshim'):$dum
   $x1=x1 $y1=y1
   shell('sleep 1'):$dum
   readhw('x1'):$x1
   readhw('y1'):$y1
   
   setvalue('x1',$x1,'processed')
   setvalue('y1',$y1,'processed')
   shell('sleep 1'):$dum
   wft
   gxyzmapfield('all')
   $date='' $rmserrs=''
   shell('date +%d%h%Y'):$date
   text('Probe name: '+probe)
   atext('Field map '+$date)
   lookup('file',userdir+'/3Dshimlib/data/xyshim.list')
   lookup('seek','rmsdiff/rmserr','read'):$rmserrs
   atext('rms diff/rms err = '+$rmserrs)
   $cy=''
   $ncy=''
   on('temp'):$tempon
   if $tempon > 0.5 then $temp=temp endif
   format(cycle,2,0):$cy
   format(ncycles,2,0):$ncy
   atext('Cycle number '+$cy+' out of '+$ncy)
   atext('')

   $shimpower='NA'
   if (shimset<10 or shimset>11) then
           format(r2,1,1):$shimpower
	   atext('Estimated shim power dissipation = '+$shimpower+' W')
   endif
   if plotter='none' then pltopt='n' endif
   if pltopt='y' or pltopt='p' then
	   pltext
	   plot3Dmap(userdir+'/3Dshimlib/data/xyshim.fit',1,2) 
           if pltopt='p' then
		$n='' format(gxyzshimPSn,'lower'):$n
                page(gxyzshimPS+$n+'.ps')
                gxyzshimPSn=gxyzshimPSn+1
                format(gxyzshimPSn,'lower'):$n
                printon:$dum text dgs printoff(gxyzshimPS+$n+'.txt'):$dum
                getplottertype(printer,'papersize'):$papersize
                strstr($papersize,'-'):$ret,$dum,$papertype
                if ($papertype='letter') then $papertype='Letter' else format($papertype,'upper'):$papertype endif
                shell('enscript -M '+$papertype+' -o '+gxyzshimPS+$n+'.ps '+gxyzshimPS+$n+'.txt &'):$dum
                gxyzshimPSn=gxyzshimPSn+1
           else
                page
		printon:$dum text dgs printoff:$dum
           endif
   endif

   $i=1 $is=''
   $fieldmapex=1
   $gxyzcalibdir=userdir+'/3Dshimlib/calibs'
   $gxyzautoshimdir=$gxyzcalibdir+'/'+mapname
   exists($gxyzautoshimdir,'file'):$e
   if $e < 0.5 then shell('mkdir '+$gxyzautoshimdir) endif
   
   repeat
           format($i,1,0):$is
           $fieldmapname=$gxyzautoshimdir+'/fieldmap_'+$date+'_'+$is+'.fid'
           exists($fieldmapname,'file'):$fieldmapex
        if $fieldmapex>0.5 then $i=$i+1 endif
   until $fieldmapex<0.5

   "Now save the fieldmap"
   svf($fieldmapname)
   n2=$fieldmapname

   shell('sleep 1'):$dum
   
   $inputfile='' $max=-1e10 $min=1e10 $trash='' $num='' $values='' $value=0 $numlines=0 $amax=0 $amin=0
   $inputfile=userdir+'/3Dshimlib/data/fieldmap'
   shell('wc -l < '+$inputfile+'; cat'):$numlines
   exists($inputfile,'file'):$ex
   if $ex<0.5 then write('error','No fieldmap!') abort endif
   lookup('file',$inputfile)
   lookup('readline',5):$trash
   $i=1
   repeat
	   lookup('read','read'):$num,$values
	   format($values,3,6):$value
	   if $value>$max then $max=$value endif
	   if $value<$min then $min=$value endif
	   $i=$i+1
   until $i > $numlines-5
   $range=$max-$min
   write('line3','Field variation %.1f %s Hz',$range,tn)
   $tauopt=1/$range
   $taumax=0.025*h1freq/sfrq
   if (tn='lk' or tn='H2') then $taumax=0.12 endif
   //write('line3','$taumax=%f',$taumax)
   if $tauopt>$taumax then $tauopt=$taumax endif
   format($tauopt,1,4):$tauopt
   //write('line3','$tauopt=%f',$tauopt)
   r3=$tauopt

   "Write range and rms diff/err info to log file"
   shell('date +%d%h%Y'):$date
   $mapname=''
   $xyshimlist=userdir+'/3Dshimlib/data/xyshim.list'
   lookup('file',$xyshimlist)
   lookup('seek','mapname','read'):$mapname
   lookup('seek','nshims','read'):$nfitshims
   $gxyzautoshimdir=userdir+'/3Dshimlib/calibs/'+$mapname
   $shimlogfile=$gxyzautoshimdir+'/shimlog.txt'
   $date=''
   exists($shimlogfile,'file'):$logex
   if $logex < 0.5 then
           write('reset',$shimlogfile)
           write('file',$shimlogfile,'Mapname: %s',$mapname)
           write('file',$shimlogfile,'Ref.\t\tField\t\trms diff/rms err\tNo. shims\tShim power\tData')
	   write('file',$shimlogfile,'Linewidth\(Hz\)\tVariation\(Hz\)\t\t\t\tFitted\t\tDissipation\(W\)\tFilename')
           "Now create a symbolic link to this file"
           $slink=userdir+'/3Dshimlib/calibs/shimlog.txt'
           exists($slink,'file'):$slinkex
           if $slinkex > 0.5 then shell('rm '+$slink):$dum endif
           shell('ln -s '+$shimlogfile+' '+$slink):$dum
   endif
   length($gxyzautoshimdir):$strstrt
   length($fieldmapname):$strstrt2
   substr($fieldmapname,$strstrt+2,$strstrt2-$strstrt-1):$fieldmapname

   //Now write out a shimset if the shimming appears to have improved
   $lastrange=1e6
   if ($logex > 0.5) then
	shell('wc -l  <'+$shimlogfile+'; cat'):$numlinesinlog
	lookup('file',$shimlogfile)
	$i=1
	repeat
        	lookup('seekcs','NA','read'):$tmp
		if ($tmp < $lastrange) then $lastrange=$tmp endif
       		$i=$i+1
	until $i=$numlinesinlog-2
   endif
   if (($range < $lastrange) or ($logex<0.5)) then
	$shimfilename=mapname+'_best'
	write('line3','Shims saved as %s',$shimfilename)
	svs($shimfilename,'overwrite')
   endif
	
   //Write the log file		
   write('file',$shimlogfile,'NA\t\t%.2f\t\t%s\t\t\t%d\t\t%s\t\t%s',$range,$rmserrs,$nfitshims,$shimpower,$fieldmapname)

   "Now acquire the next field map"
   {$0}('nextfieldmap','auto'):$ret
   return


endif "gxyzautoshim('processmap')"
"***********************************************************************"


"***********************************************************************"
if ($function='nextfieldmap') then
	$tauopt=r3
	cycle=cycle+1
	$listfile=userdir+'/3Dshimlib/data/xyshim.list'
	lookup('file',$listfile)
	lookup('seek','rmsdiff/rmserr')
	lookup('read'):$rmserr
	$shimlogfile=userdir+'/3Dshimlib/calibs/shimlog.txt'
	exists($shimlogfile,'file'):$ex
	$fileline=0 $filelines=''
	shell('(cat '+$shimlogfile+' | wc -l)'):$fileline
	lookup('file',$shimlogfile)
	$junk='' $line=''
	$i=1
	repeat
        	lookup('readline'):$junk
        	$i=$i+1
	until $i>=$fileline
	lookup('skip','read'):$last_field_var
	//write('line3','Last field variation=%f',$last_field_var)

	//Write the observe nucleus and last field variation to a temp file
	//Why? To help with decision making about 1D gradient shim parameters
	//for next iteration of 1D shimming
	//See gxyzautocalib('gmap1Dinit') for references to this file
	$tmpvarfile=userdir+'/3Dshimlib/calibs/lastvar'
	write('reset',$tmpvarfile)
	write('file',$tmpvarfile,'%s %f',tn,$last_field_var)

	gxyzrmserr_coarse=2
	gxyzrmserr_fine=1
	gxyzcalibset('check','pw90'):$dopw90
	gxyzcalibset('check','1Dautoshim'):$do1Dautoshim
	gxyzcalibset('check','xycalib'):$doxycalib

	if (gxyzfitgroup='all') then $errval=gxyzrmserr_fine else $errval=gxyzrmserr_coarse endif "Relax convergence criterion when not using all shims under full autocalibration"
	
	if ((cycle<=ncycles) and ($rmserr > $errval) and ($last_field_var >  gxyzfieldvar)) then "Designated number of shim cycles not reached and rms error > errval - continue autoshimming"

		n1='p'     "Continue plotting into PS files if pltopt='p'"
		if (varytau='y') then
			write('line3','tau[2] set to %.3f s, continuing...',$tauopt)
			tau=0,$tauopt
		else
			write('line3','tau[2] held at %.3f s, continuing...',tau[2])
		endif
   		xmhaha_calib('addNode',$0+'(\'shim\')','next',120)
                return
	else "Designated number of cycles reached or rms error < errval"
		if (($rmserr <= $errval) or ($last_field_var <= gxyzfieldvar)) then "Shimming converged, but are we using all the shim maps?"
			if (gxyzfitgroup <> 'all') then "Not all shims being used yet"
				n1='p'     "Continue plotting into PS files if pltopt='p'"
				if (gxyzfitgroup <> gxyzshimgroup) then "If in full auto mode then if not all currently available shim maps used -> increase number of shims to fit and continue autoshimming"
					if (shimset=9 or shimset=14 or shimset=26) then
					   if gxyzfitgroup='3rd' then gxyzfitgroup='4th'
					   //elseif gxyzfitgroup='4th' then gxyzfitgroup='5th'
					   else gxyzfitgroup='all'
					   endif
					else
					   gxyzfitgroup = 'all'
					endif
					gxyzfitshims
					cycle=1 "Reset cycle to allow more iterations"
   					xmhaha_calib('addNode',$0+'(\'shim\')','next',120)
                                        return
				else 	"All of the shims that have been mapped have been used for fitting"
					 "and the shimming has converged, but not all shims are being utilised yet -> repeat everything "
					if (gxyzautorepeat='y') then
						if ($rmserr<$errval) then
							write('line3','rms diff/rms err < %.1f, shimming converged!',$errval)
						else
							write('line3','rms diff/rms err = %.1f, field variation = %.1f',$rmserr,$last_field_var)
						endif
						write('line3','Automated shimming completed, repeating procedure...')					
						$nodes='' $done=0
						if ($do1Dautoshim) then $nodes=$nodes,'gXYZ_gmap1Dinit' endif
						if ($doxycalib) then $nodes=$nodes,'gXYZ_xycalib' endif
						$nodes=$nodes,'gXYZ_mkmap'
  						$alias=$macdir+'/alias'
						getsampglobal($macdir)
						array2array($nodes):$nodes
						$tot=size('$nodes')
						if $nodes[1]='' then $tot=0 endif
						$i=1 $t=120
						if ($tot) then
          					   repeat
            					      $r=0 $n=''
            					      write('line3','Adding node %s to SQ',$nodes[$i])
            					      walkupQ_runtime('add',$nodes[$i],'last','no',$t,'nocheck','quiet'):$r,$n
            					      if ($r) then
                				      	write('file',$alias,'%s: %s',$n,$nodes[$i])
                					write('file',$macdir+'/'+$n+'acq','wshim=\'n\' alock=\'n\' wtune=\'n\'')
            					      endif
            					      $i=$i+1
          					   until $i > $tot
        					endif
						return

					else "Just stop when convergence criterion reached"
						$str='' $str1='' $str2=''
						if ($rmserr<$errval) then
							write('line3','rms diff/rms err < %.1f, shimming converged!',$errval)
							write('line3','rms diff/rms err < %.1f, shimming converged!',$errval):$str1
							write('line3','rms diff/rms err = %.1f, field variation = %.1f',$rmserr,$last_field_var)
							write('line3','rms diff/rms err = %.1f, field variation = %.1f',$rmserr,$last_field_var):$str2
						else
							write('line3','rms diff/rms err = %.1f, field variation = %.1f',$rmserr,$last_field_var)
							write('line3','rms diff/rms err = %.1f, field variation = %.1f',$rmserr,$last_field_var):$str
						endif
						is_IP:$isIP
						if ($isIP) then
						    readallshims
						    $shimpower=0
						    gxyzshimpwr:$shimpower
						    VPtext
						    write('line3','Full 3D shim optimization'):$str
						    atext($str)
						    atext($str1)
						    atext($str2)
						    VPgettarget('shim3D'):$vartarget,$pwrtarget
						    VPhistory($last_field_var,$shimpower)
						    //Add 3D map name to probe file
						    gxyzupdateprobe
						endif
						banner('Shimming completed!')
						if (gxyzmake1Dmap='y') then
							banner('Now making final 1D gradient shim map and setting name into probe file')
            						walkupQ_runtime('add','gXYZ_gmap1Dfinal','last','no',120,'nocheck','quiet'):$r,$n
            						if ($r) then
                						write('file',$alias,'%s: gXYZ_gmap1Dfinal',$n)
                						write('file',$macdir+'/'+$n+'acq','wshim=\'n\' alock=\'n\' wtune=\'n\'')
            						endif
						endif
					endif
                                       	return
				endif
			else "rms error less than errval with all shims - stop now"
				if ($rmserr <= $errval) then
					write('line3','rms diff/rms err < %.1f, shimming converged, field variation = %.1f Hz',$errval,$last_field_var)
					write('line3','rms diff/rms err < %.1f, shimming converged, field variation = %.1f Hz',$errval,$last_field_var):$str1
				else
					write('line3','rms diff/rms err = %.1f, field variation = %.1f Hz',$rmserr,$last_field_var)
					write('line3','rms diff/rms err = %.1f, field variation = %.1f Hz',$rmserr,$last_field_var):$str1
				endif
				$finalshims='gxyzshim_'+probe+'_last'
				svs($finalshims,'overwrite')
				write('line3','Shimming converged - shims stored as %s',$finalshims)
				if pltopt='p' then gxyzshimPDF endif
				banner('Shimming converged!')
				
				//Just stop if not in full autocalibration mode
				//if ($#<1.5) then return endif

                                if ((gxyzmake1Dmap<>'y') and (gxyzautorepeat='y')) then
					if (gxyzremap='y') then
						write('line3','Automated shimming completed, repeating procedure...')
 						$nodes='' $done=0
                                                if ($do1Dautoshim) then $nodes=$nodes,'gXYZ_gmap1Dinit' endif
                                                if ($doxycalib) then $nodes=$nodes,'gXYZ_xycalib' endif
                                                $nodes=$nodes,'gXYZ_mkmap'
                                                $alias=$macdir+'/alias'
                                                getsampglobal($macdir)
                                                array2array($nodes):$nodes
                                                $tot=size('$nodes')
                                                if $nodes[1]='' then $tot=0 endif
                                                $i=1 $t=120
                                                if ($tot) then
                                                   repeat
                                                      $r=0 $n=''
                                                      write('line3','Adding node %s to SQ',$nodes[$i])
                                                      walkupQ_runtime('add',$nodes[$i],'last','no',$t,'nocheck','quiet'):$r,$n
                                                      if ($r) then
                                                        write('file',$alias,'%s: %s',$n,$nodes[$i])
                                                        write('file',$macdir+'/'+$n+'acq','wshim=\'n\' alock=\'n\' wtune=\'n\'')
                                                      endif
                                                      $i=$i+1
                                                   until $i > $tot
                                                endif
					endif
                                        return
                                else
					is_IP:$isIP
					if ($isIP) then
					    readallshims
					    $shimpower=0
					    gxyzshimpwr:$shimpower
					    VPtext
					    write('line3','Full 3D shim optimization'):$str
					    atext($str)
					    atext($str1)
					    VPgettarget('shim3D'):$vartarget,$pwrtarget
					    VPhistory($last_field_var,$shimpower)
					    //Add 3D map name to probe file
					    gxyzupdateprobe
					endif
                                 	if ((gxyzmake1Dmap='y') and (gxyzremap<>'y')) then
                                        	banner('Now making final 1D gradient shim map and setting name into probe file')
            					walkupQ_runtime('add','gXYZ_gmap1Dfinal','last','no',120,'nocheck','quiet'):$r,$n
            					if ($r) then
                					write('file',$alias,'%s: gXYZ_gmap1Dfinal',$n)
                					write('file',$macdir+'/'+$n+'acq','wshim=\'n\' alock=\'n\' wtune=\'n\'')
            					endif
						return
                                        elseif (gxyzremap='y') then
						write('line3','Automated shimming completed, repeating procedure...')
 						$nodes='' $done=0
                                                if ($do1Dautoshim) then $nodes=$nodes,'gXYZ_gmap1Dinit' endif
                                                if ($doxycalib) then $nodes=$nodes,'gXYZ_xycalib' endif
                                                $nodes=$nodes,'gXYZ_mkmap'
                                                $alias=$macdir+'/alias'
                                                getsampglobal($macdir)
                                                array2array($nodes):$nodes
                                                $tot=size('$nodes')
                                                if $nodes[1]='' then $tot=0 endif
                                                $i=1 $t=120
                                                if ($tot) then
                                                   repeat
                                                      $r=0 $n=''
                                                      write('line3','Adding node %s to SQ',$nodes[$i])
                                                      walkupQ_runtime('add',$nodes[$i],'last','no',$t,'nocheck','quiet'):$r,$n
                                                      if ($r) then
                                                        write('file',$alias,'%s: %s',$n,$nodes[$i])
                                                        write('file',$macdir+'/'+$n+'acq','wshim=\'n\' alock=\'n\' wtune=\'n\'')
                                                      endif
                                                      $i=$i+1
                                                   until $i > $tot
                                                endif
                                                return
					else
                                                return
                                        endif
                                endif
				return
			endif
		else "Shimming not converged and maximum number of iterations reached"
			if (gxyzautorepeat='y') then
				write('line3','Automated shimming completed, repeating procedure...')
				n1='p'     "Continue plotting into PS files if pltopt='p'"
				 $nodes='' $done=0
                                 if ($do1Dautoshim) then $nodes=$nodes,'gXYZ_gmap1Dinit' endif
                                 if ($doxycalib) then $nodes=$nodes,'gXYZ_xycalib' endif
                                 $nodes=$nodes,'gXYZ_mkmap'
                                 $alias=$macdir+'/alias'
                                 getsampglobal($macdir)
                                 array2array($nodes):$nodes
                                 $tot=size('$nodes')
                                 if $nodes[1]='' then $tot=0 endif
                                 $i=1 $t=120
                                 if ($tot) then
                                    repeat
                                       $r=0 $n=''
                                       write('line3','Adding node %s to SQ',$nodes[$i])
                                       walkupQ_runtime('add',$nodes[$i],'last','no',$t,'nocheck','quiet'):$r,$n
                                       if ($r) then
                                         write('file',$alias,'%s: %s',$n,$nodes[$i])
                                         write('file',$macdir+'/'+$n+'acq','wshim=\'n\' alock=\'n\' wtune=\'n\'')
                                       endif
                                       $i=$i+1
                                    until $i > $tot
                                 endif
                                 return
			else
				write('line3','Automated shimming completed, final rms diff/rms err = %.2f',$rmserr)
				write('line3','Automated shimming completed, final rms diff/rms err = %.2f',$rmserr):$str1
				is_IP:$isIP
				if ($isIP) then
				    readallshims
				    $shimpower=0
				    gxyzshimpwr:$shimpower
				    VPtext
				    write('line3','Full 3D shim optimization'):$str
				    atext($str)
				    atext($str1)
				    VPgettarget('shim3D'):$vartarget,$pwrtarget
				    VPhistory($last_field_var,$shimpower)
				    //Add 3D map name to probe file
				    gxyzupdateprobe
				endif
                                if (gxyzmake1Dmap='y') then
                                       	banner('Now making final 1D gradient shim map and setting name into probe file')
                                       	write('line3','Now making final 1D gradient shim map and setting name into probe file')
            				walkupQ_runtime('add','gXYZ_gmap1Dfinal','last','no',120,'nocheck','quiet'):$r,$n
            				if ($r) then
                				write('file',$alias,'%s: gXYZ_gmap1Dfinal',$n)
                				write('file',$macdir+'/'+$n+'acq','wshim=\'n\' alock=\'n\' wtune=\'n\'')
            				endif
					return
                                else
                                        return
                                endif
			endif
			return
		endif
	endif
	 
endif  "gxyzautoshim('nextfieldmap') "
"***********************************************************************"
