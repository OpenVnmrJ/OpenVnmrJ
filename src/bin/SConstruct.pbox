import os
import sys

platform = sys.platform        # sys.platform -> 'linux2' linux, 'interix6' win7 SUA

# we need to specify an absolute path so this SConstruct file
# can be called from any other SConstruct file
cwd = os.getcwd()

# source files
pboxFileList = ['Pbox.c',
                'Pxfid.c',
                'Pxsim.c',
                'Pxspy.c']

# build environments
env = Environment(CCFLAGS = '-O2 -Wall -m64',
                  LINKFLAGS = '-m64')

if ('darwin' in platform):
    env.Replace(CC = 'clang')
    env.Replace(CCFLAGS = '-Os -Wall -arch x86_64 -Wno-implicit-function-declaration ')
    osxflags = os.getenv('osxflags')
    if osxflags:
       env.Append(CCFLAGS = os.getenv('osxflags'))
    env.Replace(LINKFLAGS = ' -arch x86_64')

# define with absolute path where built files will be copied
vnmrInstallPath = os.path.join(cwd, os.pardir, os.pardir, os.pardir, 'vnmr', 'bin')

# make sure the path(s) exist
if not os.path.exists(vnmrInstallPath):
   os.makedirs(vnmrInstallPath)

# actual builds
for i in pboxFileList:
   (filename, ext) = os.path.splitext(i)
   tempBuildObj = env.Program(target = filename,
                              source = i,
                              LIBS   = ['m'])

   env.AddPostAction(tempBuildObj,
                     Action(Copy(vnmrInstallPath, os.path.join(cwd, filename))))

