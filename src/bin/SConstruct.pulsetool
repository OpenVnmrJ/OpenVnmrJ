import os
import sys

platform = sys.platform

# targets
pulsetoolTarget = 'pulsetool'
pulsechildTarget = 'pulsechild'

# we need to specify an absolute path so this SConstruct file
# can be called from any other SConstruct file
cwd = os.getcwd()

# source files
pulseToolFileList = ['pulsetool.c',
                     'pulsetool_mf.c']

pulseChildFileList = ['pulsechild.c',
                      'pulsechild_mf.c']

# build environments
pulseEnv = Environment(CCFLAGS    = '-O2',
                       CPPDEFINES = ['LINUX'],
                       LIBS    = ['Xm', 'Xt', 'X11', 'm'])

if ('darwin' in platform):
     pulseEnv.Replace(CC = 'clang')
     pulseEnv.Replace(CCFLAGS = '-Wall -Os -arch x86_64 -I/opt/X11/include -I/Applications/OpenMotif21/include ')
     pulseEnv.Replace(LINKFLAGS = '-Wall -Os -arch x86_64 -L/opt/X11/lib -L/Applications/OpenMotif21/lib ')
     pulseEnv.Replace(LIBS = ['Xm', 'Xext', 'Xt', 'Xp', 'X11', 'm'])


# actual builds
pulsetool = pulseEnv.Program(target  = pulsetoolTarget,
                             source  = [pulseToolFileList])

pulsechild = pulseEnv.Program(target  = pulsechildTarget,
                              source  = [pulseChildFileList])

# define with absolute path where built files will be copied
vnmrInstallPath = os.path.join(cwd, os.pardir, os.pardir, os.pardir, 'vnmr', 'bin')

# make sure the path(s) exist
if not os.path.exists(vnmrInstallPath):
   os.makedirs(vnmrInstallPath)

pulseEnv.AddPostAction(pulsetool,
                       Action(Copy(vnmrInstallPath, os.path.join(cwd, pulsetoolTarget))))

pulseEnv.AddPostAction(pulsechild,
                       Action(Copy(vnmrInstallPath, os.path.join(cwd, pulsechildTarget))))

