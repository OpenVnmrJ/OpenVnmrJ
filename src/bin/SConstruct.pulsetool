import os
import sys

platform = sys.platform

# targets
pulsetoolTarget = 'pulsetool'
pulsechildTarget = 'pulsechild'

# we need to specify an absolute path so this SConstruct file
# can be called from any other SConstruct file
cwd = os.getcwd()

# source files
pulseToolFileList = ['pulsetool.c',
                     'pulsetool_mf.c']

pulseChildFileList = ['pulsechild.c',
                      'pulsechild_mf.c']

# build environments
pulseEnv = Environment(CCFLAGS    = '-O2 -Wno-incompatible-pointer-types -Wno-implicit-function-declaration -Wno-implicit-int -Wno-int-conversion',
                       CPPDEFINES = ['LINUX'],
                       LIBS    = ['Xm', 'Xt', 'X11', 'm'])

if ('darwin' in platform):
     pulseEnv.Replace(CC = 'clang')
     pulseEnv.Replace(CCFLAGS = '-Wall -Os -Wno-implicit-function-declaration -mmacosx-version-min=10.13 -Wno-error=implicit-function-declaration -Wno-error=implicit-int -Wno-error=incompatible-function-pointer-types -Wno-error=int-conversion')
     osxflags = os.getenv('osxflags')
     if osxflags:
        pulseEnv.Append(CCFLAGS = os.getenv('osxflags'))
     pulseEnv.Replace(LINKFLAGS = '-Wall -Os ')
     if os.path.exists('/opt/homebrew'):
        pulseEnv.Append(CCFLAGS = ' -I/opt/homebrew/include')
        pulseEnv.Append(LINKFLAGS = ' -L/opt/homebrew/lib')
     else:
        pulseEnv.Append(CCFLAGS = ' -I/usr/local/include')
        pulseEnv.Append(LINKFLAGS = ' -L/usr/local/lib')


# actual builds
pulsetool = pulseEnv.Program(target  = pulsetoolTarget,
                             source  = [pulseToolFileList])

pulsechild = pulseEnv.Program(target  = pulsechildTarget,
                              source  = [pulseChildFileList])

# define with absolute path where built files will be copied
vnmrInstallPath = os.path.join(cwd, os.pardir, os.pardir, os.pardir, 'vnmr', 'bin')

# make sure the path(s) exist
if not os.path.exists(vnmrInstallPath):
   os.makedirs(vnmrInstallPath)

pulseEnv.AddPostAction(pulsetool,
                       Action(Copy(vnmrInstallPath, os.path.join(cwd, pulsetoolTarget))))

pulseEnv.AddPostAction(pulsechild,
                       Action(Copy(vnmrInstallPath, os.path.join(cwd, pulsechildTarget))))

