"macro BP_PR2"
" PR2  - Projection-Reconstruction macro, Eriks Kupce, 21.07.2005 "

cd(PRdir+'/'+PRexp[PRn]) 
BP_PRproc1('ni')

BPsvf('f1')

"find location of program"
BPfindfile('pr3db'):$path1  

"make bin direcory if needed"
exists(userdir+'/../bin','file'):$e
if $e=0 then mkdir(userdir+'/../bin') endif

"copy program from appdir/bin or /vnmr/bin if not there" 
if $path1<>userdir+'/../bin/pr3db' then
 cp($path1,userdir+'/../bin')  
endif

shell('pr3db F1.pk F2.pk 0.5')

"remove program if already in appdir/bin or /vnmr/bin"
if $path1<>userdir+'/../bin/pr3db' then
 rm(userdir+'/../bin/pr3db') 
endif

shell('sleep 2')
if(PRbpa[PRn]='y') or (PRbpa[PRn]='yy') then
  $file=''
  pwd:$file
  exists($file+'/PR.bpa','file'):$e
  if($e) then
    BP_PRgetbpa($file+'/PR.bpa'):pra
  else
    write('file','../PRmessages','PR2: failed to open PR.bpa file')
    write('error','PR2: failed to open PR.bpa file')
    return
  endif
else
  if(PRbpa[PRn]='r') then            " regular "
    $prainc = 90.0/(PRnpr[PRn] + 1)
  else
    if(PRsw1[PRn] > PRsw2[PRn]) then
      pra = 0.0
      atan(PRsw2[PRn]/PRsw1[PRn]):$prx
    else
      pra = 90.0
      atan(-PRsw1[PRn]/PRsw2[PRn]):$prx
    endif
    $prx = $prx*180.0/3.14159
    $prainc = 2.0*$prx/(PRnpr[PRn] + 1)
    pra = pra + $prainc    
    if(PRvarsw1 = 'y') then
      atan(PRsw2[PRn]/PRsw1[PRn]):$prx
      $prx=$prx*180/3.14159
      if(pra > $prx) then
        cos(pra*3.14159/180.0):$cf
        sw1 = PRsw1[PRn]/$cf
      else
        sin(pra*3.14159/180.0):$cf
        sw1 = PRsw2[PRn]/$cf
      endif    
    endif
  endif
endif

ni=PRniTilt[PRn]
phase=1,2 phase2=1,2 ni2=0
wexp='BP_PR3'
au

